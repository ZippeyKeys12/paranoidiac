class RolfBellmer : PlayerPawn
{
	override void CheckCrouch(bool totallyfrozen)
	{
		//Prevent crouching when in disguise
		if(!CountInv("ScientistDisguise"))
			super.CheckCrouch(totallyfrozen);
	}
	Default
	{
		+NOSKIN
		Height 60;
		Player.DisplayName "Rolf Bellmer";
		Player.ViewHeight 56;
		Player.AttackZOffset 24;
		Player.WeaponSlot 1,"Crowbar";
		Player.WeaponSlot 2,"Beretta","Python";
		Player.WeaponSlot 3,"HornetGun","PShotgun";
		Player.WeaponSlot 4,"Xbow","ARifle";
		Player.WeaponSlot 5,"RPGLauncher";
		Player.WeaponSlot 6,"GluonGun";
		Player.StartItem "Crowbar";
		Player.StartItem "HEVSuit";
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_JumpIf(CountInv("ScientistDisguise"),"Spawn2");
		HLI2 AB 2 A_JumpIf(CountInv("ScientistDisguise"),"Spawn2");
		Loop;
	See:
		HLR1 A 1 A_JumpIf(CountInv("ScientistDisguise"),"See2");
		HLR1 BCDEFGHIJKLMNOPQRSTUVW 1;
		Loop;
	Missile:
		HLA1 Y 1 A_JumpIf(CountInv("ScientistDisguise"),"Missile2");
		HLA1 Z[\]FGHI 1;
		HLA2 AB 1;
		HLA1 V 2;
		Goto Spawn;
	Pain:
		HLP1 A 0 A_JumpIf(CountInv("ScientistDisguise"),"Pain2");
		HLP1 A 1 A_Pain;
		HLP1 DGJMPSU 1;
		Goto Spawn;
	Death:
		HLD1 A 0 A_JumpIf(CountInv("ScientistDisguise"),"Death2");
		HLD1 A 2
		{
			A_PlaySound("HLPlayerHEVFail",CHAN_VOICE);
			A_NoBlocking();
		}
		HLD1 BCDEFG 2;
		HLD1 H 2 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 IJKL 2;
		HLD1 M -1;
		Stop;
	Spawn2:
		HLI2 CCCCCDDDDDEEEEEFFFFFGGGGGHHHHHIIIIIJJJJJKKKKKLLLLLMMMMMNNNNNOOOOOPPPPPQQQQQ 1 A_JumpIf(vel.x!=0||vel.y!=0,"See");
		Goto Spawn;
	See2:
		HLW2 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 A_JumpIf(vel.x==0&&vel.y==0,"Spawn");
		Goto See;
	Missile2:
		HLI2 C 12;
		Goto Spawn;
	Pain2:
		HLP2 A 1 A_PlaySound("HLPlayerScientistPain",CHAN_VOICE);
		HLP2 BCDEFGHI 1;
		Goto Spawn;
	Death2:
		HLD2 A 1 A_PlaySound("HLPlayerScientistDeath",CHAN_VOICE);
		HLD2 BC 1;
		HLD2 D 1 A_NoBlocking;
		HLD2 EFGHIJKLMNOPQRS 1;
		HLD2 T 1 A_PlaySound("halflife/weapons/bodydrop");
		HLD2 UVW 1;
		HLD2 X -1;
		Stop;
	}
}
class ScientistDisguise : Inventory
	{Default{+INVENTORY.UNDROPPABLE}}