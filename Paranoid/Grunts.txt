//====================================
// Government Troops: Random Selector
//====================================

class HLGruntAny : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1";
		DropItem "HLGrunt2";
		DropItem "HLGrunt3";
		DropItem "HLGrunt4";
		DropItem "HLGrunt5";
		DropItem "HLGrunt6";
	}
}

class HLGruntM16 : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1";
		DropItem "HLGrunt2";
		DropItem "HLGrunt3";
	}
}

class HLGruntShotgun : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt4";
		DropItem "HLGrunt5";
		DropItem "HLGrunt6";
	}
}

class HLGruntNoGrenadesAny : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1NoGren";
		DropItem "HLGrunt2NoGren";
		DropItem "HLGrunt3NoGren";
		DropItem "HLGrunt4NoGren";
		DropItem "HLGrunt5NoGren";
		DropItem "HLGrunt6NoGren";
	}
}

class HLGruntM16NoGrenades : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1NoGren";
		DropItem "HLGrunt2NoGren";
		DropItem "HLGrunt3NoGren";
	}
}

class HLGruntShotgunNoGrenade : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt4NoGren";
		DropItem "HLGrunt5NoGren";
		DropItem "HLGrunt6NoGren";
	}
}

//==========================================
// Government Troops: M16 & Grenade Attack
//==========================================

class HLGrunt1 : HLMonster
{
	bool closegrencheck,shotgun;
	int GrenTimeCounter;
	Property Shotgun:Shotgun;
	Default
	{
		Health 150;
		Mass 150;
		Speed 3.5;
		Height 63;
		PainChance 70;
		PainChance "Crowbar",218;
		maxdropoffheight 128;
		DropItem "M16Pickup";
		DamageFactor "SoldierDamage",0;
		SeeSound "halflife/monsters/humangrunt/sight";
		PainSound "halflife/monsters/humangrunt/pain";
		DeathSound "halflife/monsters/humangrunt/death";
		ActiveSound "halflife/monsters/humangrunt/active";
		Species "HLGrunt";
	}
	States
	{
	Spawn:
		TNT1 A 0;
		HLI1 A 3;
		HLI1 A 3 A_Look;
		Wait;
	See:
		HLR1 A 0
		{
			if(closegrencheck)
			{
				bFRIGHTENED=1;
				if(GrenTimeCounter>=4)
				{
					closegrencheck=0;
					GrenTimeCounter-=4;
					bFRIGHTENED=0;
				}
				else
				{
					GrenTimeCounter++;
					return ResolveState("Flee");
				}
			}
			if(CheckIfCloser(target,300))
				return ResolveState("Walking");
			A_SetSpeed(3.5);
			return state(null);
		}
		HLR1 A 1 A_Chase;
		HLR1 B 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 C 1 A_Chase;
		HLR1 D 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 E 1 A_Chase;
		HLR1 F 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 G 1 A_Chase;
		HLR1 H 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 I 1 A_Chase;
		HLR1 J 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 K 1 A_Chase;
		HLR1 L 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 M 1 A_Chase;
		HLR1 N 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 O 1 A_Chase;
		HLR1 P 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 Q 1 A_Chase;
		HLR1 R 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 S 1 A_Chase;
		HLR1 T 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 U 1 A_Chase;
		HLR1 V 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 W 1 A_Chase;
		HLR1 X 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Walking:
		HLW1 A 0
		{
			bMISSILEEVENMORE=1;
			bMISSILEMORE=1;
			A_SetSpeed(1.75);
		}
		HLW1 A 1 A_Chase;
		HLW1 BCD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 E 1 A_Chase;
		HLW1 FGH 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 I 1 A_Chase;
		HLW1 JKL 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 M 1 A_Chase;
		HLW1 NOP 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 Q 1 A_Chase;
		HLW1 RST 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 U 1 A_Chase;
		HLW1 VWX 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 X 0
		{
			bMISSILEEVENMORE=0;
			bMISSILEMORE=0;
		}
		Goto See;
	Flee:
		HLR1 A 0 A_SetSpeed(3.5);
		HLR1 A 1 A_Chase(null,null);
		HLR1 B 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 C 1 A_Chase(null,null);
		HLR1 D 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 E 1 A_Chase(null,null);
		HLR1 F 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 G 1 A_Chase(null,null);
		HLR1 H 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 I 1 A_Chase(null,null);
		HLR1 J 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 K 1 A_Chase(null,null);
		HLR1 L 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 M 1 A_Chase(null,null);
		HLR1 N 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 O 1 A_Chase(null,null);
		HLR1 P 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 Q 1 A_Chase(null,null);
		HLR1 R 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 S 1 A_Chase(null,null);
		HLR1 T 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 U 1 A_Chase(null,null);
		HLR1 V 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 W 1 A_Chase(null,null);
		HLR1 X 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto See;
	Grenade:
		HLA1 A 1 A_FaceTarget;
		HLA1 BCDEFGHIJKLMNOPQRST 1;
		HLA1 U 1 A_FaceTarget;
		HLA1 VWXY 1;
		HLA1 Z 1 A_FaceTarget;
		HLA1 "[\]" 1;
		HLA2 A 1;
		HLA2 B 1 A_SpawnProjectile("GruntHandGrenade",40);
		HLA2 CDEFGHIJKLMN 1;
		Goto See;
	Melee:
		HLA1 A 1 A_FaceTarget;
		HLA1 BCEFGIJKMNOQRS 1;
		HLA1 U 1 A_FaceTarget;
		HLA1 VWY 1;
		HLA1 "Z[]" 1 A_FaceTarget;
		HLA2 A 1
		{
			closegrencheck=1;
			actor gren=A_SpawnProjectile("GruntHandGrenade",40);
			gren.speed=10;
		}
		HLA2 B 1;
		HLA2 DEFGHIKLM 1;
		Goto See;
	Missile:
		HK1O A 0
		{
			if(shotgun)
			{
				if(random(0,255)<200)
					return resolvestate("Shotgun");
				if(CheckIfCloser(target,600)&&!CheckIfCloser(target,256))
					return resolvestate("Grenade");
				return resolvestate("Shotgun");
			}
			else
			{
				if(random(0,255)<200)
					return resolvestate("Assault");
				if(CheckIfCloser(target,600))
				{
					if(!CheckIfCloser(target,256))
					{
						if(random(0,255)<64)
							return resolvestate("GrenadeGun");
						return resolvestate("Grenade");
					}
					return resolvestate("Assault");
				}
				if(CheckIfCloser(target,1000))
					return resolvestate("GrenadeGun");
				return resolvestate("Assault");
			}
			return state(null);
		}
	Assault:
		HLI1 AAAA 3 A_FaceTarget;
		HLI1 A 0 A_FaceTarget;
		HLA2 Q 1 Light("ImShootingAGun") A_GruntGunshot(1);
		HLA2 RST 1;
		HLA2 A 0 A_Jump(15,"See");
		HLA2 A 0 A_CposRefire;
		Goto Assault+4;
	Shotgun:
		HLI1 AAAA 3 A_FaceTarget;
		HLI1 A 0 A_Jump(64,"ShotgunDouble");
		HLI1 AAA 4 A_FaceTarget;
		HLA2 Q 1 Light("ImShootingAGun") A_GruntGunshot(5);
		HLA2 "RSTUVWXYYZZ[[\\]]" 1;
		HLA2 A 0 A_Jump(40,"See");
		HLA2 A 0 A_CposRefire;
		Goto Shotgun+2;
	ShotgunDouble:
		HLI1 AAAAA 4 A_FaceTarget;
		HLA2 Q 1 Light("ImShootingAGun") A_GruntGunshot(10);
		HLA2 "RSTUVWWXXYYZZ[[\\]]" 1;
		HLA2 AAA 4 A_FaceTarget;
		HLA2 A 0 A_Jump(100,"See");
		HLA2 A 0 A_CposRefire;
		Loop;
	GrenadeGun:
		HLI1 AAAAAAAAA 3 A_FaceTarget;
		HLA3 ABCD 1;
		HLA3 E 1 Light("ImShootingAGun")
		{
			A_PlaySound("halflife/weapons/grenadelaunch",5);
			A_GruntGunsmoke();
			actor gren=A_SpawnProjectile("HLGruntGunGrenade",38);
			if(CheckIfCloser(target,500))
				gren.A_ChangeVelocity(z:1.25);
			else
				gren.A_ChangeVelocity(z:3.5);
		}
		HLA3 FGHIJKLMNOPQRSTUV 1;
		Goto See;
	Pain:
		HLP1 A 1
		{
			A_Pain();
			if(closegrencheck)
				GrenTimeCounter++;
		}
		HLP1 BCDEFGH 1;
		Goto See;
	Death:
		HLD1 A 1 A_Scream;
		HLD1 B 1;
		HLD1 C 1 A_NoBlocking;
		HLD1 "DEFGHIJKLMNOPQRSTUVWXYZ[\]" 1;
		HLD2 A 1 A_PlaySound("halflife/weapons/bodydrop");
		HLD2 BCD 1;
		HLD2 E -1;
		Stop;
	XDeath:
		TNT1 A 0 A_XenGibs(1);
		Stop;
	}
	void A_GruntGunshot(int shots)
	{
		if(shots==1)
		{
			A_PlaySound("halflife/weapons/machinegunshot",5);
			A_GruntGunsmoke("GunSmallSmoke");
		}
		else
		{
			if(shots==5)
				A_PlaySound("halflife/weapons/shotgunsingle",5);
			else if(shots==10)
				A_PlaySound("halflife/weapons/shotgundouble",5);
			A_GruntGunsmoke();
		}
		A_CustomBulletAttack(7,7,shots,4,"NewPuff2");
	}
	void A_GruntGunsmoke(class<actor> smoke="GunSmoke")
	{
		for(int i=0;i<2;i++)
			A_SpawnItemEx(smoke,35,3,49);
	}
}

//==========================================
// Government Troops: M16 & Grenade Attack
//==========================================

class HLGrunt2 : HLGrunt1{}

//==========================================
// Government Troops: M16 & Grenade Attack
//==========================================

class HLGrunt3 : HLGrunt1{}

//============================================
// Government Troops: Shotgun & Grenade Attack
//============================================

class HLGrunt4 : HLGrunt1
{
	Default
	{
		DropItem "ShotgunPickup";
		HLGrunt1.Shotgun 1;
	}
}

//============================================
// Government Troops: Shotgun & Grenade Attack
//============================================

class HLGrunt5 : HLGrunt4{}

//============================================
// Government Troops: Shotgun & Grenade Attack
//============================================

class HLGrunt6 : HLGrunt4{}

//===============================
// Government Troops: M16 Attack
//===============================

class HLGrunt1NoGren : HLGrunt1
{
	Default
	{
		Mass 200;
		PainChance 64;
		PainChance "Crowbar",210;
	}
	States
	{
	Missile:
		Goto Assault;
	Melee:
		Stop;
	}
}

//===============================
// Government Troops: M16 Attack
//===============================

class HLGrunt2NoGren : HLGrunt2
{
	Default
	{
		Mass 200;
		PainChance 64;
		PainChance "Crowbar",210;
	}
	States
	{
	Missile:
		Goto Assault;
	Melee:
		Stop;
	}
}

//===============================
// Government Troops: M16 Attack
//===============================

class HLGrunt3NoGren : HLGrunt3
{
	Default
	{
		Mass 200;
		PainChance 64;
		PainChance "Crowbar",210;
	}
	States
	{
	Missile:
		Goto Assault;
	Melee:
		Stop;
	}
}

//==================================
// Government Troops: Shotgun Attack
//==================================

class HLGrunt4NoGren : HLGrunt4
{
	Default
	{
		Mass 200;
		PainChance 64;
		PainChance "Crowbar",210;
	}
	States
	{
	Missile:
		Goto Shotgun;
	Melee:
		Stop;
	}
}

//==================================
// Government Troops: Shotgun Attack
//==================================

class HLGrunt5NoGren : HLGrunt5
{
	Default
	{
		Mass 200;
		PainChance 64;
		PainChance "Crowbar",210;
	}
	States
	{
	Missile:
		Goto Shotgun;
	Melee:
		Stop;
	}
}

//==================================
// Government Troops: Shotgun Attack
//==================================

class HLGrunt6NoGren : HLGrunt6
{
	Default
	{
		Mass 200;
		PainChance 64;
		PainChance "Crowbar",210;
	}
	States
	{
	Missile:
		Goto Shotgun;
	Melee:
		Stop;
	}
}

//********************************************
class GruntHandGrenade : Actor
{
	int GrenTimeCounter;
	Default
	{
		Mass 1;
		Radius 3;
		Height 6;
		Speed 20;
		Scale .3;
		Damage 1;
		Damagetype "SoldierDamage";
		PROJECTILE;
		BounceType "Doom";
		+CANBOUNCEWATER
		BounceFactor .6;
		-NOGRAVITY
		Gravity .5;
	}
	States
	{
	Spawn:
		HGN1 ABCDE 2;
		TNT1 A 0 {GrenTimeCounter++;}
		TNT1 A 0 A_JumpIf(GrenTimeCounter>=9,"Explode");
		Loop;
	Death:
		HGN1 A 10;
		TNT1 A 0 {GrenTimeCounter++;}
		TNT1 A 0 A_JumpIf(GrenTimeCounter>=9,"Explode");
		Loop;
	Explode:
		TNT1 A 10
		{
			A_PlaySound("halflife/weapons/explode1");
			A_Explode(128,64);
			A_SpawnItemEx("NewExplosionMedium");
		}
		Stop;
	}
}

//********************************************

class HLGruntGunGrenade : M16Grenade
{
	Default
	{
		Damagetype "SoldierDamage";
		Gravity .2;
		DeathSound "halflife/weapons/explode1";
		Decal "";
	}
	States
	{
	Death:
		TNT1 A 1
		{
			A_Explode(64);
			A_SpawnItemEx("NewExplosionMedium");
		}
		Stop;
	}
}