class HLBabyHeadCrab : Actor
{
	bool crabjump;
	Default
	{
		Monster;
		Health 10;
		Speed 8;
		Height 8;
		Radius 8;
		-Solid
		RenderStyle "Translucent";
		Alpha .9;
		PainChance 200;
		MaxTargetRange 128;
		Scale .09;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/headcrab/sight";
		DeathSound "halflife/monsters/headcrab/death";
		ActiveSound "halflife/monsters/headcrab/idle";
		+DONTHARMCLASS
		DamageFactor "Gonarch",0;
	}
	States
	{
	Spawn:
		HLQI A 3 A_Look;
		Loop;
	See:
		TNT1 A 0 A_JumpIf(crabjump,"SeeMelee");
		HLQW ABCDE 5 A_Chase(null,"Missile");
		Loop;
	SeeMelee:
		HLQW A 5 A_Chase("Melee",null);
		HLQW B 5
		{
			crabjump=0;
			A_Chase("Melee",null);
		}
		Goto See+3;
	Melee:
		HLQW A 10 A_CustomMeleeAttack(random(1,8),"halflife/monsters/headcrab/meleehit");
		Goto See;
	Missile:
		HLQI A 4 A_FaceTarget;
		HLQM AB 3;
		HLQM C 1
		{
			crabjump=1;
			A_SkullAttack();
		}
		HLQM C 2 ThrustThingZ(0,random(10,25),0,1);
		HLQM C 3;
		HLQM C 3 A_CheckFloor(1);
		Wait;
		HLQM D 60 A_stop;
		Goto See;
	Pain:
		HLQP A 8 {crabjump=0;}
		Goto See;
	Death:
	  	TNT1 AAAA 0 A_SpawnItemEx("XenGibsMini",random(2,-2),random(2,-2),random(0,20),random(1,4),random(1,4),random(2,6),random(0,359));
		Stop;
	}
}

class HLBabyHeadCrab2 : HLBabyHeadCrab	//for the gonarch
{
	Default
	{
		Speed 10;
	}
	States
	{
	See:
		TNT1 A 0 A_JumpIf(crabjump,"SeeMelee");
		HLQW ABCDE 3 A_Chase(null,"Missile");
		Loop;
	SeeMelee:
		HLQW A 3 A_Chase("Melee",null);
		HLQW B 3
		{
			crabjump=0;
			A_Chase("Melee",null);
		}
		Goto See+3;
	}
}