//===========
// BullSquid
//===========

class HLBullSquid : HLMonster
{
	Default
	{
		Health 150;
		Mass 350;
		Speed 5;
		Height 32;
		Radius 28;
		PainChance 40;
		PainChance "Crowbar",164;
		Scale .365;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/bullsquid/sight";
		PainSound "halflife/monsters/bullsquid/pain";
		DeathSound "halflife/monsters/bullsquid/death";
		ActiveSound "halflife/monsters/bullsquid/idle";
		Species "HLBullSquid";
	}
	States
	{
	Spawn:
		HLDI A 3 A_Look;
		Loop;
	See:
		HLDW A 1 A_Chase;
		HLDW AAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLDW B 1 A_Chase;
		HLDW BBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLDW C 1 A_Chase;
		HLDW CCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLDW D 1 A_Chase;
		HLDW DDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Melee:
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/growl");
		HLDA ABC 4 A_FaceTarget;
		HLDA D 4 A_CustomMeleeAttack(8*random(1,8));
		HLDA EF 4;
		HLDW A 1 A_Chase(null,null);
		HLDW AAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLDW B 1 A_Chase(null,null);
		HLDW BBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLDW C 1 A_Chase(null,null);
		HLDW CCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLDW D 1 A_Chase(null,null);
		HLDW DDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto See;
	Missile:
		HLDB ABC 6 A_FaceTarget;
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spit");
		HLDB D 6 A_FaceTarget;
		TNT1 AAAAAAA 0 A_SpawnItemEx("BSSpitExtra2",0,0,20,random(2,5),random(2,-2),random(1,3));
		HLDB E 6 A_SpawnProjectile("BullSquidSpit",20,0,0);
		HLDB FG 6;
		Goto See;
	Pain:
		HLDP A 8 A_Pain;
		Goto See;
	Death:
		HLDD A 5 A_Scream;
		HLDD B 4 A_NoBlocking;
		HLDD C 3 A_PlaySound("halflife/weapons/bodydrop");
		HLDD DEFG 3;
		HLDD H -1;
		Stop;
	XDeath:
		TNT1 A 0 A_XenGibs;
		Stop;
	}
}

class BullSquidSpit : Actor
{
	Default
	{
		Projectile;
		Speed 30;
		Scale 0.35;
		Damage 7;
		RenderStyle "Add";
		Alpha 0.9;
		Translation "80:111=118:127";
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		X022 ABCD 4;
		Loop;
	Death:
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit");
		TNT1 AAAA 0 A_SpawnItemEx("BSSpitExtra",0,0,0,random(1,-1),random(1,-1),random(0,2));
		Stop;
	}
}

class BSSpitExtra : BullSquidSpit
{
	Default
	{
		Speed 0;
		Damage 0;
		+NOCLIP
		Scale 0.15;
		-NOGRAVITY
		Gravity .125;
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(128,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

class BSSpitExtra2 : BSSpitExtra
{
	Default
	{
		Scale 0.26;
	}
}