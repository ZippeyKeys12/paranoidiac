class HLScientistWandererSpawner : RandomSpawner
{
	Default
	{
		DropItem "HLScientist1Wander";
		DropItem "HLScientist2Wander";
		DropItem "HLScientist3Wander";
		DropItem "HLScientist4Wander";
	}
}

class HLScientistImmovilSpawner : RandomSpawner
{
	Default
	{
		DropItem "HLScientist1Still";
		DropItem "HLScientist2Still";
		DropItem "HLScientist3Still";
		DropItem "HLScientist4Still";
	}
}

class HLScientist : Actor
{
	int user_boredom,stay,panic;
	/*override void Activate(Actor activator)
		{user_boredom=0;}
	override void Deactivate(Actor activator)
		{user_boredom=1;}*/
	Default
	{
		Monster;
		Health 1;
		Speed 1.666667;
		Height 56;
		PainChance 255;
		Scale .36;
		+FRIENDLY
		//+NODAMAGE
		+USESPECIAL
		-CANUSEWALLS
		-COUNTKILL
		Activation THINGSPEC_NoDeathSpecial;
		SeeSound "halflife/monsters/scientist/sight";
		PainSound "halflife/monsters/scientist/pain";
		DeathSound "halflife/monsters/scientist/death";
	}
	action void Chatter(int chance)
	{
		if(random(0,255)<chance)
			A_PlaySound("halflife/monsters/scientist/idle");
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Path : HLScientist
{
	States
	{
	Spawn:
		HLRI A 0;
		"####" A 0 {user_boredom=1;}
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 5
		{
			if(stay>=80)
			{
				stay=0;
				if(random(0,1))
				{
					user_boredom=0;
					return ResolveState("See");
				}
				return State(null);
			}
			stay++;
			if(!user_boredom)
			{
				A_PlaySound("scientist/randomphrase");
				return ResolveState("See");
			}
			Chatter(2);
			return state(null);
		}
		Loop;
	See:
		HLRW A 0;
	SeeLoop:
		"####" AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1
		{
			if(user_boredom)
				return ResolveState("Spawn");
			A_Chase();
			return state(null);
		}
		Loop;
	Pain:
		HLRP A 8 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist1Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist2Path : HLScientist1Path
{
	Default
	{
		Scale .3526;
	}
	States
	{
	Spawn:
		SC1I A 0;
		Goto Super::Spawn+1;
	See:
		SC1W A 0;
		Goto SeeLoop;
	Pain:
		SC1P A 8 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist2Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist3Path : HLScientist1Path
{
	Default
	{
		Scale .355;
	}
	States
	{
	Spawn:
		SC2I A 0;
		Goto Super::Spawn+1;
	See:
		SC2W A 0;
		Goto SeeLoop;
	Pain:
		SC2P A 8 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist3Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist4Path : HLScientist1Path
{
	States
	{
	Spawn:
		SC3I A 0;
		Goto Super::Spawn+1;
	See:
		SC3W A 0;
		Goto SeeLoop;
	Pain:
		SC3P A 8 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist4Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Wander : HLScientist
{
	Default
	{
		Health 4;
	}
	States
	{
	Spawn:
		HLRW AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 A_Wander;
		HLRI A 0 A_Jump(32,"StopMov");
		Loop;
	StopMov:
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 15 Chatter(4);
		"####" # 0 A_Jump(6,"Spawn");
		Loop;
	Pain:
		HLRP A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist1Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist2Wander : HLScientist1Wander
{
	Default
	{
		Scale 0.3526;
	}
	States
	{
	Spawn:
		SC1W AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 NODELAY A_Wander;
		SC1I A 0 A_Jump(32,"StopMov");
		Loop;
	Pain:
		SC1P A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist2Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist3Wander : HLScientist1Wander
{
	Default
	{
		Scale 0.355;
	}
	States
	{
	Spawn:
		SC2W AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 NODELAY A_Wander;
		SC2I A 0 A_Jump(32,"StopMov");
		Loop;
	Pain:
		SC2P A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist3Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist4Wander : HLScientist1Wander
{
	States
	{
	Spawn:
		SC3W AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 NODELAY A_Wander;
		SC3I A 0 A_Jump(32,"StopMov");
		Loop;
	Pain:
		SC3P A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist4Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Panic : HLScientist
{
	Default
	{
		Health 25;
		-USESPECIAL
		+FRIGHTENED
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_ClearTarget;
		TNT1 A 0 A_Jump(256,"Wander","WanderRunStart");
	Wander:
		HLRW A 0;
		"####" A 0 A_SetSpeed(5.0/3.0);
		"####" AAAAAA 1 A_Wander;
		"####" A 0 A_Look;
		"####" BBBBBB 1 A_Wander;
		"####" A 0 A_Look;
		"####" CCCCCC 1 A_Wander;
		"####" A 0 A_Look;
		"####" DDDDDD 1 A_Wander;
		"####" A 0 A_Look;
		"####" EEEEEE 1 A_Wander;
		"####" A 0 A_Look;
		"####" FFFFFF 1 A_Wander;
		"####" A 0 A_Look;
		"####" A 0	//how often they'll say something while moving
		{
			if(random(0,255)<8)
			{
				A_PlaySound("scientist/panic");
				return ResolveState("Wander");
			}
			if(random(0,255)<4)
			{
				if(random(0,255)<235)
					return ResolveState("IdleReg");		//stopmov, ironically also randomly starts the run seq
				return ResolveState("WanderRunStart");	//kind of rare to see them running just because of nothing.
			}
			return state(null);
		}
		Loop;
	IdleReg:
		HLRI A 0;
		"####" A 0
		{
			if(random(0,255)<3)
				A_PlaySound("scientist/panic");
		}
		"####" "###" 5 A_Look;
		"####" # 0 A_Jump(5,"Wander");
		Loop;
	See:			//scream the 1st time they enter here
		"####" # 0 A_PlaySound("halflife/monsters/scientist/sight");
		"####" # 0 A_Jump(256,"SeeDecide");
	SeeDecide:		//decide if they calm down or not
		HLRR A 0 A_JumpIf(panic>=35,"SeeCalmDown");
		"####" # 0 A_Jump(256,"SeeLoop");
	SeeLoop:
		"####" A 0 A_SetSpeed(5);
		"####" AAABBBCCCDDDEEEFFF 1 A_Chase;
		"####" A 0
		{
			panic++;
			if(random(0,255)<4)//every once in a while it will say something
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("SeeLoop");
			}
			return state(null);
		}
		Goto SeeDecide;
	SeeCalmDown:
		TNT1 A 0 {panic-=8;}	//If decides to go running againg, will last a bit more
		TNT1 A 0 A_Jump(160,"WanderRun");
		TNT1 A 0 {panic=0;}
		TNT1 A 0 A_ClearTarget;
		TNT1 A 0 A_Jump(256,"Wander");
	WanderRunStart:		//scream the 1st time they enter here
		TNT1 A 0 A_PlaySound("halflife/monsters/scientist/sight");
		TNT1 A 0 A_Jump(256,"WanderRunDecide");
	WanderRunDecide:	//decide if they calm down or not
		TNT1 A 0 A_JumpIf(panic>=30,"CalmDown");
	WanderRun:		//not calmed at all
		HLRR A 0;
		"####" A 0 A_SetSpeed(5);
		"####" AAA 1 A_Wander;
		"####" A 0 A_Look;
		"####" BBB 1 A_Wander;
		"####" A 0 A_Look;
		"####" CCC 1 A_Wander;
		"####" A 0 A_Look;
		"####" DDD 1 A_Wander;
		"####" A 0 A_Look;
		"####" EEE 1 A_Wander;
		"####" A 0 A_Look;
		"####" FFF 1 A_Wander;
		"####" A 0 A_Look;
		"####" A 0
		{
			panic++;
			if(random(0,255)<4)//every once in a while it will say something
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("WanderRun");
			}
			return state(null);
		}
		Goto WanderRunDecide;
	CalmDown:
		TNT1 A 0 {panic-=8;}	//If decides to go running again, will last a bit more
		TNT1 A 0 A_Jump(160,"WanderRun");
		TNT1 A 0 {panic=0;}
		TNT1 A 0 A_Jump(256,"Wander");
	Pain:
		HLRP A 8 A_Pain;
		Goto See;
	Death:
		HLRD A 3 A_Scream;
		HLRD B 3 A_NoBlocking;
		HLRD CDEFGHIJ 3;
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop");
		HLRD KL 3;
		HLRD M -1;
		Stop;
	}
}

class HLScientist2Panic : HLScientist1Panic
{
	Default
	{
		Scale 0.36;
	}
	States
	{
	Wander:
		SC1W A 0;
		Goto Super::Wander+1;
	IdleReg:
		SC1I A 0;
		Goto Super::IdleReg+1;
	SeeDecide:
		SC1R A 0 A_JumpIf(panic>=45,"SeeCalmDown");
		"####" # 0 A_Jump(256,"SeeLoop");
	WanderRunDecide:
		TNT1 A 0 A_JumpIf(panic>=35,"CalmDown");
	WanderRun:
		SC1R A 0;
		Goto Super::WanderRun+1;
	Pain:
		SC1P A 8 A_Pain;
		Goto See;
	Death:
		SC1D A 3 A_Scream;
		SC1D B 3 A_NoBlocking;
		SC1D CDEFGHIJ 3;
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop");
		SC1D KL 3;
		SC1D M -1;
		Stop;
	}
}

class HLScientist3Panic : HLScientist1Panic
{
	Default
	{
		Scale 0.36;
	}
	States
	{
	Wander:
		SC2W A 0;
		Goto Super::Wander+1;
	IdleReg:
		SC2I A 0;
		Goto Super::IdleReg+1;
	SeeDecide:
		SC2R A 0 A_JumpIf(panic>=55,"SeeCalmDown");
		"####" # 0 A_Jump(256,"SeeLoop");
	WanderRunDecide:
		TNT1 A 0 A_JumpIf(panic>=65,"CalmDown");
	WanderRun:
		SC2R A 0;
		Goto Super::WanderRun+1;
	Pain:
		SC2P A 8 A_Pain;
		Goto See;
	Death:
		SC2D A 3 A_Scream;
		SC2D B 3 A_NoBlocking;
		SC2D CDEFGHIJ 3;
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop");
		SC2D KL 3;
		SC2D M -1;
		Stop;
	}
}

class HLScientist4Panic : HLScientist1Panic
{
	States
	{
	Wander:
		SC3W A 0;
		Goto Super::Wander+1;
	IdleReg:
		SC3I A 0;
		Goto Super::IdleReg+1;
	SeeDecide:
		SC3R A 0 A_JumpIf(panic>=65,"SeeCalmDown");
		"####" # 0 A_Jump(256,"SeeLoop");
	WanderRunDecide:
		TNT1 A 0 A_JumpIf(panic>=65,"CalmDown");
	WanderRun:
		SC3R A 0;
		Goto Super::WanderRun+1;
	Pain:
		SC3P A 8 A_Pain;
		Goto See;
	Death:
		SC3D A 3 A_Scream;
		SC3D B 3 A_NoBlocking;
		SC3D CDEFGHIJ 3;
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop");
		SC3D KL 3;
		SC3D M -1;
		Stop;
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Still : HLScientist
{
	Default
	{
		Health 2;
	}
	States
	{
	Spawn:
		HLRI A 0;
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 10 Chatter(2);
		Loop;
	Pain:
		HLRP A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist1Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist2Still : HLScientist1Still
{
	Default
	{
		Scale 0.3526;
	}
	States
	{
	Spawn:
		SC1I A 0;
		Goto Super::Spawn+1;
	Pain:
		SC1P A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist2Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist3Still : HLScientist1Still
{
	Default
	{
		Scale 0.355;
	}
	States
	{
	Spawn:
		SC2I A 0;
		Goto Super::Spawn+1;
	Pain:
		SC2P A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist3Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

class HLScientist4Still : HLScientist1Still
{
	Default
	{
		Scale 0.36;
	}
	States
	{
	Spawn:
		SC3I A 0;
		Goto Super::Spawn+1;
	Pain:
		SC3P A 8 A_Pain;
		Goto Spawn;
	Death:
		TNT1 A 0 A_NoBlocking;
		TNT1 A 10 A_SpawnItemEx("HLScientist4Panic",flags:SXF_NOCHECKPOSITION);
		Stop;
	}
}

//////////////////////////////////////////////////////////
//Previous code for scientists; only used for dead ones //
//////////////////////////////////////////////////////////

class HLScientistUpdate : Actor
{
	Default
	{
		Scale .36;
	}
	States
	{
	Spawn:
		HLRD M -1;
		Stop;
	}
}

class HLScientist1 : Actor
{
	Default
	{
		Scale 0.3526;
	}
	States
	{
	Spawn:
		SC1D M -1;
		Stop;
	}
}

class HLScientist2 : HLScientistUpdate
{
	Default
	{
		Scale 0.355;
	}
	States
	{
	Spawn:
		SC2D I -1;
		Stop;
	}
}

class HLScientist3 : HLScientistUpdate
{
	States
	{
	Spawn:
		SC3D N -1;
		Stop;
	}
}