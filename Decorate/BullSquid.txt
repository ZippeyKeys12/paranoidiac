//===========
// BullSquid
//===========

actor HLBullSquid 7404
{
    spawnid 158
	Monster
//	Health 200
	Health 150
	Mass 350
	Speed 20
	Height 32
	Radius 28
	PainChance 40
	Scale 0.365
	MeleeDamage 8
	BloodColor "DE FE 72"
	SeeSound "halflife/monsters/bullsquid/sight"
	PainSound "halflife/monsters/bullsquid/pain"
	DeathSound "halflife/monsters/bullsquid/death"
	ActiveSound "halflife/monsters/bullsquid/idle"
	States
	{
	Spawn:
		HLDI A 3 A_Look
		Loop
	See:
		HLDW ABCD 4 A_Chase
		Loop
	Melee:
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/growl")
		HLDA ABC 4 A_FaceTarget
		HLDA D 4 A_MeleeAttack
		HLDA EF 4
		HLDW ABCD 4 A_ExtChase(0,0)
		Goto See
	Missile:
		HLDB ABC 6 A_FaceTarget
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spit")
		HLDB D 6 A_FaceTarget
		TNT1 AAAAAAA 0 A_SpawnItemEx("BSSpitExtra2",0,0,20,random(2,5),random(2,-2),random(1,3),0,0)
		HLDB E 6 A_CustomMissile("BullSquidSpit",20,0,0) 
		HLDB FG 6
		Goto See
	Pain:
		HLDP A 8 A_Pain
		Goto See
	Death:
		HLDD A 5 A_Scream
		HLDD B 4 A_Fall
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop")
		HLDD CDEFG 3
		HLDD H -1
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath")
		TNT1 A 0 A_Fall
	  	NULL AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359))
		Stop
	}
}

actor BullSquidSpit 
{
	Projectile
	Speed 30
	Scale 0.35
	Damage 7
	RenderStyle Add
	Alpha 0.9
	Translation "80:111=118:127"
	+FORCEXYBILLBOARD
	DeathSound ""
	States
	{
	Spawn:
		X022 ABCD 4
		Loop
	Death:
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit")
		TNT1 AAAA 0 A_SpawnItemEx("BSSpitExtra",0,0,0,random(1,-1),random(1,-1),random(0,2),0,0)
		Stop
	}
}

Actor BSSpitExtra
{
	Projectile
	Speed 0
	Damage 0
	+NOCLIP
	Scale 0.15
	RenderStyle Add
	Alpha 0.9
	-NOGRAVITY
	+LOWGRAVITY
	+FORCEXYBILLBOARD
	Translation "80:111=118:127"
	States
	{
	Spawn:
		X023 AB 3
		TNT1 A 0 A_Jump(128,"FadeOut")
		Loop
	FadeOut:
		X023 AB 3 A_FadeOut(0.1)
		Loop
	}
}

Actor BSSpitExtra2 : BSSpitExtra
{
	Scale 0.26
}