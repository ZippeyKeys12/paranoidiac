//===========
// BullSquid
//===========

class HLBullSquid : HLMonster
{
	int countdown;
	Default
	{
		Health 150;
		Mass 350;
		Speed 5;
		Height 32;
		Radius 28;
		PainChance 40;
		PainChance "Crowbar",164;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/bullsquid/sight";
		PainSound "halflife/monsters/bullsquid/pain";
		DeathSound "halflife/monsters/bullsquid/death";
		ActiveSound "halflife/monsters/bullsquid/idle";
		Species "Xen";
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_Look;
		HLI2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_Look;
		HLI3 ABCDEFGHIJKLMNOPQR 2 A_Look;
		Loop;
	See:
		HLR1 A 1 A_BullSquidChase;
		HLR1 BCD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 E 1 A_BullSquidChase;
		HLR1 FGH 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 I 1 A_BullSquidChase;
		HLR1 JKL 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 M 1 A_BullSquidChase;
		HLR1 NOP 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 Q 1 A_BullSquidChase;
		HLR1 RST 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 U 1 A_BullSquidChase;
		HLR1 VWX 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 Y 1 A_BullSquidChase;
		HLR1 Z[\ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 ] 1 A_BullSquidChase;
		HLR2 ABC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR2 D 1 A_BullSquidChase;
		HLR2 EFGH 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Melee:
		HLA1 A 1
		{
			countdown=4;
			A_PlaySound("halflife/monsters/bullsquid/growl");
			A_FaceTarget();
		}
		HLA1 BCD 1;
		HLA1 E 1 A_FaceTarget;
		HLA1 FGH 1;
		HLA1 I 1 A_FaceTarget;
		HLA1 JKL 1;
		HLA1 M 1 A_CustomMeleeAttack(8*random(1,8));
		HLA1 NOPQRSTUVWXYZ[\] 1;
		Goto See;
	Missile:
		HLA2 A 1 A_FaceTarget;
		HLA2 BCDEFG 1;
		HLA2 H 1 A_FaceTarget;
		HLA2 IJKLM 1;
		HLA2 N 1
		{
			A_FaceTarget();
			A_PlaySound("halflife/monsters/bullsquid/spit");
		}
		HLA2 OPQRS 1;
		HLA2 T 1
		{
			for(int i=0;i<7;i++)
				A_SpawnItemEx("BSSpitExtra2",0,0,20,random(2,5),random(2,-2),random(1,3));
			A_SpawnProjectile("BullSquidSpit",20);
		}
		HLA2 UVWXYZ[\] 1;
		HLA3 ABCDEFGHIJKLM 1;
		Goto See;
	Pain:
		HLI1 A 1
		{
			A_Pain();
			countdown=0;
		}
		HLI1 BCDEFGH 1;
		Goto See;
	Death:
		HLD1 A 2 A_Scream;
		HLD1 B 2;
		HLD1 C 2 A_NoBlocking;
		HLD1 D 2;
		HLD1 E 2 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 FGHI 2;
		HLD1 J -1;
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs(16,"XenGibs");
		Stop;
	}
	void A_BullSquidChase()
	{
		if(countdown)
		{
			countdown--;
			A_Chase(null,null);
		}
		else
			A_Chase();
	}
}

class BSSpitExtra : Actor
{
	Default
	{
		Projectile;
		RenderStyle "Add";
		Alpha .9;
		Translation "80:111=118:127";
		+FORCEXYBILLBOARD
		+NOCLIP
		Scale .15;
		-NOGRAVITY
		Gravity .125;
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(128,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

class BSSpitExtra2 : BSSpitExtra
{
	Default
	{
		Scale .26;
	}
}

class BullSquidSpit : BSSpitExtra
{
	Default
	{
		Speed 30;
		Scale .35;
		Damage 7;
		-NOCLIP
		+NOGRAVITY
		DeathSound "halflife/monsters/bullsquid/spithit";
	}
	States
	{
	Spawn:
		X022 ABCD 4;
		Loop;
	Death:
		TNT1 AAAA 0 A_SpawnItemEx("BSSpitExtra",0,0,0,random(1,-1),random(1,-1),random(0,2));
		Stop;
	}
}