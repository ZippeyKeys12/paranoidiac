class RolfBellmer : PlayerPawn
{
	override void CheckCrouch(bool totallyfrozen)
	{
		//Prevent crouching when in disguise
		if(!CountInv("ScientistDisguise"))
			super.CheckCrouch(totallyfrozen);
	}
	Default
	{
		+NOSKIN
		Height 60;
		//Scale .28;
		Player.DisplayName "Rolf Bellmer";
		Player.RunHealth 15;
		Player.ViewHeight 56;
		Player.AttackZOffset 24;
		Player.WeaponSlot 1,"Crowbar";
		Player.WeaponSlot 2,"Beretta","Python";
		Player.WeaponSlot 3,"HornetGun","PShotgun";
		Player.WeaponSlot 4,"Xbow","ARifle";
		Player.WeaponSlot 5,"RPGLauncher";
		Player.WeaponSlot 6,"GluonGun";
		Player.StartItem "Crowbar";
		Player.StartItem "HEVSuit";
	}
	States
	{
	Spawn:
		HPLM A 0 A_SetScale(.28);
		HPLM A 1 A_JumpIf(CountInv("ScientistDisguise"),"Spawn2");
		Loop;
	See:
		HPLM A 0 A_SetScale(.28);
		HPLW A 4 A_JumpIf(CountInv("ScientistDisguise"),"See2");
		HPLW BCDEF 4;
		Loop;
	Melee:
		HPLM A 0 A_SetScale(.28);
		HPLM A 0 A_JumpIf(CountInv("ScientistDisguise"),"Melee2");
	Missile:
		HPLM A 0 A_SetScale(.28);
		HPLM A 3 A_JumpIf(CountInv("ScientistDisguise"),"Missile2");
		HPLM ABC 3;
		Goto Spawn;
	Pain:
		HPLM A 0 A_SetScale(.28);
		HPLP A 4 A_JumpIf(CountInv("ScientistDisguise"),"Pain2");
		HPLP A 4 A_Pain;
		Goto Spawn;
	Death:
		HPLM A 0 A_SetScale(.28);
		HPLD A 1 A_JumpIf(CountInv("ScientistDisguise"),"Death2");
		HPLD A 1 A_PlaySound("HLPlayerHEVFail",CHAN_VOICE);
		HPLD A 2 A_PlayerScream;
		HPLD B 3 A_NoBlocking;
		HPLD CDEFGHIJKLMNOP 3;
		HPLD Q -1;
		Stop;
	Spawn2:
		HLI1 A 0 A_SetScale(1);
		HLI1 A 1 A_JumpIf(vel.x!=0||vel.y!=0,"See");
		Goto Spawn;
	See2:
		HLW1 A 0 A_SetScale(1);
		HLW1 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 A_JumpIf(vel.x==0&&vel.y==0,"Spawn");
		Goto See;
	Melee2:
		HLI1 B 12;
		Goto Spawn;
	Missile2:
		HLI1 C 12;
		Goto Spawn;
	Pain2:
		HLP1 A 1 A_PlaySound("HLPlayerScientistPain",CHAN_VOICE);
		HLP1 BCDEFGH 1;
		Goto Spawn;
	Death2:
		HLD1 A 1 A_PlaySound("HLPlayerScientistDeath",CHAN_VOICE);
		HLD1 BC 1;
		HLD1 D 1 A_NoBlocking;
		HLD1 EFGHIJKLMNOPQRS 1;
		HLD1 T 1 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 U 1;
		HLD1 V -1;
		Stop;
	}
}
class ScientistDisguise : Inventory
	{Default{+INVENTORY.UNDROPPABLE}}