//============================
// Explosive barrels & crates
//============================

Actor ExplosiveItemBang
{
	Radius 4
	Height 4
	Renderstyle Translucent
	Alpha 0.9
	+dontsplash
	+noclip
	States
	{
	Spawn:
		CBNG ABCDEFGHIJKLMNOPQRS 2 Bright
		CBNG TU 2
		stop
	}
}
Actor ExplosiveItemBangCrate : ExplosiveItemBang
{
	Scale 0.6
}
Actor ExplosiveItemBangBarrel : ExplosiveItemBang
{
	Scale 0.4
}

ACTOR Xbarrel1 30100
{
	Health 50
	Radius 12
	Height 32
	Mass 100
	DeathSound "world/barrelx"
	Obituary "%o stood too close to an exploding barrel."
	+PUSHABLE
	+SHOOTABLE
	+SOLID
	+NOBLOOD
	States
	{
	Spawn:
		POSS A -1
		Stop
	Death:
		POSS B 0 A_SpawnItemEx ("ExplosiveItemBangBarrel", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
		POSS B 2 A_AlertMonsters
		POSS C 2 A_Scream
		POSS D 1 A_Explode
		POSS D 1 A_SetTranslucent(0.5, 0)
		POSS E 1 A_SetTranslucent(0.4, 0)
		POSS E 1 A_SetTranslucent(0.3, 0)
		POSS F 1 A_NoBlocking
		POSS F 1 A_SetTranslucent(0.3, 0)
		POSS G 1 A_SetTranslucent(0.2, 0)
		POSS G 1 A_SetTranslucent(0.1, 0)
		Stop
	}
}
ACTOR Xbarrel2 : Xbarrel1 30101
{
	Health 20
}
ACTOR Xbarrel3 : Xbarrel1 30102
{
	Health 30
}

ACTOR Xcrate01 : Xbarrel1 30110
{
	-PUSHABLE
	Health 30
	Radius 32
	Height 64
	Mass 200
	Obituary "%o stood too close to an exploding crate."
	States
	{
	Death:
		POSS B 0 A_SpawnItemEx ("ExplosiveItemBangCrate", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
		Goto Super::Death+1
	}
}
ACTOR Xcrate02 : Xcrate01 30111{}

//====================
// General Explosions
//====================

Actor GeneralExplosion
{
	Projectile
	+NOCLIP
	SeeSound "halflife/weapons/randomexplo"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(255,1,2,3,4)
		TNT1 A 10 A_CustomMissile("GeneralExplosion1",0,0,0)
		Stop
		TNT1 A 10 A_CustomMissile("GeneralExplosion2",0,0,0)
		Stop
		TNT1 A 10 A_CustomMissile("GeneralExplosion3",0,0,0)
		Stop
		TNT1 A 10 A_CustomMissile("GeneralExplosion4",0,0,0)
		Stop
	}
}

Actor GeneralExplosion1
{
	Projectile
	Speed 0
	Damage 0
	+NOCLIP
	RenderStyle Add
	Scale 0.7
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360))
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeMedium",0,0,0,random(3,-3),random(3,-3),random(3,-3),random(0,359),0)
		X005 ABCDEFGHIJKLM 1 Bright A_FadeOut(0.01)
		X005 NOPQRSTUVWX 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor GeneralExplosion2 : GeneralExplosion1
{
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360))
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeMedium",0,0,0,random(2,-2),random(2,-2),random(2,-2),random(0,359),0)
		X003 ABCDEFGHIJKLMNO 1 Bright A_FadeOut(0.01)
		X003 PQRSTUVWXYZ 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor GeneralExplosion3 : GeneralExplosion1
{
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360))
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeMedium",0,0,0,random(2,-2),random(2,-2),random(2,-2),random(0,359),0)
		X004 ABCDE 1 Bright A_FadeOut(0.01)
		X004 FGHIJKLMNOPQ 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor GeneralExplosion4 : GeneralExplosion1
{
	Scale 1.4
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,2),random(1,2),random(-1,3),random(1,360))
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeMedium",0,0,0,random(2,-2),random(2,-2),random(2,-2),random(0,359),0)
		X002 ABCDEFGHIJKLMN 1 Bright A_FadeOut(0.01)
		X002 OPQRSTU 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor GeneralExplosionSmokeMedium
{
	Scale 0.45
	RenderStyle Add
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetTranslucent(0.5)
		VAPB ABCDEFGHI 3 Bright A_FadeOut(0.08)
		Wait
	}
}

Actor RocketSmoke : GeneralExplosionSmokeMedium
{
	Scale 0.2
}

Actor GeneralExploXtraSparks
{
	Height 4
	Radius 4
	Speed 3
	Scale 0.5
	+NOCLIP
	PROJECTILE
	-NOGRAVITY
	+DONTBLAST
	+LOWGRAVITY
	//Gravity 0.5
	+DONTSPLASH
	RenderStyle Add
	States
	{
	Spawn:
		WRBL K 18 Bright
		WRBL K 1 Bright A_FadeOut
		Wait
	}
}