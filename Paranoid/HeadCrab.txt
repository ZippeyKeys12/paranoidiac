//===========
// Head Crab
//===========

class HLHeadCrab : HLMonster
{
	bool crabjump;
	int drowncounter;
	Default
	{
		Health 10;
		Speed 1.6;
		Height 25;
		Radius 8;
		PainChance 200;
		PainChance "Crowbar",256;
		Scale .25;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/headcrab/sight";
		PainSound "halflife/monsters/headcrab/pain";
		DeathSound "halflife/monsters/headcrab/death";
		ActiveSound "halflife/monsters/headcrab/idle";
		maxdropoffheight 384;
		+DONTHARMCLASS;
		DamageFactor "Gonarch",0;
		Species "HLHeadCrab";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 3 A_Look;
		Loop;
	See:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		TNT1 A 0 A_JumpIf(crabjump,"SeeMelee");
		HLBW A 1 A_Chase(null,"Missile");
		HLBW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW B 1 A_Chase(null,"Missile");
		HLBW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
	SeeCont:
		HLBW C 1 A_Chase;
		HLBW CCCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW D 1 A_Chase;
		HLBW DDDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW E 1 A_Chase;
		HLBW EEEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW F 1 A_Chase;
		HLBW FFFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW G 1 A_Chase;
		HLBW GGGG 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	SeeMelee:
		HLBW A 1 A_Chase("Melee",null);
		HLBW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW B 1
		{
			crabjump=0;
			A_Chase("Melee",null);
		}
		HLBW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto SeeCont;
	Melee:
		HLBW A 10 A_HeadCrabMelee;
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 4 A_FaceTarget;
		HLBA A 3 A_PlaySound("halflife/monsters/headcrab/jump");
		HLBA B 3;
		HLBA C 3 A_HeadCrabAttack(5,8.75);
		HLBA D 3;
		HLBA E 3 A_CheckFloor(1);
		Wait;
		WLBA F 30 A_Stop;
		WLBA G 30;
		Goto See;
	Pain:
		HLBP A 8
		{
			crabjump=0;
			A_Pain();
		}
		Goto See;
	Drown:
		HLBP A 12
		{
			if(drowncounter>=3)
				A_Die();
			else
			{
				A_Pain();
				drowncounter++;
				A_SetTics(random(3,5)*4);
			}
		}
		Goto See;
	Death:
		HLBD A 2 A_Scream;
		HLBD B 2 A_NoBlocking;
		HLBD CDEFGHIJ 3;
		HLBD K 2
		{
			if(waterlevel>=3)
				A_ChangeVelocity(z:.75);
		}
		Wait;
	XDeath:
		TNT1 A 0
		{
			A_XenGibs(2);
			for(int i=0;i<4;i++)
				A_SpawnItemEx("XenGibsMini",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		}
		Stop;
	}
	void A_HeadCrabMelee()
		{A_CustomMeleeAttack(random(1,8),"halflife/monsters/headcrab/meleehit");}
	void A_HeadCrabAttack(float min,float max)
	{
		let self=HLHeadCrab(self);
		self.crabjump=1;
		A_SkullAttack();
		A_ChangeVelocity(z:frandom(min,max));
	}
}

class HLBabyHeadCrab : HLHeadCrab
{
	Default
	{
		Height 8;
		-Solid
		RenderStyle "Translucent";
		Alpha .9;
		MaxTargetRange 128;
		Scale .09;
		maxdropoffheight 24;
		Species "HLBabyHeadCrab";
	}
	States
	{
	Spawn:
		HLQI A 3 A_Look;
		Loop;
	See:
		TNT1 A 0 A_JumpIf(crabjump,"SeeMelee");
		HLQW A 1 A_Chase(null,"Missile");
		HLQW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW B 1 A_Chase(null,"Missile");
		HLQW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
	SeeCont:
		HLQW C 1 A_Chase(null,"Missile");
		HLQW CCCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW D 1 A_Chase(null,"Missile");
		HLQW DDDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW E 1 A_Chase(null,"Missile");
		HLQW EEEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	SeeMelee:
		HLQW A 1 A_Chase("Melee",null);
		HLQW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW B 1
		{
			crabjump=0;
			A_Chase("Melee",null);
		}
		HLQW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto SeeCont;
	Melee:
		HLQW A 10 A_HeadCrabMelee;
		Goto See;
	Missile:
		HLQI A 4 A_FaceTarget;
		HLQM AB 3;
		HLQM C 6 A_HeadCrabAttack(2.5,6.25);
		HLQM C 3 A_CheckFloor(1);
		Wait;
		HLQM D 60 A_stop;
		Goto See;
	Pain:
		HLQP A 8 {crabjump=0;}
		Goto See;
	Death:
	XDeath:
		TNT1 A 0
		{
			for(int i=0;i<4;i++)
				A_SpawnItemEx("XenGibsMini",random(2,-2),random(2,-2),random(0,20),random(1,4),random(1,4),random(2,6),random(0,359));
		}
		Stop;
	}
}

class HLBabyHeadCrab2 : HLBabyHeadCrab	//for the gonarch
{
	Default
	{
		Speed 3.333333;
	}
	States
	{
	See:
		TNT1 A 0 A_JumpIf(crabjump,"SeeMelee");
		HLQW A 1 A_Chase(null,"Missile");
		HLQW AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW B 1 A_Chase(null,"Missile");
		HLQW BB 1 A_Chase(null,null,HLSMOOTHCHASE);
	SeeCont:
		HLQW C 1 A_Chase(null,"Missile");
		HLQW CC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW D 1 A_Chase(null,"Missile");
		HLQW DD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW E 1 A_Chase(null,"Missile");
		HLQW EE 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	SeeMelee:
		HLQW A 1 A_Chase("Melee",null);
		HLQW AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLQW B 1
		{
			crabjump=0;
			A_Chase("Melee",null);
		}
		HLQW BB 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto SeeCont;
	}
}