//===========
// Head Crab
//===========

class HLHeadCrab : HLMonster
{
	int drowncounter;
	override bool Slam(Actor victim)
	{
		A_PlaySound("halflife/monsters/headcrab/meleehit",CHAN_BODY);
		return super.slam(victim);
	}
	Default
	{
		Damage 1;
		Health 10;
		Speed 1.6;
		Height 25;
		Radius 8;
		PainChance 200;
		PainChance "Crowbar",256;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/headcrab/sight";
		PainSound "halflife/monsters/headcrab/pain";
		DeathSound "halflife/monsters/headcrab/death";
		ActiveSound "halflife/monsters/headcrab/idle";
		MaxDropoffHeight 384;
		+DONTHARMCLASS;
		DamageFactor "Gonarch",0;
		Species "HLHeadCrab";
	}
	States
	{
	Spawn:
		HLI1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLI1 A 3 A_Look;
		Loop;
	See:
		HLR1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLR1 A 1 A_Chase;
		HLR1 BCDE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 F 1 A_Chase;
		HLR1 GHIJ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 K 1 A_Chase;
		HLR1 LMNO 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 P 1 A_Chase;
		HLR1 QRST 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 U 1 A_Chase;
		HLR1 VWXY 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 Z 1 A_Chase;
		HLR1 [\] 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		HLA1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLA1 A 1 A_FaceTarget;
		HLA1 CEG 1;
		HLA1 I 1 A_PlaySound("halflife/monsters/headcrab/jump");
		HLA1 KMOQS 1;
		HLA1 U 1 A_HeadCrabAttack(5,8.75);
		HLA1 WY[] 1;
		HLA1 \ 3 A_CheckFloor(1);
		Wait;
		HLA1 ] 60 A_stop;
		Goto See;
	Pain:
		HLP1 A 1 A_Pain;
		HLP1 CEGIKMO 1;
		Goto See;
	Drown:
		HLI1 A 12
		{
			if(drowncounter>=3)
				A_Die();
			else
			{
				A_Pain();
				drowncounter++;
				A_SetTics(random(3,5)*4);
			}
		}
		Goto See;
	Death:
		HLD1 A 1 A_Scream;
		HLD1 B 1;
		HLD1 C 1 A_NoBlocking;
		HLD1 DEFGHIJKLMNOPQRSTUVWXYZ[\ 1;
		HLD1 ] 2
		{
			if(waterlevel>=3)
				vel.z+=.75;
		}
		Wait;
	XDeath:
	Crush:
		TNT1 A 0
		{
			A_HLGibs(2,"XenGibs");
			scale=(.75,.75);
			for(int i=0;i<4;i++)
				A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359),SXF_TRANSFERSCALE);
		}
		Stop;
	}
	void A_HeadCrabAttack(float min,float max)
	{
		A_SkullAttack();
		vel.z+=frandom(min,max);
	}
}

class HLBabyHeadCrab : HLHeadCrab
{
	bool gonarchbaby;
	Default
	{
		Height 8;
		-SOLID
		RenderStyle "Translucent";
		Alpha .9;
		MaxTargetRange 128;
		maxdropoffheight 24;
		PainSound "";
		DeathSound "";
		Species "HLBabyHeadCrab";
	}
	States
	{
	Spawn:
		Goto HLMonster::Spawn;
	See:
		HLW1 A 0 A_JumpIf(gonarchbaby,"See2");
		HLW1 A 1 A_Chase;
		HLW1 CEGI 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 K 1 A_Chase;
		HLW1 MOQS 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 U 1 A_Chase;
		HLW1 WY[] 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 B 1 A_Chase;
		HLW2 DF 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	See2:
		HLW1 A 1 A_Chase;
		HLW1 CE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 G 1 A_Chase;
		HLW1 IK 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 M 1 A_Chase;
		HLW1 OQ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 S 1 A_Chase;
		HLW1 UW 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 Y 1 A_Chase;
		HLW1 [] 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 B 1 A_Chase;
		HLW2 DF 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		HLA1 A 1 A_FaceTarget;
		HLA1 CEGIKMOQS 1;
		HLA1 U 1 A_HeadCrabAttack(2.5,6.25);
		HLA1 WY[] 1;
		HLA1 \ 3 A_CheckFloor(1);
		Wait;
		HLA1 ] 60 A_stop;
		Goto See;
	XDeath:
		TNT1 A 0
		{
			scale=(.75,.75);
			for(int i=0;i<4;i++)
				A_SpawnItemEx("XenGibs",random(2,-2),random(2,-2),random(0,20),random(1,4),random(1,4),random(2,6),random(0,359),SXF_TRANSFERSCALE);
		}
		Stop;
	}
}