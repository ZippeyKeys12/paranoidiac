//===========
// Gargantua
//===========

class HLGargantua : HLMonster
{
	bool flametoken;
	float defspeed;
	override void BeginPlay()
	{
		defspeed=speed;
		Super.BeginPlay();
	}
	Default
	{
		Health 10000;
		Mass 0x7FFFFFFF;
		Speed 21;
		Height 170;
		Radius 57;
		+BOSS
		+NORADIUSDMG
		PainChance 5;
		PainChance "Crowbar",29;
		DamageFactor "Hornet",0;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/gargantua/sight";
		PainSound "halflife/monsters/gargantua/pain";
		DeathSound "halflife/monsters/gargantua/death";
		ActiveSound "halflife/monsters/gargantua/idle";
		Species "HLGargantua";
	}
	States
	{
	See:
		HLR1 A 0 A_JumpIf(flametoken,"FlameEnd");	//to stop the looping sound in case player dies while "Flame"
		HLR1 A 0 A_JumpIfCloser(400,"Walk");		//if is close enough, the gargantua will walk instead of run
		HLR1 A 0 A_SetSpeed(defspeed/3.0);
		HLR1 A 1 A_Chase;
		HLR1 BC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 D 1 A_Chase;
		HLR1 EF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 G 1 A_Chase;
		HLR1 HI 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 J 1 A_Chase();
		HLR1 K 1
		{
			A_PlaySound("halflife/monsters/gargantua/step",7);
			A_Chase(null,null,HLSMOOTHCHASE);
		}
		HLR1 L 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 M 1 A_Chase;
		HLR1 NO 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 P 1 A_Chase;
		HLR1 QR 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 S 1 A_Chase();
		HLR1 T 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 A 0 A_PlaySound("halflife/monsters/gargantua/step",7);
		Goto See+2;
	Walk:
		HLW1 A 1
		{
			A_SetSpeed(defspeed/5.0);
			A_Chase();
		}
		HLW1 ABBC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 C 1 A_Chase;
		HLW1 DDEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 F 1 A_Chase;
		HLW1 FGGH 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 H 1 A_Chase;
		HLW1 IIJJ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 K 1 A_Chase;
		HLW1 KLLM 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 M 1 A_Chase;
		HLW1 NNOO 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 P 1 A_Chase;
		HLW1 P 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 A 0 A_PlaySound("halflife/monsters/gargantua/step",7);
		HLW1 QQR 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 R 1 A_Chase;
		HLW1 SSTT 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 U 1 A_Chase;
		HLW1 UVVW 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 W 1 A_Chase;
		HLW1 XXYY 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 Z 1 A_Chase;
		HLW1 "Z[[" 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 \ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 \ 1 A_Chase;
		HLW1 "]]" 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 B 1 A_Chase;
		HLW2 BCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 A 0 A_PlaySound("halflife/monsters/gargantua/step",7);
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIfCloser(250,"Flame");
		TNT1 A 0 A_Jump(256,"Boom","Hornets","EnergyBalls","EnergyBallsSeek");
	Boom:
		HLI1 A 8 A_FaceTarget;
		HLA1 ABCDEFGHIJKLMNOPQRS 1 A_FaceTarget;
		HLA1 T 1
		{
			A_PlaySound("halflife/monsters/gargantua/stomp",7,1,0,ATTN_NONE);
			Radius_Quake(8,20,0,15,0);
			A_SpawnItemEx("GargFloorBoomFake",10);
		}
		HLA1 T 22;
		HLA1 T 1
		{
			if(defspeed==21)
				A_SpawnProjectile("GargFloorBoomSeeker",0);	//aimed at target
			else
				A_SpawnItemEx("GargFloorBoom",10,0,0,20);	//where it aimed first
		}
		HLA1 SQOMKIGECA 1;
		Goto See;
	Hornets:
		HLA2 ABCD 2 A_FaceTarget;
		HLA2 EFGHIJKLMNOPSS 1 A_FaceTarget;
		HLA2 SSSSSSSSS 5 A_GargProjectile("HGruntHornet");
		HLA2 SSPONMLKJIHGFEDCBA 1;
		Goto See;
	Flame:
		HLA2 A 1 A_PlaySound("halflife/monsters/gargantua/flameon",5);
		HLA2 BCDEFGHIJKLMNOP 1 A_FaceTarget;
		HLA2 S 0
		{
			A_PlaySound("halflife/monsters/gargantua/flameloop",6,1,1);
			flametoken=1;
		}
	FlameLoop:
		HLA2 SSSSS 1 A_GargProjectile("HLGargFlame");
		HLA2 S 0 A_CPosRefire;
		HLA2 S 0 A_JumpIfCloser(300,"FlameLoop");
	FlameEnd:
		HLA2 P 1
		{
			A_StopSound(6);
			A_PlaySound("halflife/monsters/gargantua/flameoff",5);
			flametoken=0;
		}
		HLA2 ONMLKJIHGFEDCBA 1;
		Goto See;
	EnergyBalls:
		HLA2 ABCDE 2 A_FaceTarget;
		HLA2 FGHI 1 A_FaceTarget;
		HLA2 J 1 A_GargDischarge;
		HLA2 KLMNOPSS 1 A_FaceTarget;
		HLA2 SSSSSSSSSSSSSSSS 5 A_GargControllerBall;
		HLA2 SSPONMLKJIHGFEDCBA 1;
		Goto See;
	EnergyBallsSeek:
		HLA2 ABCDE 2 A_FaceTarget;
		HLA2 FGHI 1 A_FaceTarget;
		HLA2 J 1 A_GargDischarge;
		HLA2 KLMNOPSS 1 A_FaceTarget;
		HLA2 S 5 A_GargControllerBall(2);
		HLA2 SSSSSS 5 A_FaceTarget;
		HLA2 S 7 A_GargDischarge;
		HLA2 S 5 A_GargControllerBall(2);
		HLA2 SSSSSS 5 A_FaceTarget;
		HLA2 S 7 A_GargDischarge;
		HLA2 S 5 A_GargControllerBall(2);
		HLA2 SSPONMLKJIHGFEDCBA 1;
		Goto See;
	Pain:
		HLP1 A 0 A_Pain;
		HLP1 A 1 A_JumpIf(flametoken,"FlameEnd");
		HLP1 BCDEFGH 1;
		Goto See;
	Death:
		HLD1 A 1
		{
			A_StopSound(6);
			A_Scream();
		}
		HLD1 BCDE 1;
		HLD1 F 1 A_NoBlocking;
		HLD1 "GHIJKLMNOPQRSTUVWXYZ[\]" 1;
		HLD2 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 1;
		HLD3 ABCDEF 1;
		HLD3 G 1 A_PlaySound("halflife/monsters/gonarch/step",7);
		HLD3 HIJKLMNOP 1;
		HLD3 Q 100;
		TNT1 A 10 A_XenGibsBig;
		Stop;
	XDeath:
		HLD1 A 1
		{
			A_StopSound(6);
			A_Scream();
		}
		HLD1 BCDEFGHIJKLMNOPQRSTUVWXY 1;
		HLD1 ZZZZZZZ 1 {scale.x+=.025;}
		TNT1 A 10 A_XenGibsBig;
		Stop;
	}
	void A_GargProjectile(class<actor> proj)
	{
		float a=80*scale.x,b=90*scale.x;
		A_FaceTarget();
		A_SpawnProjectile(proj,a,0-b);
		A_SpawnProjectile(proj,a,b);
	}
	void A_GargControllerBall(int mode=0)
	{
		class<actor> ball;
		if(mode==2)
			ball="GargControllerFireball2";
		else
			ball="GargControllerFireball1";
		A_FaceTarget();
		A_SpawnProjectile(ball,90,-66,random(8,-8));
		A_SpawnProjectile(ball,90,66,random(8,-8));
	}
	void A_GargDischarge()
	{
		A_FaceTarget();
		A_PlaySound("halflife/weapons/shockroachdischarge");
	}
}

//=================
// Young Gargantua
//=================

class HLGargantua2 : HLGargantua
{
	Default
	{
		Health 8000;
		Speed 18;
		Height 128;
		Radius 40;
		Scale .75;
	}
	States
	{
	Missile:
		TNT1 A 0 A_JumpIfCloser(250,"Flame");
		TNT1 A 0 A_Jump(256,"Boom","Hornets");
	}
}

//********************
//	Flame attack
//********************

class HLGargFlame : Actor
{
	Default
	{
		Radius 2;
		Height 4;
		Speed 16;
		Projectile;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha .67;
		Scale .5;
		+THRUGHOST
		+DONTSPLASH
		seesound "gargantua/flamestrm";
		DamageFunction (random(1,4)*2);
		Translation "48:63=200:193","64:79=213:220";
	}
	States
	{
	Spawn:
		TNT1 A 1;
		FRFX ABCD 1 Bright A_Explode(3,6);
		FRFX D 0 Bright A_Lowgravity;
		FRFX EFG 1 Bright A_Explode(3,12);
		FRFX HIJ 1 Bright A_Explode(3,24);
		FRFX KLM 1 Bright A_Explode(3,48);
		FRFX NO 1 Bright;
		stop;
	Death:
		FRFX HIJ 2 Bright A_Explode(3,24);
		FRFX KLM 2 Bright A_Explode(3,48);
		FRFX NO 2 Bright;
		stop;
	}
}

//*******************
//	Floor attack
//*******************

class GargFloorBoomFake : Actor
{
	Default
	{
		Projectile;
		Damage 15;
		+RIPPER
		Scale .65;
		+FLOORHUGGER
		RenderStyle "Add";
		Alpha .9;
	}
	States
	{
	Spawn:
		X036 BCDEFGHIJKLMNOPQRSTUVWXY 1 Bright LIGHT("RedMediumPoint2") NODELAY A_GargFXtra;
		TNT1 A 0
		{
			if(speed==0)
				return resolvestate("Death");
			return state(null);
		}
		Loop;
	Death:
		TNT1 A 1;
		Stop;
	}
	void A_GargFXtra()
	{
		A_Explode(64,64,0);
		for(int i=0;i<3;i++)
			A_SpawnItemEx("GargFXtra",0,0,0,random(5,-5),random(5,-5),random(1,6));
	}
}

class GargFloorBoom : GargFloorBoomFake			//stays still a little
	{Default{Speed 10;}}

class GargFloorBoomSeeker : GargFloorBoom
{
	Default
	{
		+SeekerMissile
	}
	States
	{
	Spawn:
		X036 BCDEFGH 1 BRIGHT NODELAY LIGHT("RedMediumPoint2") A_GargFXtra;
		X036 I 1 BRIGHT LIGHT("RedMediumPoint2")
		{
			A_SeekerMissile(10,10);
			A_GargFXtra();
		}
		X036 JKLMNOP 1 BRIGHT LIGHT("RedMediumPoint2") A_GargFXtra;
		X036 Q 1 BRIGHT LIGHT("RedMediumPoint2")
		{
			A_SeekerMissile(10,10);
			A_GargFXtra();
		}
		X036 RSTUVWXY 1 BRIGHT LIGHT("RedMediumPoint2") A_GargFXtra;
		TNT1 A 0 A_SeekerMissile(10,10);
		Loop;
	}
}

class GargFXtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Radius 1;
		+DONTBLAST
		Height 1;
		Scale .15;
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		STAR P 3 BRIGHT A_Jump(128,1);
		Loop;
		STAR P 3 Bright;
		STAR P 1 Bright A_FadeOut;
		Wait;
	Death:
		TNT1 A 1;
		Stop;
	}
}

//******************************
// Gargantua controller fireball
//******************************
class GargControllerFireball1 : ControllerFireball1
{
	Default
	{
		Speed 20;
		+FORCEXYBILLBOARD
	}
}

//*******************************
// Gargantua cont seeker fireball
//*******************************
class GargControllerFireball2 : ControllerFireball2
{
	Default
	{
		Speed 12;
		Damage 15;
	}
}