class ZenHair : Actor
{
	Default
	{
		Radius 4;
		Height 20;
		Mass 0x7FFFFFFF;
		+FLOORCLIP
		+RANDOMIZE
	}
	States
	{
	Spawn:
		POSS A 35;
		POSS ABCDEFGHI 6;
		Goto Spawn+1;
	}
}

class ZenLight : ZenHair
{
	Default
	{
		Radius 8;
		Height 32;
		Health 10000;
		PainChance 256;
		PainSound "misc/chat";
		+SHOOTABLE
		+NOTAUTOAIMED
		+NOBLOOD
		+NORADIUSDMG
		+NODAMAGE
		+SOLID
	}
	States
	{
	Spawn:
		POSS AB 6 Bright LIGHT("ZENLIGHT1") ZenLightLook;
		POSS CD 6 Bright LIGHT("ZENLIGHT2") ZenLightLook;
		POSS EFG 6 Bright LIGHT("ZENLIGHT3") ZenLightLook;
		POSS HI 6 Bright LIGHT("ZENLIGHT2") ZenLightLook;
		POSS JK 6 Bright LIGHT("ZENLIGHT1") ZenLightLook;
		Loop;
	Shrink:
		POSS LM 3 Bright LIGHT("ZENLIGHTFADE1");
		POSS NO 3 Bright LIGHT("ZENLIGHTFADE2");
		POSS P 3;
	Shrunk1:
		POSS Q 8 A_CheckRange(128,"Grow");
		Loop;
	Grow:
		POSS Q 70;
		POSS P 3;
		POSS ON 3 Bright LIGHT("ZENLIGHTFADE2");
		POSS ML 3 Bright LIGHT("ZENLIGHTFADE1");
		Goto Spawn;
	Pain:
		POSS Q 24;
		Goto Shrunk1;
	}
	state ZenLightLook()
		{return CheckRange(128)?null:ResolveState("Shrink");}
}

class ZenTree : Actor
{
	Default
	{
		Height 144;
		Mass 0x7FFFFFFF;
		MeleeRange 128;
		PainChance 128;
		PainChance "Crowbar",252;
		PainSound "tree/pain";
		+NOTAUTOAIMED
		+SHOOTABLE
		+NOBLOOD
		+NORADIUSDMG
		+NODAMAGE
		+SOLID
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_TreeLook();
		HLI2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_TreeLook();
		HLI3 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_TreeLook();
		HLI4 ABCDEFGHIJKLMN 2 A_TreeLook();
		Loop;
	Melee:
		HLA1 ABCDEFGHI 1;
		HLA1 J 1 A_XenTreeAttack;
		HLA1 KLMNOPQRSTUVWXYZ[\] 1;
		HLA2 AB 1;
		Goto Spawn;
	Pain:
		HLA1 A 1 A_Pain;
		HLA1 BCDEFEDCBA 1;
		Goto Spawn;
	}
	void A_TreeLook()
		{A_LookEx(LOF_NOSOUNDCHECK,0,112,112,60,"Melee");}
	void A_XenTreeAttack()
	{
		for(int i=0;i<12;i++)
		{
			if(TryPunch(angle+i*(45./12))||TryPunch(angle-i*(45./12)))
				return;
		}
	}
	private action bool TryPunch(double angle)
	{
		FTranslatedLineTarget t;
		double slope=AimLineAttack(angle,112,t);
		if(t.linetarget!=null)
		{
			LineAttack(angle,112,slope,10,'Melee',"BulletPuff",true,t);
			if(t.linetarget!=null)
			{
				A_PlaySound("tree/attack");
				return true;
			}
		}
		A_PlaySound("tree/swipe");
		return false;
	}
}

class Fungus : ModelBase
{
	Default
	{
		Radius 32;
		Height 72;
		Mass 0x7FFFFFFF;
		+NOTAUTOAIMED
		+FLOORCLIP
		+SHOOTABLE
		+NOBLOOD
		+NODAMAGE
	}
}

class HeadCrab : Fungus //Dissected HeadCrab Model
{
	Default
	{
		Radius 8;
		Height 16;
		Mass 100;
	}
}