//==============
// Hornet Grunt
//==============

actor HLHornetGrunt 7416
{
	Monster
	Health 500
	Mass 800
	Speed 10
	Height 64
	PainChance 40
	Scale 0.47
	BloodColor "DE FE 72"
	DamageFactor "Hornet", 0.0
	SeeSound "halflife/monsters/hornetgrunt/sight"
	PainSound "halflife/monsters/hornetgrunt/pain"
	DeathSound "halflife/monsters/hornetgrunt/death"
	ActiveSound "halflife/monsters/hornetgrunt/idle"
	States
	{
	Spawn:
		HLOI A 3 A_Look
		Loop
	See:
		HLOW ABCDEF 4 A_Chase
		Loop
	Melee:
		HLOM ABCD 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/attswing")
		HLOM E 5 A_CustomMeleeAttack(12*random(1,8),"halflife/monsters/zombie/hit")
		HLOM F 5
		Goto See
	Missile:
		HLON AB 5 A_FaceTarget
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		TNT1 A 0 A_Jump(80,"See")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		TNT1 A 0 A_Jump(100,"See")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		TNT1 A 0 A_Jump(120,"See")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		TNT1 A 0 A_Jump(140,"See")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		Goto See
	Pain:
		HLOP A 8 A_Pain
		Goto See
	Death:
		HLOD A 2 A_Scream
		HLOD B 2 A_NoBlocking
		HLOD CDE 3
		HLOD F 3 A_PlaySound("halflife/weapons/bodydrop")
		HLOD GH 3
		HLOD I -1
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath")
		TNT1 A 0 A_NoBlocking
	  	TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359))
		Stop
	}
}

Actor HGruntHornet
{
	Radius 8
	Height 12
	Projectile
	Speed 17
	Damage 4
	Scale 0.085
	DamageType "Hornet"
	+SEEKERMISSILE
	SeeSound "halflife/monsters/hornetgrunt/flysight"
	States
	{
	Spawn:
		HLOH B 1 NODELAY A_SeekerMissile(2,2)
		X099 A 0 A_SpawnItemEx("HGHornetTrail", -2, 0, 0, 0, 0, 0, 0, 160)
		Loop
	Death:
	Crash:
		TNT1 A 0 A_SpawnItemEx("HGHornetShooter1")
		TNT1 A 1 A_PlaySound("halflife/monsters/hornetgrunt/flybuzz")
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("halflife/monsters/hornetgrunt/flyhit")
		Stop
	}
}

actor HGHornetTrail
{
	radius 6
	height 8
	+NOINTERACTION
	Scale 0.015
	RenderStyle Add
	Translation "80:111=160:167"
	states
	{
	Spawn:
		X099 A 1 A_FadeOut (0.03)
		loop
	}
}

Actor HGHornetShooter1
{
	+FRIENDLY
	MONSTER
	Radius 1
	Height 1
	+NOGRAVITY
	+NOCLIP
	+LOOKALLAROUND
	+MISSILEMORE
	+MISSILEEVENMORE
	-SOLID
	-SHOOTABLE
	-COUNTKILL
	MinMissileChance 0
	ReactionTime 25
	States
	{
	Spawn:
		TNT1 A 0 A_CountDown
		TNT1 A 1 A_Look
		//loop
	See:
		TNT1 A 0 A_Chase
	Missile:
		TNT1 A 0 A_JumpIfCloser(128,"SuccessfulChase")
		TNT1 A 0 A_Jump(128,"NinetyNeg")
		TNT1 A 0 A_SpawnItemEx("HGruntHornetFake",0,0,0,10,0,random(2,-2),90,0)
		Stop
	NinetyNeg:
		TNT1 A 0 A_SpawnItemEx("HGruntHornetFake",0,0,0,10,0,random(5,-5),-90,0)
		Stop
	SuccessfulChase:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("HGruntHornetNoSightSound",0,0,0)
		Stop
	}
}

Actor HGruntHornetNoSightSound : HGruntHornet
{
	SeeSound ""
	States
	{
	Death:
	Crash:
//		TNT1 A 0 A_SpawnItemEx("HGHornetShooter1",0,0,0,0,0,0,0,32)
		TNT1 A 1 A_PlaySound("halflife/monsters/hornetgrunt/flybuzz")
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("halflife/monsters/hornetgrunt/flyhit")
		Stop
	}
}

Actor HGruntHornetFake : HGruntHornet
{
	SeeSound ""
	States
	{
	Spawn:
		TNT1 A 0
		HLOH B 1 A_JumpIfInventory("HornetCounter",20,"Death")
		TNT1 A 0 A_GiveInventory("HornetCounter",1)
		X099 A 0 A_SpawnItemEx("HGHornetTrail", -2, 0, 0, 0, 0, 0, 0, 160)
		Loop
	Death:
		TNT1 A 1 A_PlaySound("halflife/monsters/hornetgrunt/flybuzz")
		Stop
	}
}

Actor HornetCounter : Inventory{ Inventory.MaxAmount 20 }