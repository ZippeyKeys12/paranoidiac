class HLBat : HLMonster
{
	Default
	{
		Health 200;
		Speed 3;
		FloatSpeed 1;
		Height 76;
		Radius 60;
		Mass 400;
		PainChance 160;
		PainChance "Crowbar",255;
		Scale .6;
		+FLOAT
		+NOGRAVITY
		Translation "16:47=64:79","64:79=96:111";
		BloodColor "DE FE 72";
		SeeSound "stukabat/sight";
		PainSound "stukabat/pain";
		DeathSound "stukabat/death";
		ActiveSound "stukabat/active";
		AttackSound "stukabat/attack";
		Obituary "%o was fried by a StukaBat.";
		Species "HLBat";
	}
	States
	{
	Spawn:
		HLSX ABCDEFG 2 A_Look;
		Loop;
	See:
		HLSW A 1 A_Chase;
		HLSW AAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLSW B 1 A_Chase;
		HLSW BBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLSW C 1 A_Chase;
		HLSW CCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLSW D 1 A_Chase;
		HLSW DDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLSW E 1 A_Chase;
		HLSW EEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLSW F 1 A_Chase;
		HLSW FFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		HLSM ABC 4 A_FaceTarget;
		HLSM D 4
		{
			for(int i=0;i<7;i++)
				A_SpawnItemEx("SBFireExtra2",0,0,20,random(2,5),random(2,-2),random(1,3));
			A_SpawnProjectile("StukaFireBall");
		}
		HLSM EF 4;
		Goto See;
	Pain:
		HLSX B 8;
		Goto See;
	Death:
		HLSD A -1 A_Scream;
		Stop;
	Crash:
		HLSD B 2
		{
			A_PlaySound("halflife/weapons/bodydrop");
			A_NoBlocking();
		}
		HLSD CDEFGHIJK 2;
		HLSD K -1;
		Stop;
	XDeath:
	  	TNT1 A 0 A_XenGibs(14);
		Stop;
	}
}

class StukaFireBall : Actor
{
	Default
	{
		Projectile;
		Speed 25;
		Scale .35;
		Damage 10;
		RenderStyle "Add";
		Alpha .9;
		Translation "80:111=16:47";
	}
	States
	{
	Spawn:
		X022 ABCD 4;
		Loop;
	Death:
		TNT1 A 0
		{
			A_PlaySound("halflife/weapons/hl2explo1");
			for(int i=0;i<4;i++)
				A_SpawnItemEx("SBFireExtra",0,0,0,random(1,-1),random(1,-1),random(0,2));
		}
		Stop;
	}
}

class SBFireExtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Scale .15;
		RenderStyle "Add";
		Alpha .9;
		-NOGRAVITY
		Gravity .125;
		Translation "80:111=26:47";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(128,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

class SBFireExtra2 : SBFireExtra
{
	Default
	{
		Scale .26;
	}
}

class HLBossBat : HLBat
{
	Default
	{
		Health 1600;
		Speed 6;
		Height 128;
		Radius 104;
		Mass 600;
		PainChance 30;
		PainChance "Crowbar",135;
		Scale 1;
		Translation "None";
		SeeSound "bossbat/sight";
		PainSound "bossbat/pain";
		DeathSound "bossbat/death";
		ActiveSound "bossbat/active";
		AttackSound "bossbat/attack";
		Obituary "%o was iced by a Boss Bat.";
	}
	States
	{
	Missile:
		HLSM ABC 4 A_FaceTarget;
		HLSM D 2
		{
			A_SpawnProjectile("IceBall",32,-24);
			A_SpawnProjectile("IceBall",32,24);
		}
		HLSM D 4 A_SpawnProjectile("IceBall",8);
		HLSM EF 4;
		Goto See;
	}
}

class IceBall : Actor	//Based on Mancubus FireBalls
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 20;
		Damage 8;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		translation "0:255=%[0.0,0.0,0.0]:[0.25,1.0,1.5]";
		SeeSound "fatso/attack";
		DeathSound "fatso/shotx";
	}
	States
	{
	Spawn:
		MANF AB 4 Bright;
		Loop;
	Death:
		MISL B 8 Bright;
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}