//==============================================================
//Credit to CaptainToenail for the original hanger monster idea
//==============================================================

class HLBarnacle : Actor
{
	Default
	{
		obituary "%o was spat on by a Barnacle.";
		health 80;
		height 32;
		mass 0x7FFFFFFF;
		maxtargetrange 384;
		painchance 128;
		PainChance "Crowbar",252;
		BloodColor "DE FE 72";
		seesound "barnacle/sight";
		painsound "barnacle/pain";
		deathsound "barnacle/death";
		activesound "barnacle/active";
		Monster;
		+FLOORCLIP
		+NOGRAVITY
		+SPAWNCEILING
		+CEILINGHUGGER
		+DONTFALL
	}
	states
	{
	Spawn:
		POSS AAA 8 A_Look;
		POSS A 8 A_Jump(32,1);
		Loop;
		POSS BCB 4 A_Look;
		Loop;
	See:
		POSS AA 4 A_Chase;
		POSS A 4 A_Jump(96,1);
		Loop;
		POSS BCB 4 A_Chase;
		Loop;
	Missile:
		POSS DEF 3 A_FaceTarget;
		POSS G 6
		{
			for(int i=0;i<7;i++)
				A_SpawnItemEx("BarnacleSpitParticle",0,0,12,frandom(0.5,2),frandom(1.3,-1.3),frandom(-0.3,-1.2),random(0,359));
			A_PlaySound("barnacle/attack");
			A_SpawnProjectile("BarnacleSpitBall",12);
		}
		POSS HI 3;
		goto See;
	Pain:
		POSS J 2;
		POSS J 2 A_Pain;
		goto See;
	Death:
		POSS K 5 {bNOCLIP=1;}  //stops you being able to jump up and push the model into the ceiling
		POSS K 5 A_Scream;
		POSS L 5 A_NoBlocking;
		POSS LLL 2 A_SpawnItemEx("XenGibsRed",random(3,-3),random(3,-3),random(8,20),random(1,2),random(1,2),random(-1,-3),random(0,359));
		POSS MMM 2 A_SpawnItemEx("XenGibsRed",random(3,-3),random(3,-3),random(12,24),random(1,2),random(1,2),random(-1,-3),random(0,359));
		POSS NOPQR 5;
		POSS S -1;
		Stop;
	}
}

class BarnacleSpitBall : Actor
{
	Default
	{
		Radius 4;
		Height 6;
		Speed 10;
		FastSpeed 15;
		Mass 5;
		Damage 4;
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Alpha .8;
		DeathSound "barnaclespitball/splat";
		Scale .3;
		Translation "80:111=160:167";
		Projectile;
		+RANDOMIZE
		-NOGRAVITY
		Gravity .125;
	}
	states
	{
	Spawn:
		X022 AABBCCDD 1 A_SpawnItemEx("BarnacleTrail",frandom(-2.3,2.3),frandom(-2.3,2.3),frandom(-2.3,2.3));
		Loop;
	Death:
		TNT1 A 1
		{
			for(int i=0;i<8;i++)
				A_SpawnItemEx("BarnacleSpitParticle",0,0,0,random(1,-1),random(1,-1),random(0,2));
		}
		stop;
	}
}

class BarnacleSpitParticle : BarnacleSpitBall
{
	Default
	{
		Speed 0;
		FastSpeed 0;
		Damage 0;
		+NOCLIP
		-RANDOMIZE
		Scale .11;
		Alpha .67;
		DeathSound "";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(128,1);
		Loop;
		X023 A 1 A_FadeOut(.01);
		Wait;
	}
}
class BarnacleSpitParticle2 : BarnacleSpitParticle
{
	Default
	{
		Scale .23;
	}
}

class BarnacleTrail : BarnacleSpitParticle
{
	Default
	{
		-NOCLIP
		Scale 1;
		Radius 0;
		Height 1;
		Translation "112:127=162:167";
		Alpha .75;
		+dontsplash
		Gravity .4;
	}
	States
	{
	Spawn:
		TNT1 A 2;
		BSP2 A 1 A_FadeOut(.03);
		Wait;
	}
}