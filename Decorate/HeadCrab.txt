//===========
// Head Crab
//===========

actor HLHeadcrab 7402
{
	Monster
	Health 10
	Speed 8
	Height 25
	Radius 8
	PainChance 200
	Scale 0.25
	BloodColor "DE FE 72"
	SeeSound "halflife/monsters/headcrab/sight"
	PainSound "halflife/monsters/headcrab/pain"
	DeathSound "halflife/monsters/headcrab/death"
	ActiveSound "halflife/monsters/headcrab/idle"
	maxdropoffheight 384
	+DONTHARMCLASS
	DamageFactor "Gonarch", 0.0
	var int user_CrabJump;
	var int user_DrownCounter;
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(waterlevel>=2,"Drown")
		HLBI A 3 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown")
		TNT1 A 0 A_JumpIf(user_CrabJump==1,"SeeMelee")
		HLBW ABCDEFG 5 A_ExtChase(0,1)
		Loop
	SeeMelee:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown")
		HLBW A 5 A_ExtChase(1,0)
		TNT1 A 0 A_SetUserVar("user_CrabJump",0)
		HLBW B 5 A_ExtChase(1,0)
		Goto See+4
	Melee:
		HLBW A 10 A_CustomMeleeAttack(random(1,8),"halflife/monsters/headcrab/meleehit")
		Goto See
	Missile:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown")
		HLBI A 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("halflife/monsters/headcrab/jump")
		HLBA AB 3
		TNT1 A 0 A_SetUserVar("user_CrabJump",1)
		HLBA C 1 A_SkullAttack
		HLBA C 2 ThrustThingZ(0,random(20,35),0,1)
		HLBA D 3
//		TNT1 A 0 A_MeleeAttack
		HLBA E 3 A_CheckFloor(1)
		wait
//		Goto Missile+8
		WLBA F 30 A_stop
		WLBA G 30
		Goto See
	Pain:
		TNT1 A 0 A_SetUserVar("user_CrabJump",0)
		HLBP A 8 A_Pain
		Goto See
	Drown:
		TNT1 a 0 A_JumpIf(user_DrownCounter>=3,"DieDrowned")
		TNT1 A 0 A_jump(230,1,2,3)
		HLBP A 0 A_Pain
		HLBP AAA 4
		HLBP A 8 A_SetUserVar("user_DrownCounter",user_DrownCounter+1)
		Loop
	DieDrowned:
		HLBD A 1 A_Die("DrownDMG")
		Wait
	Death.DrownDMG:
		HLBD A 2 A_Scream
		HLBD B 2 A_NoBlocking
		HLBD CDEFGHIJ 3
		HLBD K -1 A_JumpIf(waterlevel==3,"DrownUp")
		Stop
	DrownUp:
		TNT1 A 0 A_JumpIf(waterlevel<3,"DrownStay")
		HLBD K 2 ThrustThingZ(0,3,0,1)
		Loop
	DrownStay:
		HLBD K 16 A_JumpIf(waterlevel==3,9)
		Loop
		HLBD K 2 ThrustThingZ(0,2,0,1)
		Loop
	Death:
		HLBD A 2 A_Scream
		HLBD B 2 A_NoBlocking
		HLBD CDEFGHIJ 3
		HLBD K -1
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath")
		TNT1 A 0 A_NoBlocking
	  	TNT1 AAAA 0 A_SpawnItemEx("XenGibsMini",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359))
	  	TNT1 AA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359))
		Stop
	}
}