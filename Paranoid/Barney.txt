//=========
// Barney
//=========

class HLBarney : HLMonster
{
	bool follow,follower;
	override void tick()
	{
		if(follower)
		{
			if(goal==null)
				goal=players[0].mo;
			if(!follow&&Distance3D(goal)>96.0)
			{
				follow=1;
				SetStateLabel("See");
			}
			else if(follow&&Distance3D(goal)<=96.0)
			{
				follow=0;
				A_ClearTarget();
				SetStateLabel("Stand");
			}
		}
		Super.Tick();
	}
	Default
	{
		Health 150;
		Speed 4;
		Height 63;
		PainChance 128;
		PainChance "Crowbar",252;
		SeeSound "halflife/monsters/barney/sight";
		PainSound "halflife/monsters/barney/pain";
		DeathSound "halflife/monsters/barney/death";
		maxdropoffheight 64;
		dropitem "PistolPickup";
		+FRIENDLY
		-COUNTKILL
		Species "HLBarney";
	}
	States
	{
	Stand:
		HLI1 B 3 A_Face(goal);
		Wait;
	See:
		HLR1 A 1 A_Chase;
		HLR1 ABB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 C 1 A_Chase;
		HLR1 CDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 E 1 A_Chase;
		HLR1 EFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 G 1 A_Chase;
		HLR1 GGH 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 I 1 A_Chase;
		HLR1 IJJ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 K 1 A_Chase;
		HLR1 KLL 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 M 1 A_Chase;
		HLR1 MNN 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 O 1 A_Chase;
		HLR1 OPP 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 A 0
		{
			if(random(0,255)<8)
				A_PlaySound("halflife/monsters/barney/idle");
		}
		Loop;
	Missile:
		HLA1 A 1 A_FaceTarget;
		HLA1 BCD 1;
		HLA1 E 1 A_FaceTarget;
		HLA1 FGH 1;
		HLA1 I 1 A_FaceTarget;
		HLA1 JKL 1;
		HLA1 M 1 A_FaceTarget;
		HLA1 NOP 1;
		HLA1 Q 1 A_FaceTarget;
		HLA1 RST 1;
		HLA1 U 1 A_FaceTarget;
		HLA1 V 1;
		TNT1 A 0 A_Jump(64,"Magnum");
	Pistol:
		HLA1 W 1 A_FaceTarget;
		HLA1 X 1;
		HLA1 Y 1 Light("ImShootingAGun") A_BarneyGunshot;
		HLA1 Z[\] 1 Light("ImShootingAGun");
		HLA2 AB 1;
		HLA1 VV 2 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(0,"See");
		Loop;
	Magnum:
		HLA1 W 1 A_FaceTarget;
		HLA1 X 1;
		HLA1 Y 1 Light("ImShootingAGun") A_BarneyGunshot(2);
		HLA1 Z[\] 1 Light("ImShootingAGun");
		HLA2 AB 1;
		HLA1 V 6 A_FaceTarget;
		HLA1 VVVV 8 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(0,"See");
		HLA1 Y 1 Light("ImShootingAGun") A_BarneyGunshot(2);
		HLA1 Z[\] 1 Light("ImShootingAGun");
		HLA2 AB 1;
		HLA1 V 6 A_FaceTarget;
		HLA1 VVVV 8 A_FaceTarget;
		Goto See;
	Pain:
		HLP1 A 1 A_Pain;
		HLP1 DGJMPS 1;
		Goto See;
	Death:
		HLD1 A 2 A_Scream;
		HLD1 B 2 A_NoBlocking;
		HLD1 CDEFG 2;
		HLD1 H 2 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 IJKL 2;
		HLD1 M -1;
		Stop;
	XDeath:
		TNT1 A 0 A_XenGibs(1);
		Stop;
	}
	void A_BarneyGunshot(int type=1)
	{
		if(type==2)
		{
			A_PlaySound("halflife/weapons/magnumshot",5);
			A_BarneyGunsmoke();
			A_CustomBulletAttack(2,2,1,25,"BulletPuff");
		}
		else
		{
			A_PlaySound("halflife/weapons/pistol",5);
			A_BarneyGunsmoke("GunSmallSmoke");
			A_CustomBulletAttack(7,7,1,5,"BulletPuff");
		}
	}
	void A_BarneyGunsmoke(class<actor> smoke="GunSmoke")
	{
		for(int i=0;i<2;i++)
			A_SpawnItemEx(smoke,28,2,48);
	}
}