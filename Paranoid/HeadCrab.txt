//===========
// Head Crab
//===========

class HLHeadcrab : HLMonster
{
	bool crabjump;
	int drowncounter;
	Default
	{
		Health 10;
		Speed 1.6;
		Height 25;
		Radius 8;
		PainChance 200;
		PainChance "Crowbar",256;
		Scale .25;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/headcrab/sight";
		PainSound "halflife/monsters/headcrab/pain";
		DeathSound "halflife/monsters/headcrab/death";
		ActiveSound "halflife/monsters/headcrab/idle";
		maxdropoffheight 384;
		+DONTHARMCLASS;
		DamageFactor "Gonarch",0;
		Species "HLHeadcrab";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 3 A_Look;
		Loop;
	See:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		TNT1 A 0 A_JumpIf(crabjump,"SeeMelee");
		HLBW A 1 A_Chase(null,"Missile");
		HLBW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW B 1 A_Chase(null,"Missile");
		HLBW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
	SeeCont:
		HLBW C 1 A_Chase;
		HLBW CCCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW D 1 A_Chase;
		HLBW DDDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW E 1 A_Chase;
		HLBW EEEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW F 1 A_Chase;
		HLBW FFFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW G 1 A_Chase;
		HLBW GGGG 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	SeeMelee:
		HLBW A 1 A_Chase("Melee",null);
		HLBW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW B 1
		{
			crabjump=0;
			A_Chase("Melee",null);
		}
		HLBW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto SeeCont;
	Melee:
		HLBW A 10 A_CustomMeleeAttack(random(1,8),"halflife/monsters/headcrab/meleehit");
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 4 A_FaceTarget;
		HLBA A 3 A_PlaySound("halflife/monsters/headcrab/jump");
		HLBA B 3;
		HLBA C 1
		{
			crabjump=1;
			A_SkullAttack();
		}
		HLBA C 2 AddZ(random(20,35)/4.0);
		HLBA D 3;
		HLBA E 3 A_CheckFloor(1);
		wait;
		WLBA F 30 A_Stop;
		WLBA G 30;
		Goto See;
	Pain:
		HLBP A 8
		{
			crabjump=0;
			A_Pain();
		}
		Goto See;
	Drown:
		TNT1 A 0 A_JumpIf(drowncounter>=3,"DieDrowned");
		TNT1 A 0 A_jump(230,1,2,3);
		HLBP A 0 A_Pain;
		HLBP AAA 4;
		HLBP A 8 {drowncounter++;}
		Loop;
	DieDrowned:
		HLBD A 1 A_Die("DrownDMG");
		Wait;
	Death.DrownDMG:
		HLBD A 2 A_Scream;
		HLBD B 2 A_NoBlocking;
		HLBD CDEFGHIJ 3;
		HLBD K -1 A_JumpIf(waterlevel==3,"DrownUp");
		Stop;
	DrownUp:
		TNT1 A 0 A_JumpIf(waterlevel<3,"DrownStay");
		HLBD K 2 AddZ(.75);
		Loop;
	DrownStay:
		HLBD K 16 A_JumpIf(waterlevel==3,9);
		Loop;
		HLBD K 2 AddZ(.5);
		Loop;
	Death:
		HLBD A 2 A_Scream;
		HLBD B 2 A_NoBlocking;
		HLBD CDEFGHIJ 3;
		HLBD K -1;
		Stop;
	XDeath:
		TNT1 A 0
		{
			A_XenGibs(2);
			for(int i=0;i<4;i++)
				A_SpawnItemEx("XenGibsMini",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		}
		Stop;
	}
}