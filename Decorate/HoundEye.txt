//==========
// HoundEye
//==========

actor HLHoundEye 7401
{
	Monster
//	Health 150
	Health 100
	Mass 120
	Speed 11
	Height 45
	PainChance 128
	Scale 0.335
	MeleeRange 80
	BloodColor "DE FE 72"
	SeeSound "halflife/monsters/houndeye/sight"
	PainSound "halflife/monsters/houndeye/pain"
	DeathSound "halflife/monsters/houndeye/death"
	ActiveSound "halflife/monsters/houndeye/idle"
	var int user_theta;
	States
	{
	Spawn:
		HLAI A 3 A_Look
		Loop
	See:
		HLAW ABCDEF 3 A_Chase
		TNT1 A 0 A_Jump(32,1)
		Loop
		TNT1 A 0 A_PlaySound("halflife/monsters/houndeye/idlefull")
		HLAW ABCDEF 3 A_Chase
		HLAW ABCDEF 3 A_Chase
		HLAW ABCDEF 3 A_Chase
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_PlaySound("halflife/monsters/houndeye/idlefull")
		TNT1 A 0
		Loop
	Melee:
		TNT1 A 0 A_FaceTarget
		HLAA A 4 A_PlaySound("halflife/monsters/houndeye/attackcharge",5)
		HLAA BCBCBCBCBCBCBCBCBCBCB 3
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_PlaySound("halflife/monsters/houndeye/attackblast")
		TNT1 A 0 A_SetUserVar("user_theta",0)
	Shock1:
		TNT1 A 0 A_CustomMissile("HoundEyeShockWFast",0,0,user_theta)
		TNT1 A 0 A_SetUserVar("user_theta",user_theta+1)
		TNT1 A 0 A_JumpIf(user_theta==360,1)
		Loop
		TNT1 A 0 A_SetUserVar("user_theta",0)
	Shock2:
		TNT1 A 0 A_CustomMissile("HoundEyeShockWSlow",0,0,user_theta)
		TNT1 A 0 A_SetUserVar("user_theta",user_theta+1)
		TNT1 A 0 A_JumpIf(user_theta==360,1)
		Loop
		HLAA A 7
		Goto See
	Pain:
		TNT1 A 0 A_StopSound(5)
		HLAP A 8 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_StopSound(5)
		HLAD A 5 A_Scream
		HLAD B 4 A_Fall
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop")
		HLAD CDE 4
		HLAD F -1
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath")
		TNT1 A 0 A_Fall
	  	TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359))
		Stop
	}
}

ACTOR HoundEyeShockWFast
{
	Speed 19
	Damage (1)
	Radius 12
	Height 16
	RenderStyle Translucent
	Alpha 0.3
	PROJECTILE
	+DROPOFF
	-NoGravity
	+FloorHugger
	+Ripper
	Scale 0.4
	States
	{
	Spawn:
		HSHK ABC 3 A_Gravity
	Death:
		HSHK C 0
		Stop
	}
}

Actor HoundEyeShockWSlow : HoundEyeShockWFast
{
	Speed 12
}