class HLScientistWandererSpawner : RandomSpawner
{
	Default
	{
		DropItem "HLScientist1Wander";
		DropItem "HLScientist2Wander";
		DropItem "HLScientist3Wander";
		DropItem "HLScientist4Wander";
	}
}

class HLScientistImmovilSpawner : RandomSpawner
{
	Default
	{
		DropItem "HLScientist1Still";
		DropItem "HLScientist2Still";
		DropItem "HLScientist3Still";
		DropItem "HLScientist4Still";
	}
}

class HLScientist : Actor
{
	int limit,limit2,boredom,stay,panic;
	override bool Used(Actor user)
	{
		boredom=0;
		return false;
	}
	Default
	{
		Monster;
		Health 25;
		Speed 1.666667;
		Height 56;
		PainChance 255;
		Scale .36;
		+FRIENDLY
		-CANUSEWALLS
		-COUNTKILL
		SeeSound "halflife/monsters/scientist/sight";
		PainSound "halflife/monsters/scientist/pain";
		DeathSound "halflife/monsters/scientist/death";
	}
	action void Chatter(int chance)
	{
		if(random(0,255)<chance)
			A_PlaySound("halflife/monsters/scientist/idle");
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Path : HLScientist
{
	override void postbeginplay()
	{
		limit=35;
		limit2=30;
	}
	States
	{
	Spawn:
		HLRI A 0;
		"####" A 0 {boredom=1;}
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 5
		{
			if(stay>=80)
			{
				stay=0;
				if(random(0,1))
				{
					boredom=0;
					return ResolveState("See");
				}
				return State(null);
			}
			stay++;
			if(!boredom)
			{
				A_PlaySound("scientist/randomphrase");
				return ResolveState("See");
			}
			Chatter(2);
			return state(null);
		}
		Loop;
	See:
		HLRW A 0;
		"####" A 0
		{
			if(bFRIGHTENED)//scream the 1st time they enter here
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("SeeDecidePanic");
			}
			A_SetSpeed(5.0/3.0);
			return state(null);
		}
		"####" AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1
		{
			if(boredom)
				return ResolveState("Spawn");
			A_Chase();
			return state(null);
		}
		Goto See+1;
	Pain:
		HLRP A 0;
		"####" A 8 A_Pain;
		"####" A 0 A_Jump(256,"See");
	Death:
		HLRD A 0;
		"####" A 3 A_Scream;
		"####" B 3 A_NoBlocking;
		"####" CDEFGHIJ 3;
		"####" K 3 A_PlaySound("halflife/weapons/bodydrop");
		"####" L 3;
		"####" M -1;
		Stop;
	Panic:
		TNT1 A 0
		{
			A_ClearTarget();
			bFRIGHTENED=1;
		}
		TNT1 A 0 A_Jump(256,"WanderPanic","WanderRunStartPanic");
	WanderPanic:
		HLRW A 0;
		"####" A 0 A_SetSpeed(5.0/3.0);
		"####" AAAAAA 1 A_Chase;
		"####" A 0 A_Look;
		"####" BBBBBB 1 A_Chase;
		"####" A 0 A_Look;
		"####" CCCCCC 1 A_Chase;
		"####" A 0 A_Look;
		"####" DDDDDD 1 A_Chase;
		"####" A 0 A_Look;
		"####" EEEEEE 1 A_Chase;
		"####" A 0 A_Look;
		"####" FFFFFF 1 A_Chase;
		"####" A 0 A_Look;
		"####" A 0	//how often they'll say something while moving
		{
			if(random(0,255)<8)
			{
				A_PlaySound("scientist/panic");
				return ResolveState("WanderPanic");
			}
			if(random(0,255)<4)
			{
				if(random(0,255)<235)
					return ResolveState("IdleRegPanic");	//stopmov, ironically also randomly starts the run seq
				return ResolveState("WanderRunStartPanic");	//kind of rare to see them running just because of nothing.
			}
			return state(null);
		}
		Goto WanderPanic+1;
	IdleRegPanic:
		HLRI A 0;
		"####" A 0
		{
			if(random(0,255)<3)
				A_PlaySound("scientist/panic");
		}
		"####" "###" 5 A_Look;
		"####" # 0 A_Jump(5,"WanderPanic");
		Goto IdleRegPanic+1;
	SeeDecidePanic:		//decide if they calm down or not
		HLRR A 0;
		"####" # 0 A_JumpIf(panic>=limit,"SeeCalmDownPanic");
	SeeLoopPanic:
		"####" A 0 A_SetSpeed(5);
		"####" AAABBBCCCDDDEEEFFF 1 A_Chase;
		"####" A 0
		{
			panic++;
			if(random(0,255)<4)//every once in a while it will say something
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("SeeLoopPanic");
			}
			return state(null);
		}
		"####" # 0 A_Jump(256,"SeeDecidePanic");
	SeeCalmDownPanic:
		TNT1 A 0
		{
			panic-=8;	//If decides to go running again, will last a bit more
			if(random(0,255)<160)
				return ResolveState("WanderRunPanic");
			panic=0;
			A_ClearTarget();
			return ResolveState("WanderPanic");
		}
	WanderRunStartPanic:		//scream the 1st time they enter here
		TNT1 A 0 A_PlaySound("halflife/monsters/scientist/sight");
		TNT1 A 0 A_Jump(256,"WanderRunDecidePanic");
	WanderRunDecidePanic:	//decide if they calm down or not
		TNT1 A 0 A_JumpIf(panic>=limit,"CalmDownPanic");
		TNT1 A 0 A_Jump(256,"WanderRunPanic");
	WanderRunPanic:		//not calmed at all
		HLRR A 0;
		"####" A 0 A_SetSpeed(5);
		"####" AAA 1 A_Chase;
		"####" A 0 A_Look;
		"####" BBB 1 A_Chase;
		"####" A 0 A_Look;
		"####" CCC 1 A_Chase;
		"####" A 0 A_Look;
		"####" DDD 1 A_Chase;
		"####" A 0 A_Look;
		"####" EEE 1 A_Chase;
		"####" A 0 A_Look;
		"####" FFF 1 A_Chase;
		"####" A 0 A_Look;
		"####" A 0
		{
			panic++;
			if(random(0,255)<4)//every once in a while it will say something
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("WanderRunPanic");
			}
			return state(null);
		}
		TNT1 A 0 A_Jump(256,"WanderRunDecidePanic");
	CalmDownPanic:
		TNT1 A 0
		{
			panic-=8;	//If decides to go running again, will last a bit more
			if(random(0,255)<160)
				return ResolveState("WanderRunPanic");
			panic=0;
			return ResolveState("WanderPanic");
		}
	}
}

class HLScientist2Path : HLScientist1Path
{
	override void postbeginplay()
	{
		limit=45;
		limit2=35;
	}
	Default
	{
		Scale .3526;
	}
	States
	{
	Spawn:
		SC1I A 0;
		Goto Super::Spawn+1;
	See:
		SC1W A 0;
		Goto Super::See+1;
	Pain:
		SC1P A 0;
		Goto Super::Pain+1;
	Death:
		SC1D A 0;
		Goto Super::Death+1;
	WanderPanic:
		SC1W A 0;
		Goto Super::WanderPanic+1;
	IdleRegPanic:
		SC1I A 0;
		Goto Super::IdleRegPanic+1;
	SeeDecidePanic:
		SC1R A 0;
		Goto Super::SeeDecidePanic+1;
	WanderRunPanic:
		SC1R A 0;
		Goto Super::WanderRunPanic+1;
	}
}

class HLScientist3Path : HLScientist1Path
{
	override void postbeginplay()
	{
		limit=55;
		limit2=65;
	}
	Default
	{
		Scale .355;
	}
	States
	{
	Spawn:
		SC2I A 0;
		Goto Super::Spawn+1;
	See:
		SC2W A 0;
		Goto Super::See+1;
	Pain:
		SC2P A 0;
		Goto Super::Pain+1;
	Death:
		SC2D A 0;
		Goto Super::Death+1;
	WanderPanic:
		SC2W A 0;
		Goto Super::WanderPanic+1;
	IdleRegPanic:
		SC2I A 0;
		Goto Super::IdleRegPanic+1;
	SeeDecidePanic:
		SC2R A 0;
		Goto Super::SeeDecidePanic+1;
	WanderRunPanic:
		SC2R A 0;
		Goto Super::WanderRunPanic+1;
	}
}

class HLScientist4Path : HLScientist1Path
{
	override void postbeginplay()
	{
		limit=65;
		limit2=65;
	}
	States
	{
	Spawn:
		SC3I A 0;
		Goto Super::Spawn+1;
	See:
		SC3W A 0;
		Goto Super::See+1;
	Pain:
		SC3P A 0;
		Goto Super::Pain+1;
	Death:
		SC3D A 0;
		Goto Super::Death+1;
	WanderPanic:
		SC3W A 0;
		Goto Super::WanderPanic+1;
	IdleRegPanic:
		SC3I A 0;
		Goto Super::IdleRegPanic+1;
	SeeDecidePanic:
		SC3R A 0;
		Goto Super::SeeDecidePanic+1;
	WanderRunPanic:
		SC3R A 0;
		Goto Super::WanderRunPanic+1;
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Wander : HLScientist1Path
{
	States
	{
	Spawn:
	See:
		HLRW AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 A_Wander;
		HLRI A 0 A_Jump(32,"StopMov");
		Loop;
	StopMov:
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 15 Chatter(4);
		"####" # 0 A_Jump(6,"Spawn");
		Loop;
	}
}

class HLScientist2Wander : HLScientist2Path
{
	States
	{
	Spawn:
	See:
		SC1W AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 NODELAY A_Wander;
		SC1I A 0 A_Jump(32,"StopMov");
		Loop;
	StopMov:
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 15 Chatter(4);
		"####" # 0 A_Jump(6,"Spawn");
		Loop;
	}
}

class HLScientist3Wander : HLScientist3Path
{
	States
	{
	Spawn:
	See:
		SC2W AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 NODELAY A_Wander;
		SC2I A 0 A_Jump(32,"StopMov");
		Loop;
	StopMov:
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 15 Chatter(4);
		"####" # 0 A_Jump(6,"Spawn");
		Loop;
	}
}

class HLScientist4Wander : HLScientist4Path
{
	States
	{
	Spawn:
	See:
		SC3W AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 NODELAY A_Wander;
		SC3I A 0 A_Jump(32,"StopMov");
		Loop;
	StopMov:
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 15 Chatter(4);
		"####" # 0 A_Jump(6,"Spawn");
		Loop;
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Still : HLScientist1Path
{
	States
	{
	Spawn:
	See:
		HLRI A 0;
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 10 Chatter(2);
		Loop;
	}
}

class HLScientist2Still : HLScientist2Path
{
	States
	{
	Spawn:
	See:
		SC1I A 0;
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 10 Chatter(2);
		Loop;
	}
}

class HLScientist3Still : HLScientist3Path
{
	States
	{
	Spawn:
		SC2I A 0;
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 10 Chatter(2);
		Loop;
	}
}

class HLScientist4Still : HLScientist4Path
{
	States
	{
	Spawn:
		SC3I A 0;
		"####" A 0 A_Jump(256,1,2,3);
		"####" ABC 0 A_Jump(256,"Pondering");
	Pondering:
		"####" # 10 Chatter(2);
		Loop;
	}
}

//////////////////////////////////////////////////////////
//Previous code for scientists; only used for dead ones //
//////////////////////////////////////////////////////////

class HLScientistUpdate : Actor
{
	Default
	{
		Scale .36;
	}
	States
	{
	Spawn:
		HLRD M -1;
		Stop;
	}
}

class HLScientist1 : Actor
{
	Default
	{
		Scale 0.3526;
	}
	States
	{
	Spawn:
		SC1D M -1;
		Stop;
	}
}

class HLScientist2 : HLScientistUpdate
{
	Default
	{
		Scale 0.355;
	}
	States
	{
	Spawn:
		SC2D I -1;
		Stop;
	}
}

class HLScientist3 : HLScientistUpdate
{
	States
	{
	Spawn:
		SC3D N -1;
		Stop;
	}
}