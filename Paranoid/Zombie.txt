//=========================
// Zombie & Moaning Zombie
//=========================

class HLZombie : HLMonster
{
	bool moan;
	Default
	{
		Health 150;
		Mass 100;
		Speed 1.4;
		Height 63;
		PainChance 128;
		PainChance "Crowbar",252;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/zombie/sight";
		PainSOund "halflife/monsters/zombie/pain";
		DeathSound "halflife/monsters/zombie/pain";
		ActiveSound "halflife/monsters/zombie/idle";
		maxdropoffheight 64;
		Species "HLZombie";
	}
	States
	{
	See:
		HLW1 A 0
		{
			if(moan)
				A_PlaySound("halflife/monsters/zombie/moan",6,1,1,ATTN_STATIC);
		}
		HLW1 A 1 A_Chase;
		HLW1 BCDE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 F 1 A_Chase;
		HLW1 GHIJ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 K 1 A_Chase;
		HLW1 LMOP 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 Q 1 A_Chase;
		HLW1 RSTU 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 V 1 A_Chase;
		HLW1 WXYZ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 [ 1 A_Chase;
		HLW1 \ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 ] 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 AB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 C 1 A_Chase;
		HLW2 DEFG 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 H 1 A_Chase;
		HLW2 IJKL 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 M 1 A_Chase;
		HLW2 NOPQ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 R 1 A_Chase;
		HLW2 STUV 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 W 1 A_Chase;
		HLW2 "XYZ[" 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 \ 1 A_Chase;
		HLW2 ] 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW3 A 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Melee:
		HLA1 A 1 A_FaceTarget;
		HLA1 BCDEF 1;
		HLA1 G 1 A_ZombieMelee;
		HLA1 HI 1;
		HLA1 J 1 A_FaceTarget;
		HLA1 KLMNO 1;
		HLA1 P 1 A_ZombieMelee;
		HLA1 QR 1;
		HLA1 S 1 A_FaceTarget;
		HLA1 "TUVWXYZ[\]" 1;
		Goto See;
	Pain:
		HLP1 A 1
		{
			A_StopSound(6);
			A_Pain();
		}
		HLP1 BCDEFGH 1;
		Goto See;
	Death:
		HLD1 A 1
		{
			A_StopSound(6);
			A_Scream();
		}
		HLD1 BC 1;
		HLD1 D 1 A_NoBlocking;
		HLD1 EFGHIJKLMNOPQR 1;
		HLD1 S 1 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 "TUVWXYZ[" 1;
		HLD1 \ 1;
		HLD1 ] -1;
		Stop;
	XDeath:
		TNT1 A 0
		{
			A_StopSound(6);
			A_XenGibs();
		}
		Stop;
	}
}

class HLZombieMoan : RandomSpawner
{
	override void PostSpawn(Actor spawned)
	{
		let me=HLZombie(spawned);
		me.moan=1;
	}
	Default
	{
		DropItem "HLZombie";
	}

}