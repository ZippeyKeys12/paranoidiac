//=========================
// Zombie & Moaning Zombie
//=========================

class HLZombie : Actor
{
	Default
	{
		Monster;
		Health 150;
		Mass 100;
		Speed 1.4;
		Height 63;
		PainChance 128;
		PainChance "Crowbar",252;
		Scale .32;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/zombie/sight";
		PainSOund "halflife/monsters/zombie/pain";
		DeathSound "halflife/monsters/zombie/pain";
		ActiveSound "halflife/monsters/zombie/idle";
		maxdropoffheight 64;
	}
	States
	{
	Spawn:
		HLEI A 3 NODELAY A_StopSound(7);
		HLEI A 3 A_Look;
		Wait;
	See:
		HLEW A 1 A_Chase;
		HLEW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW B 1 A_Chase;
		HLEW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW C 1 A_Chase;
		HLEW CCCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW D 1 A_Chase;
		HLEW DDDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW E 1 A_Chase;
		HLEW EEEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW F 1 A_Chase;
		HLEW FFFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW G 1 A_Chase;
		HLEW GGGG 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto See;
	Melee:
		HLEA AB 3 A_FaceTarget;
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/attswing");
		HLEA C 3 A_FaceTarget;
		HLEA D 3 A_CustomMeleeAttack(2*random(1,8),"halflife/monsters/zombie/hit");
		HLEA E 3 A_FaceTarget;
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/attswing");
		HLEA F 3 A_FaceTarget;
		HLEA G 3 A_CustomMeleeAttack(2*random(1,8),"halflife/monsters/zombie/hit");
		HLEA HIJ 3;
		Goto See;
	Pain:
		HLEP A 8
		{
			A_StopSound(6);
			A_Pain();
		}
		Goto See;
	Death:
		HLED A 3
		{
			A_StopSound(6);
			A_Scream();
		}
		HLED B 3 A_Fall;
		HLED CDEFGH 2;
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop");
		HLED HIJ 3;
		HLED K -1;
		Stop;
	XDeath:
		TNT1 A 0
		{
			A_StopSound(6);
			A_PlaySound("other/gibdeath");
			for(int i=0;i<16;i++)
				A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
	  	}
		Stop;
	}
}

class HLZombieMoan : HLZombie
{
	States
	{
	See:
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/moan",6,1,1,ATTN_STATIC);
		Goto Super::See;
	}
}