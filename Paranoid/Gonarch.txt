class HLGonarch : Actor
{
	override void tick()
	{
		if(health>0)
		{
			A_SpawnItemEx("LegShield1Tic",48,48);	//front right
			A_SpawnItemEx("LegShield1Tic",48,-48);	//front left
			A_SpawnItemEx("LegShield1Tic",-48,48);	//back right
			A_SpawnItemEx("LegShield1Tic",-48,-48);	//back left
			A_SpawnItemEx("TopShield1Tic",0,0,70);	//"head" shield
		}
		super.tick();
	}
	Default
	{
		Monster;
		Health 5000;
		Mass 0x7FFFFFFF;
		+DONTHARMCLASS
		Speed 8;
		Height 90;
		Radius 55;
		PainChance 10;
		PainChance "Crowbar",54;
		+BOSS
		+NORADIUSDMG
		+MISSILEEVENMORE
		BloodColor "DE FE 72";
		SeeSound "gonarch/sight";
		PainSound "gonarch/pain";
		DeathSound "gonarch/death";
		ActiveSound "gonarch/idle";
		Obituary "%o was melted by the gonarch's acid.";
		HitObituary "%o suffered internal bleedings, courtesy of the gonarch.";
		MeleeRange 80;
		MaxTargetRange 1100;
		Scale 0.8;
	}
	States
	{
	Spawn:
		HLNI A 3 A_Look;
		Loop;
	See:
		HLNW A 1 A_Chase();
		HLNW AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW A 1 A_Chase();
		HLNW AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW B 1 A_Chase();
		HLNW BB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW B 1 A_Chase();
		HLNW BB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW C 1 A_Chase();
		HLNW CC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW C 1 A_Chase();
		HLNW CC 1 A_Chase(null,null,HLSMOOTHCHASE);
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		HLNW D 1 A_Chase();
		HLNW DD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW D 1 A_Chase();
		HLNW DD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW E 1 A_Chase();
		HLNW EE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW E 1 A_Chase();
		HLNW EE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW F 1 A_Chase();
		HLNW FF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW F 1 A_Chase();
		HLNW FF 1 A_Chase(null,null,HLSMOOTHCHASE);
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		TNT1 A 0 A_Jump(210,"See");
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/sight",5);
		Loop;
	Melee:
		TNT1 A 0 A_Jump(100,"AcidSimple");
		TNT1 A 0
		{
			if(random(0,255)<80)
				A_PlaySound("halflife/monsters/gonarch/sight",4);
			else
				A_PlaySound("halflife/monsters/gonarch/attack",5);
		}
		HLNM AB 5 A_FaceTarget();
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/attswing",6);
		HLNM C 5 A_FaceTarget();
		HLNM D 4 A_CustomMeleeAttack(random(1,8)*25,"zombie/hit");
		HLNM EF 5;
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(145,"AcidSimple");
		Goto SpawnCrab;
	AcidSimple:
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/attack",5);
		HLNO AABBBB 4 A_FaceTarget;
		TNT1 A 0 A_Jump(80,"AcidHeavy");
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcidExtra",0,0,80,random(2,-2),random(2,-2),random(1,8));
		HLNO C 8 A_SpawnProjectile("GonarchAcid",80);
		HLNO D 8;
		Goto See;
	AcidHeavy:
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Extra",0,0,80,random(2,-2),random(2,-2),random(1,8));
		HLNO C 8 A_SpawnProjectile("GonarchAcid2",80);
		HLNO D 8;
		Goto See;
	SpawnCrab:
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/attack",5);
		HLNN ABC 8;
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/spawnchild",4);
		HLNN DD 2 A_SpawnItemEx("HLBabyHeadCrab2",random(10,-10),random(10,-10),flags:SXF_NOCHECKPOSITION);
		HLNN D 1 A_SpawnItemEx("HLBabyHeadCrab2",random(10,-10),random(10,-10),flags:SXF_NOCHECKPOSITION);
		HLNN E 32;
		Goto See;
	Pain:
		HLNI A 4 A_Pain;
		Goto See;
	Death:
		HLND A 3 A_Scream;
		HLND B 3 A_Fall;
		HLND CD 4;
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		HLND EF 4;
		HLND G 100;
		TNT1 A 0 A_PlaySound("halflife/weapons/explode2");
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("NewExplosionMedium",random(30,-30),random(30,-30),random(10,90),random(1,2),random(1,2),random(1,3));
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("gargantuagibs",random(30,-30),random(30,-30),random(10,60),random(1,7),random(1,7),random(5,18),random(0,359));
	  	TNT1 AAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		TNT1 A 10;
		Stop;
	}
}

//-------------------------------------------------------------
class GonarchAcid : Actor
{
	Default
	{
		Projectile;
		Radius 12;
		Height 8;
		Speed 24;
		Scale .6;
		Damage 25;
		RenderStyle "Add";
		Alpha .9;
	//	Translation "80:111=118:127";
		-NOGRAVITY
		Gravity .18;
		PoisonDamage 50;
		+THRUGHOST
		+FORCERADIUSDMG
		DamageType "Gonarch";
	}
	States
	{
	Spawn:
		X022 A 0 NODELAY ThrustThingZ(0,12,0,1);
		X022 AB 4 AcidExtra;
		TNT1 A 0 {bTHRUGHOST=0;}
	SpawnLoop:
		X022 CDAB 4 AcidExtra;
		Loop;
	Death:
		TNT1 A 0 A_Explode(128,128,0);
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit");
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GonarchAcidExtra",0,0,0,random(2,-2),random(2,-2),random(0,4));
		Stop;
	}
	action void AcidExtra()
	{
		for(int i=0;i<2;i++)
			A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5));
	}
}

class GonarchAcidExtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Scale .3;
		RenderStyle "Add";
		Alpha .9;
		-NOGRAVITY
		Gravity .2;
	//	Translation "80:111=118:127";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(100,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

//-------------------------------------------------------------
class GonarchAcid2 : GonarchAcid
{
	Default
	{
		Projectile;
		Radius 12;
		Height 8;
		Speed 19;
		Scale 0.9;
		Damage 30;
		RenderStyle "Translucent";
		Alpha 0.8;
		Translation "80:111=152:159";
		DeathSound "";
		-NOGRAVITY
		PoisonDamage 50;
		Gravity 0.18;
		+FORCERADIUSDMG
		+THRUGHOST
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY ThrustThingZ(0,12,0,1);
		X022 AB 4
		{
			for(int i=0;i<2;i++)
				A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5));
		}
		TNT1 A 0 {bTHRUGHOST=0;}
		X022 CDAB 4
		{
			for(int i=0;i<2;i++)
				A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5));
		}
		Goto Spawn+4;
	Death:
		TNT1 A 0 A_Explode(256,256,0);
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit");
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Extra",0,0,0,random(2,-2),random(2,-2),random(0,4));
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GonarchAcid2MiniExplosive",0,0,5,random(-3,3),random(-3,3),random(2,7),random(0,359));
		Stop;
	}
}

class GonarchAcid2Extra : GonarchAcidExtra
{
	Default
	{
		RenderStyle "Translucent";
		Alpha 0.8;
		Scale 0.3;
		Translation "80:111=152:159";
	}
}
class GonarchAcid2MiniExplosive : Actor
{
	Default
	{
		Projectile;
		Damage 6;
		Speed 10;
		Radius 8;
		Height 12;
		Scale 0.55;
		DamageType "Gonarch";
		RenderStyle "Translucent";
		Alpha 0.8;
		-NOGRAVITY
		Gravity 0.3;
		PoisonDamage 15;
		Translation "80:111=152:159";
	}
	States
	{
	Spawn:
		X022 ABCD 3;
		Loop;
	Death:
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Mini",0,0,0,random(3,-3),random(3,-3),random(1,5),random(0,359));
		Stop;
	}
}

class GonarchAcid2Mini : GonarchAcidExtra
{
	Default
	{
		RenderStyle "Translucent";
		Alpha 0.8;
		DamageType "Gonarch";
		PoisonDamage 3;
		-NOCLIP
		Damage 1;
		Scale 0.18;
		Translation "80:111=152:159";
	}
}

class LegShield1Tic : Actor
{
	Default
	{
		+GHOST
		radius 14;
		height 90;
		+NOCLIP
		+SHOOTABLE
		+INVULNERABLE
	}
	states
	{
	Spawn:
		TNT1 A 2;
		stop;
	}
}

class TopShield1Tic : LegShield1Tic
{
	Default
	{
		Radius 64;
		Height 30;
	}
}