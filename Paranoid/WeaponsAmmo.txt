class ParanoidWeapon : Weapon
{
	override void PostBeginPlay()
	{
		if(bTOSSED)
			angle=random(0,359);
	}
	Default
	{
		+WEAPON.NOALERT
		+INVENTORY.IGNORESKILL
		+WEAPON.AMMO_CHECKBOTH
		Weapon.BobRangeX 0;
		Weapon.BobRangeY .25;
		Weapon.BobStyle "Smooth";
	}
	States
	{
	Deselect:
		HLD1 ABCDEFGHIJKLMNO 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select:
		TNT1 A 0 A_Raise(100);
		Loop;
	Select1:
		HLU1 ABCDEFGHIJKLMNO 1;
		HLU1 O 0 A_Jump(256,"Ready");
	Fire:
	Ready:
		HLI1 A 1 A_WeaponReady;
		Loop;
	DryFire:
		HLI1 A 5;
		HLI1 A 0
		{
			if(CountInv(invoker.AmmoType1))
				return ResolveState("Reload");
			return ResolveState("Ready");
		}
	Spawn:
		POSS A -1;
		Stop;
	}
	action void A_HLWeaponRecoil(int climb)
		{A_SetPitch(pitch-climb*(360.0/65536.0));}
	action void A_HLWeaponReady(int max,bool water=0,class<ammo> am2x=null)
	{
		class<ammo> am2=invoker.ammotype2;
		if(am2x)
			am2=am2x;
		int wepflags,allowreload;
		if(water&&waterlevel==3)
			wepflags=WRF_NOFIRE;
		if(CountInv(am2)<max&&CountInv(invoker.AmmoType1))
			wepflags+=WRF_ALLOWRELOAD;
		A_WeaponReady(wepflags);
	}
	override State GetReadyState()
		{return FindState('Select1');}
	action void A_HLReload(string snd,class<ammo> am2x=null,int amt=0)
	{
		A_PlaySound(snd,CHAN_WEAPON);
		class<ammo> am1=invoker.ammotype1,am2=invoker.ammotype2;
		if(am2x)
			am2=am2x;
		if(!amt)
			amt=GetDefaultByType(am2).MaxAmount;
		if(!am1||!am2)
			return;
		while(CountInv(am2)<amt&&CountInv(am1))
		{
			A_TakeInventory(am1,1,TIF_NOTAKEINFINITE);
			A_GiveInventory(am2);
		}
	}
	action void A_HLShootLead(int sprdxy,int sprdz,int num,int dmg,int climb,string snd,int smoke=1,int smokexy=5,class<ammo> am2x=null)
	{
		A_PlaySound(snd,CHAN_WEAPON);
		A_AlertMonsters();
		A_SpawnItemEx("LighCheapHack");
		for(int i=0;i<smoke;i++)
			A_FireProjectile("GunSmoke",0,0,smokexy,4);
		A_FireBullets(sprdxy,sprdz,num,dmg,"NewPuff1",0);
		A_HLWeaponRecoil(climb);
		class<ammo> am2=invoker.ammotype2;
		if(am2x)
			am2=am2x;
		if(am2)
			A_TakeInventory(am2,1);
	}
	action state A_HLDryFireCheck(class<ammo> am2x=null)
	{
		class<ammo> am2=invoker.ammotype2;
		if(am2x)
			am2=am2x;
		if(!CountInv(am2))
			return ResolveState("DryFire");
		return state(null);
	}
}

class ParanoidWeaponGiver : WeaponGiver
{
	class<ammo> Weapon1,Weapon2;
	int Weapon1Amount;
	Property Weapon1: Weapon1,Weapon1Amount;
	Default
	{
		Radius 0;
		Height 0;
	}
	override void PostBeginPlay()
	{
		class<weapon> wep;
		wep=GetDropItems().Name;
		if(wep==null)
			return;
		if(!height)
			height=GetDefaultByType(wep).height;
		if(!radius)
			A_SetSize(GetDefaultByType(wep).radius);
		if(Weapon1==null)
			Weapon1=GetDefaultByType(wep).AmmoType2;
		Weapon2=GetDefaultByType(wep).AmmoType1;
		if(!Weapon1Amount)
			Weapon1Amount=GetDefaultByType(Weapon1).MaxAmount;
		if(bTOSSED)
			angle=random(0,359);
	}
	override bool TryPickup(in out Actor toucher)
	{
		if(WeaponGiver.TryPickup(toucher))
		{
			toucher.A_GiveInventory(Weapon1,Weapon1Amount);
			GoAwayAndDie();
			return true;
		}
		if(toucher.A_GiveInventory(Weapon2,Weapon1Amount))
		{
			GoAwayAndDie();
			return true;
		}
		return false;
	}
	override String PickupMessage()
	{
		class<inventory> wep;
		wep=GetDropItems().Name;
		return GetDefaultByType(wep).PickupMsg;
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	}
}

//****************************************
// Dummy item for light on player weapons
//****************************************

class LighCheapHack : Actor
{
	Default
	{
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 3 LIGHT("YellowMediumFlicker3");
		TNT1 A 3 LIGHT("YellowSmallFlicker1");
		Stop;
	}
}

//===============
// Gun Smoke
//===============

class GunSmoke : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Projectile;
		Alpha .33;
		Scale .16;
		RenderStyle "Add";
		+FORCEXYBILLBOARD
		Speed 14;
	}
	States
	{
	Spawn:
		TNT1 A 2 NODELAY A_Stop();
		WPXS A 1 {Frame=(random(0,14));}
		WPXS # 1
		{
			Frame++;
			if(frame>=28)
				SetStateLabel("Null");
			A_FadeOut(.01);
		}
		Wait;
	}
}

class NewPuff1 : Actor replaces BulletPuff
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+NOCLIP
		Scale .2;
		RenderStyle "Add";
		Decal "BulletChip";
	}
	states
	{
	Spawn:
		TNT1 A 0 NODELAY
		{
			A_SpawnItemEx("PuffSmoke",zvel:frandom(.25,1));
			if(random(0,1))
				scale.x=-scale.x;
		}
		FX57 ABCDEFGHIJ 2 Bright A_FadeOut;
		Stop;
	}
}

class NewPuff2 : NewPuff1  //does soldier damage type
{
	Default
	{
		DamageType "SoldierDamage";
	}
}

class PuffSmoke : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Scale .3;
		RenderStyle "Add";
		Alpha .67;
	}
	States
	{
	Spawn:
		WPXS ACEGIKMOQSUWYZ 2 A_FadeOut;
		Wait;
	}
}

//================
// Weapons & Ammo
//================

class HLCrowbar : ParanoidWeapon
{
	Default
	{
		Tag "Crowbar";
		Height 4;
		Weapon.SelectionOrder 2100;
		Weapon.Kickback 0;
		Inventory.PickupSound "misc/i_pkup";
		Inventory.PickupMessage "Picked up a crowbar.";
		Obituary "%k shoved %o's crowbar where the sun don't shine.";
		+WEAPON.MELEEWEAPON
		-WEAPON.NOALERT
		-WEAPON.AMMO_CHECKBOTH
		Inventory.Icon "i_cbar1";
	}
	States
	{
	Deselect:
		HLD1 ABCDEFGHIJKL 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select1:
		HLU1 ABCDEFGHIJKL 1;
		Goto Ready;
	Fire:
		TNT1 A 0 A_Jump(256,"Fire1","Fire2","Fire3");
	Fire1:
		HLF1 A 2 A_JumpIfCloser(80,"FireFast1");
		HLF1 BC 2;
		HLF1 D 2 A_HLCrowbarHit;
		HLF1 EFGHIJK 2;
		Goto Ready;
	FireFast1:
		HLF1 LMNO 1;
		HLF1 P 1 A_HLCrowbarHit;
		HLF1 QRSTUV 1;
		HLI1 A 2;
		Goto Ready;
	Fire2:
		HLF1 W 2 A_JumpIfCloser(80,"FireFast2");
		HLF1 XYZ 2;
		HLF1 [ 2 A_HLCrowbarHit;
		HLF1 \] 2;
		HLF2 ABCDEF 2;
		Goto Ready;
	FireFast2:
		HLF2 HIJKLM 1;
		HLF2 N 1 A_HLCrowbarHit;
		HLF2 OPQRSTU 1;
		Goto Ready;
	Fire3:
		HLF2 V 2 A_JumpIfCloser(80,"FireFast3");
		HLF2 WXY 2;
		HLF2 Z 2 A_HLCrowbarHit;
		HLF2 [\] 2;
		HLF3 AB 2;
		HLF3 CDEFGHIJK 1;
		Goto Ready;
	FireFast3:
		HLF3 LMNOP 1;
		HLF3 Q 1 A_HLCrowbarHit;
		HLF3 RSTUVWXYZ[\] 1;
		HLF4 A 1;
		Goto Ready;
	}
	action void A_HLCrowbarHit()
	{
		if(player==null)
			return;
		int damage=3*random(1,18);
		for(int i=0;i<16;i++)
		{
			if(TryPunch(angle+i*(12./16),damage)||TryPunch(angle-i*(12./16),damage))
				return;
		}
		//didn't find any creatures, so try to strike any walls
		double slope=AimLineAttack(angle,80);
		LineAttack(angle,80,slope,damage,'Crowbar',"CrowbarPuff2",true);
	}
	private action bool TryPunch(double angle,int damage)
	{
		FTranslatedLineTarget t;
		double slope=AimLineAttack(angle,80,t);
		if(t.linetarget==null)
			return false;
		LineAttack(angle,80,slope,damage,'Crowbar',"CrowbarPuff2",true,t);
		if(t.linetarget==null)
			return false;
		return true;
	}
}

class CrowbarPuff2 : NewPuff1
{
	Default
	{
		+NOEXTREMEDEATH
		+PUFFONACTORS
		ActiveSound "halflife/weapons/CrowbarMiss";
	}
	states
	{
	Spawn:
		TNT1 A 4 NODELAY A_PlaySound("halflife/weapons/Crowbarhitbody",CHAN_WEAPON);
		Stop;
	Crash:
		TNT1 A 0 A_PlaySound("halflife/weapons/Crowbarhit",CHAN_WEAPON);
		Goto Super::Spawn;
	}
}

//=========
// Beretta
//=========

class HLPistolPickup : ParanoidWeaponGiver
	{Default{DropItem "HLPistol";}}

class HLPistol : ParanoidWeapon
{
	Default
	{
		Tag "9mm Pistol";
		Radius 4;
		Height 4;
		Weapon.SelectionOrder 2000;
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoType "HLPistolAmmo";
		Weapon.AmmoType2 "HLPistolMag";
		Inventory.PickupMessage "Picked up a 9mm pistol.";
		Obituary "%o was out-gunned by %k's 9mm pistol.";
		Inventory.Icon "i_pist1";
	}
	States
	{
	Ready:
		TNT1 A 0 A_JumpIf(!CountInv("HLPistolMag"),2);
		HLI1 A 1 A_HLWeaponReady(17);
		Loop;
		HLR2 N 1 A_HLWeaponReady(17);
		Loop;
	Fire:
		HLI1 A 0 A_HLDryFireCheck;
		HLI1 A 0 A_HLShootLead(1,1,-1,5,100,"halflife/weapons/pistol");
		HLF1 A 1 A_JumpIf(!CountInv("HLPistolMag"),"Fire2");
		HLF1 BC 1 A_HLWeaponRecoil(-50);
		HLF1 DEFGHIJKL 1;
		HLF1 M 1 A_ReFire;
		HLF1 NOPQRS 1;
		Goto Ready;
	Fire2:
		HLF1 U 1;
		HLF1 VW 1 A_HLWeaponRecoil(-50);
		HLF1 XYZ[\] 1;
		HLF2 ABC 1;
		HLF2 D 1 A_ReFire;
		HLF2 EFGHIJ 1;
		HLF2 K 0 A_JumpIf(CountInv("HLPistolAmmo"),"Reload");
		HLF2 K 0 A_CheckReload;
		Goto Ready;
	AltFire:
		HLI1 A 0 A_HLDryFireCheck;
		HLI1 A 0 A_HLShootLead(7,7,-1,5,100,"halflife/weapons/pistol");
		HLI1 A 0 A_JumpIf(!CountInv("HLPistolMag"),"Fire2");
		HLF1 A 1;
		HLF1 BC 1 A_HLWeaponRecoil(-50);
		HLF1 DEFG 1;
		HLF1 H 1 A_ReFire;
		HLF1 IJKLMNOPQRS 1;
		Goto Ready;
	AltFire2:
		HLF1 U 1;
		HLF1 VW 1 A_HLWeaponRecoil(-50);
		HLF1 XYZ[ 1;
		HLF1 \ 1 A_ReFire;
		HLF1 ] 1;
		HLF2 DCBAEFGHIJ 1;
		HLF2 K 0 A_JumpIf(CountInv("HLPistolAmmo"),"Reload");
		HLF2 K 0 A_CheckReload;
		Goto Ready;
	Reload:
		HLI1 A 0 A_JumpIf(!CountInv("HLPistolMag"),"Reload2");
		HLR1 ABCDEFGHIJK 1;
		HLR1 L 1 A_PlaySound("halflife/weapons/reload1",CHAN_WEAPON);
		HLR1 MNOPQRSTUVWXYZ[\] 1;
		HLR2 A 1 A_HLReload("halflife/weapons/reload2");
		HLR2 BCDEFGHIJKLM 1;
		Goto Ready;
	Reload2:
		HLR2 NOPQRSTUVWX 1;
		HLR2 Y 1 A_PlaySound("halflife/weapons/reload1",CHAN_WEAPON);
		HLR2 Z[\] 1;
		HLR3 ABCDEFGHIJKLM 1;
		HLR3 N 1 A_HLReload("halflife/weapons/reload2");
		HLR3 OPQRSTUVWXYZ 1;
		Goto Ready;
	}
}
class HLPistolMag : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 17;
	}
}

//====================
// Beretta Clip
//====================

class HLPistolAmmo : Ammo
{
	Default
	{
		Tag "9mm";
		Radius 8;
		Height 4;
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "Picked up a 9mm pistol magazine.";
		Inventory.Amount 17;
		Inventory.MaxAmount 250;
		Ammo.BackpackAmount 17;
		Ammo.BackpackMaxAmount 400;
		Inventory.Icon "i_bul1";
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	}
}

//=====================
// Python (357 Magnum)
//=====================

class HLMagnumPickup : ParanoidWeaponGiver
	{Default{DropItem "HLMagnum";}}

class HLMagnum : ParanoidWeapon
{
	Default
	{
		Tag "Python";
		Radius 4;
		Height 4;
		Weapon.Kickback 100;
		Weapon.SelectionOrder 1500;
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoType "HLMagnumAmmo";
		Weapon.AmmoType2 "HLMagnumMag";
		Inventory.PickupMessage "Picked up a .357 Magnum revolver. You Feel Lucky, Punk!?";
		Obituary "%o's armor was pierced by %k's Python.";
		Inventory.Icon "i_pist2";
	}
	States
	{
	Ready:
		HLI1 A 1 A_HLWeaponReady(6,1);
		Loop;
	Fire:
		HLI1 A 0 A_HLDryFireCheck;
		HLF1 A 1 A_HLShootLead(0,0,1,85,450,"halflife/weapons/magnumshot",2);
		HLF1 BCDEFGHIJ 1 A_HLWeaponRecoil(-50);
		HLF1 KLMNOPQRSTUVWXYZ[\] 1;
		HLF2 A 1;
		HLI1 A 0 A_CheckReload;
		Goto Ready;
	Flash:
		TNT1 A 4 A_Light1;
		TNT1 A 3 A_Light2;
		Goto LightDone;
	Reload:
		HLR1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR3 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR4 ABCDEFGHIJKL 1;
		HLR4 M 1 A_HLReload("halflife/weapons/m357reload");
		HLR4 NOPQRSTUVWX 1;
		Goto Ready;
	}
}
class HLMagnumMag : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 6;
	}
}

//=============
// Python Ammo
//=============

class HLMagnumAmmo : Ammo
{
	Default
	{
		Tag ".357";
		Radius 4;
		Height 4;
		Inventory.PickupSound "misc/w_pkup";
		Inventory.Amount 6;
		Inventory.MaxAmount 36;
		Ammo.BackpackAmount 6;
		Ammo.BackpackMaxAmount 50;
		Inventory.Icon "i_bul2";
		Inventory.PickupMessage "Picked up a speedloader of .357 Magnum ammo.";
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	}
}

class HLMagnumAmmoBox : HLMagnumAmmo
{
	Default
	{
		Radius 6;
		Inventory.PickupMessage "Picked up a box of .357 Magnum ammo.";
		Inventory.Amount 12;
	}
}

//===================
// Paranoid Shotgun
//===================

class HLShotgunPickup : ParanoidWeaponGiver
	{Default{DropItem "HLShotgun";}}

//=============================
// Paranoid Shotgun (Vertical)
//=============================

class HLShotgunPickup2 : HLShotgunPickup
{
	Default
	{
		Radius 4;
		Height 32;
	}
}

class HLShotgun : ParanoidWeapon
{
	Default
	{
		Tag "Shotgun";
		Height 4;
		Weapon.SelectionOrder 900;
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoType "HLShotgunAmmo";
		Weapon.AmmoType2 "HLShotgunMag";
		Inventory.PickupMessage "Picked up a shotgun.";
		Obituary "%o swallowed %k's lead pellets.";
		Inventory.Icon "i_sgun1";
	}
	States
	{
	Deselect:
		HLD1 ABCDEFGHIJKL 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select1:
		HLU1 ABCDEFGHIJKL 1;
	Ready:
		HLI1 A 1 A_HLWeaponReady(8,1);
		Wait;
	Fire:
		HLI1 A 0 A_HLDryFireCheck;
		HLF1 A 1 A_HLShootLead(5,5,6,8,350,"halflife/weapons/shotgunsingle",1,4);
		HLF1 BCDEFGH 1 A_HLWeaponRecoil(-50);
		HLF1 I 1 A_CheckReload;
		HLF1 JKLMN 1;
		HLF1 O 1 A_PlaySound("halflife/weapons/shotgunpump",CHAN_WEAPON);
		HLF1 PQRSTUVWXYZ[\] 1;
		HLF2 A 1;
		HLF2 B 1 A_Refire;
		Goto Ready;
	AltFire:
		TNT1 A 0 A_JumpIf(CountInv("HLShotgunMag")<2,"Fire");	//if less than two, will shoot only one
		HLF2 C 1
		{
			A_HLShootLead(10,10,12,8,700,"halflife/weapons/shotgundouble",1,4);
			A_TakeInventory("HLShotgunMag",1,TIF_NOTAKEINFINITE);
		}
		HLF2 DEFGHIJKLMNOPQ 1 A_HLWeaponRecoil(-50);
		HLF2 RSTUVWXYZ[\ 1;
		HLF2 ] 1 A_CheckReload;
		HLF3 ABC 1;
		HLF3 D 1 A_PlaySound("halflife/weapons/shotgunpump",CHAN_WEAPON);
		HLF3 EFGHIJKLMNOPQRST 1;
		HLI1 A 13;
		Goto Ready;
	Reload:
		HLR1 ABCDEFGH 3;
	ReloadLoop:
		HLR1 JKLM 2;
		HLR1 N 2
		{
			A_PlaySound("halflife/weapons/reload1",CHAN_WEAPON);
			A_TakeInventory("HLShotgunAmmo",1,TIF_NOTAKEINFINITE);
			A_GiveInventory("HLShotgunMag",1);
		}
		HLR1 OPQR 2;
		HLR1 S 2 A_WeaponReady(WRF_NOBOB);
		HLR1 S 0
		{
			if(CountInv("HLShotgunMag")<8&&CountInv("HLShotgunAmmo"))
				return ResolveState("ReloadLoop");
			return state(null);
		}
	ReloadDone:
		HLR1 TUVW 1;
		HLR1 X 1 A_PlaySound("halflife/weapons/shotgunpump",CHAN_WEAPON);
		HLR1 YZ[\] 1;
		HLR2 ABCDEFGHI 1;
		Goto Ready;
	}
}
class HLShotgunMag : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 8;
	}
}

//=========================
// Paranoid Shotgun Shells
//=========================

class HLShotgunAmmo : Ammo
{
	Default
	{
		Tag "Shell";
		Radius 4;
		Height 4;
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "Picked up three shotgun shells.";
		Inventory.Amount 3;
		Inventory.MaxAmount 125;
		Ammo.BackpackAmount 3;
		Inventory.Icon "i_shel1";
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	}
}

class HLShotgunBox : HLShotgunAmmo
{
	Default
	{
		Radius 6;
		Inventory.PickupMessage "Picked up a box of shotgun shells.";
		Inventory.Amount 12;
	}
}

//===================
// 9mm Assault Rifle
//===================

class HLAssaultRiflePickup : ParanoidWeaponGiver
{
	Default
	{
		ParanoidWeaponGiver.Weapon1 "HLAssaultRifleMag",25;
		DropItem "HLAssaultRifle";
	}
	override bool TryPickup(in out Actor toucher)
	{
		if(WeaponGiver.TryPickup(toucher))
		{
			toucher.A_GiveInventory(Weapon1,Weapon1Amount);
			toucher.A_GiveInventory("HLGrenadeLauncherAmmo");
			GoAwayAndDie();
			return true;
		}
		if(toucher.A_GiveInventory(Weapon2,Weapon1Amount))
		{
			GoAwayAndDie();
			return true;
		}
		return false;
	}
}

//=============================
// 9mm Assault Rifle (Vertical)
//=============================

class HLAssaultRiflePickup2 : HLAssaultRiflePickup
{
	Default
	{
		Radius 4;
		Height 32;
	}
}

class HLAssaultRifle : ParanoidWeapon
{
	Default
	{
		Tag "Assault Rifle";
		Height 4;
		Weapon.SelectionOrder 1000;
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoType "HLPistolAmmo";
		Weapon.AmmoType2 "HLAssaultRifleMagEx";
		Inventory.PickupMessage "Picked up a 9mm assault rifle.";
		Obituary "%o felt %k's M4.";
		Inventory.Icon "i_arifl1";
	}
	States
	{
	Deselect:
		HLU1 KJIHGFEDCBA 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select1:
		HLU1 ABCDEFGHIJK 1;
	Ready:
		HLI1 A 1 A_HLWeaponReady(50,1,"HLAssaultRifleMag");
		Loop;
	Fire:
		HLI1 A 0 A_HLDryFireCheck("HLAssaultRifleMag");
		HLF1 A 1 A_HLShootLead(3,3,-1,5,100,"halflife/weapons/machinegunshot",1,2,"HLAssaultRifleMag");
		HLF1 BC 1 A_HLWeaponRecoil(-50);
		HLF1 D 1;
		HLF1 E 3 A_Refire;
		HLF1 FG 3;
		Goto Ready;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("HLGrenadeLauncherAmmo",1,2);
		HLI1 A 5 A_PlaySound("halflife/weapons/noammo",CHAN_WEAPON);
		Goto Ready;
		HLF1 H 1
		{
			for(int i=0;i<2;i++)
				A_FireProjectile("GunSmoke",0,0,6,6);
			A_PlaySound("halflife/weapons/grenadelaunch",CHAN_WEAPON);
			actor gren=A_FireProjectile("M16Grenade",0,0,5,0,FPF_NOAUTOAIM);
			if(gren!=null)
				gren.vel.z+=2;
			A_AlertMonsters();
			A_HLWeaponRecoil(400);
			A_TakeInventory("HLGrenadeLauncherAmmo",1,TIF_NOTAKEINFINITE);
		}
		HLF1 IJKLMNOP 1 A_HLWeaponRecoil(-50);
		HLF1 QRSTUVWXYZ[\] 1;
		HLF2 ABCDEFGHIJKL 1;
		TNT1 A 0 A_CheckReload;
		Goto Ready;
	Reload:
		HLR1 ABCDEFG 1;
		HLR1 H 1 A_PlaySound("halflife/weapons/m16cliprelease",CHAN_WEAPON);
		HLR1 IJKLMNOPQRSTUVWXYZ[ 1;
		HLR1 \] 1;
		HLR2 A 1;
		HLR2 B 1 A_HLReload("halflife/weapons/m16clipinsert","HLAssaultRifleMag",50);
		HLR2 CDEFGHIJKLMNOPQR 1;
		Goto Ready;
	}
}
class HLAssaultRifleMag : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 50;
	}
}
class HLAssaultRifleMagEx : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 70;
	}
}

class M16Grenade : Actor
{
	Default
	{
		Projectile;
		Speed 20;
		Damage 20;
		Radius 10;
		Height 20;
		-NoGravity
		Gravity .25;
		DeathSound "halflife/weapons/randomexplo";
		Decal "Scorch";
	}
	States
	{
	Spawn:
		POSS A 1
		{
			A_SetPitch(pitch+10);
			for(int i=0;i<2;i++)
				A_SpawnItemEx("GrenadeTrail",random(1,-3),random(3,-3),2+random(3,-3));
		}
		Loop;
	Death:
		TNT1 A 1
		{
			A_Explode(170,140);
			A_SpawnItemEx("NewExplosionMedium");
		}
		Stop;
	}
}

class GrenadeTrail : Actor
{
	Default
	{
		Projectile;
		+NOINTERACTION
		Scale .35;
		RenderStyle "Add";
		Alpha .67;
	}
	States
	{
	Spawn:
		WPXS ABCDEFGIHJKLMNOPQRSTUVWXYZ 1 NODELAY A_FadeOut(.03);
		Wait;
	}
}

//=======================
// Assault Rifle Magazine
//=======================

class HLAssaultRifleAmmo : HLPistolAmmo
{
	Default
	{
		Inventory.PickupMessage "Picked up a 9mm assault rifle magazine.";
		Inventory.Amount 25;
	}
}

class HLAssaultRifleBox : HLPistolAmmo
{
	Default
	{
		Radius 12;
		Height 8;
		Inventory.PickupMessage "Picked up a box of 9mm ammo.";
		Inventory.Amount 50;
	}
}

//=========
// M16Gren
//=========

class HLGrenadeLauncherAmmo : Ammo
{
	Default
	{
		Tag "Grenade";
		Inventory.PickupMessage "Picked up two 40mm grenades.";
		Inventory.Amount 2;
		Inventory.MaxAmount 10;
		Ammo.BackpackAmount 2;
		Ammo.BackpackMaxAmount 20;
		Inventory.Icon "i_gren1";
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	}
}

//=====================
// RPG Launcher
//=====================

class HLRPG : ParanoidWeapon
{
	Default
	{
		Tag "RPG Launcher";
		Radius 32;
		Height 8;
		-WEAPON.NOALERT
		-WEAPON.AMMO_CHECKBOTH
		Inventory.PickupMessage "Picked up an RPG Launcher! Let's blow things up!";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "HLRPGAmmo";
		Inventory.Icon "i_rlau1";
	}
	States
	{
	Deselect:
		HLD1 A 1 A_JumpIf(!CountInv("HLRPGAmmo"),"DeselectEmpty");
		HLD1 BCDEFGHIJKLMNO 1;
		TNT1 A 0 A_Lower;
		Wait;
	DeselectEmpty:
		HLD1 QRSTUVWXYZ[\] 1;
		HLD2 AB 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select1:
		HLU1 A 1 A_JumpIf(!CountInv("HLRPGAmmo"),"SelectEmpty");
		HLU1 BCDEFGHIJKLMNO 1;
		Goto Ready;
	SelectEmpty:
		HLU1 QRSTUVWXYZ[\] 1;
		HLU2 AB 1;
	Ready:
		TNT1 A 0 A_JumpIf(!CountInv("HLRPGAmmo"),2);
		HLI1 A 1 A_WeaponReady;
		Loop;
		HLI1 B 1 A_WeaponReady;
		Loop;
	Fire:
		HLF1 A 1
		{
			A_FireProjectile("HLRocket",0,1,5,5);
			A_HLWeaponRecoil(800);
			A_PlaySound("halflife/weapons/RPGFire",CHAN_WEAPON);
			A_SpawnItemEx("LighCheapHack");
		}
		HLF1 A 0 A_Recoil(7);
		HLF1 BCDEFGHI 1 A_HLWeaponRecoil(-100);
		HLF1 JK 1;
		HLF1 L 14;
		HLR1 A 1 A_CheckReload;
		HLR1 BCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR3 AB 1;
		Goto Ready;
	}
}

class HLRocket : Actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 35;
		Damage 70;
		PROJECTILE;
		+EXTREMEDEATH
		+ROCKETTRAIL
		+FORCERADIUSDMG
		DeathSound "halflife/weapons/randomexplo";
		Decal "HugeScorch";
	}
	States
	{
	Spawn:
		POSS A 1 Bright
		{
			A_SpawnItemEx("RPGTrail2",random(3,-3),random(3,-3),random(3,-3),zvel:frandom(.25,1.25));
			for(int i=0;i<2;i++)
				A_SpawnItemEx("RPGTrail1",random(3,-3),random(3,-3),random(3,-3),zvel:frandom(.25,1.25));
		}
		Loop;
	Death:
		TNT1 A 20
		{
			A_Explode(512,256);
			A_SpawnItemEx("NewExplosionMediumBig");
			Radius_Quake(8,20,0,35,0);
		}
		Stop;
	}
}

class RPGTrail1 : Actor
{
	Default
	{
		+NOCLIP
		+NOGRAVITY
		RenderStyle "Add";
		Alpha .6;
		Scale .4;
	}
	States
	{
	Spawn:
		TNT1 A 2;
	 	SPIR KLMNO 3;
		Stop;
	}
}

class RPGTrail2 : RPGTrail1
{
	Default
	{
		Scale .3;
	}
	States
	{
	Spawn:
		TNT1 A 2;
		WPXS ACEGIKMOQSUWY[] 1;
		Stop;
	}
}

//===================
// RPG Launcher Ammo
//===================

class HLRPGAmmo : Ammo
{
	Default
	{
		Tag "RPG";
		Radius 4;
		Height 20;
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "Picked up an RPG round.";
		Inventory.MaxAmount 10;
		Ammo.BackpackAmount 2;
		Ammo.BackpackMaxAmount 20;
		Inventory.Icon "i_rock1";
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	}
}

//=============
// Cross-Bow
//=============

class HLXBowPickup : ParanoidWeaponGiver
	{Default{DropItem "HLXBow";}}

class HLXBow : ParanoidWeapon
{
	bool zoomtoken;
	Default
	{
		Radius 16;
		Height 8;
		Weapon.AmmoType "HLXBowAmmo";
		Weapon.AmmoType2 "HLXBowMag";
		Weapon.AmmoGive 5;
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Inventory.PickupMessage "Picked up a crossbow. Snipe and impale!";
		Tag "Crossbow";
		+WEAPON.NOAUTOFIRE
		Inventory.Icon "i_xbow1";
	}
	States
	{
	Deselect:
		HLD1 A 0
		{
			invoker.ZoomToken=0;
			A_ZoomFactor(1.0,1);
		}
		Goto Super::Deselect;
	Ready:
		HLI1 A 0 A_JumpIf(invoker.ZoomToken,4);
		HLI1 A 0 A_JumpIf(!CountInv("HLXBowMag"),2);
		HLI1 A 1 A_HLWeaponReady(5);
		Loop;
		HLI1 B 1 A_HLWeaponReady(5);
		Loop;
		R016 A 1 A_HLWeaponReady(5);
		Loop;
	Fire:
		"####" # 0 A_HLDryFireCheck;
		TNT1 A 0
		{
			A_TakeInventory("HLXBowMag",1);
			A_PlaySound("HLCrossbow/Fire",CHAN_WEAPON);
			A_FireProjectile("XbowBolt",0,0,4,3);
			A_SetPitch(pitch-2.8);
			//Fire1 (shoot and recock)
			if(CountInv("HLXBowMag"))
			{
				if(invoker.ZoomToken)
					return resolvestate("Fire1Zoom");
				else
					return resolvestate("Fire1");
			}
			//Fire2 (shoot, reload, recock)
			else if(CountInv("HLXBowAmmo"))
			{
				if(invoker.ZoomToken)
					return resolvestate("Fire2Zoom");
				else
					return resolvestate("Fire2");
			}
			//Fire3 (shoot, no reload or recock)
			else
			{
				if(invoker.ZoomToken)
					return resolvestate("Fire3Zoom");
				else
					return resolvestate("Fire3");
			}
			return state(null);
		}
	Fire1:
		HLF1 BD 1;
		HLF1 FHJLNPR 1 A_SetPitch(pitch+0.4);
		HLF1 TVX 1;
		HLF1 Z 1 A_PlaySound("HLCrossbow/Reload",CHAN_WEAPON);
		HLF1 \ 1;
		HLF2 ACEGIKMOQSUWY[] 1;
		HLF3 BDFHJLNPRTVXZ\ 1;
		HLF4 AC 1;
		Goto Ready;
	Fire1Zoom:
		R016 A 2;
		R016 AAAAAAA 1 A_SetPitch(pitch+0.4);
		R016 A 3;
		R016 A 35 A_PlaySound("HLCrossbow/Reload",CHAN_WEAPON);
		Goto Ready;
	Fire2:
		HLF5 EF 1;
		HLF5 GHIJKLM 1 A_SetPitch(pitch+0.4);
		HLF5 NO 1;
	Fire2Cont:
		HLF5 P 1 A_CrossbowZoomOut();
		HLF5 QRSTUVWXYZ[\] 1;
		HLF6 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLF7 ABCDEFGHIJKLMNOP 1;
		HLF7 Q 1 A_HLReload("HLCrossbow/Reload");
		HLF7 RSTUVWXYZ[\] 1;
		HLF8 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLF9 ABCDEFGHIJKLMNOPQRSTUVWXY 1;
		Goto Ready;
	Fire2Zoom:
		R016 A 2;
		R016 AAAAAAA 1 A_SetPitch(pitch+0.4);
		R016 A 2;
		Goto Fire2Cont;
	Fire3:
		HLF9 Z[ 1;
		HLF9 \] 1 A_SetPitch(pitch+0.4);
		HLF0 ABCDE 1 A_SetPitch(pitch+0.4);
		HLF0 FGHIJK 1;
		Goto Ready;
	Fire3Zoom:
		R016 A 2;
		R016 AAAAAAA 1 A_SetPitch(pitch+0.4);
		R016 A 6;
		Goto Ready;
	Reload:
		HLR1 A 1 A_CrossbowZoomOut;
		HLR1 BCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR2 ABCDEFGHIJKLMNOPQRSTUVW 1;
		HLR2 XYZ[\] 1;
		HLR3 ABCDEFGHIJ 1;
		HLR3 K 1 A_HLReload("HLCrossbow/Reload");
		HLR3 LMNOPQRSTUVWXYZ[\] 1;
		HLR4 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR5 ABCDEFGHIJKLMNOPQRS 1;
		Goto Ready;
	AltFire:
		R016 A 1
		{
			if(invoker.ZoomToken)
				A_CrossbowZoomOut();
			else
			{
				A_PlaySound("BS/Headhunter/ZoomIn",7);
				invoker.ZoomToken=1;
				A_ZoomFactor(5);
			}
		}
		Goto Ready;
	}
	action void A_CrossbowZoomOut()
	{
		A_PlaySound("BS/Headhunter/ZoomOut",7);
		invoker.ZoomToken=0;
		A_ZoomFactor(1,1);
	}
}
class HLXBowMag : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 5;
	}
}

class HLXBowAmmo : Ammo
{
	Default
	{
		Radius 8;
		Height 4;
		inventory.pickupmessage "Picked up a crossbow bolt.";
		inventory.pickupsound "misc/w_pkup";
		inventory.maxamount 50;
		ammo.backpackamount 5;
		inventory.icon "i_bolt1";
	}
	states
	{
	Spawn:
		POSS A -1;
		Stop;
	}
}

class HLXBowBox : HLXBowAmmo
{
	Default
	{
		inventory.amount 5;
		inventory.pickupmessage "Picked up a clip of crossbow bolts.";
	}
}

class XbowBolt : FastProjectile
{
	Default
	{
		PROJECTILE;
		Radius 3;
		Height 5;
		Speed 80;
		Damage 50;
		DeathSound "weapons/crbreak";
		Decal "BulletChip";
		+BLOODSPLATTER
	}
	States
	{
	Spawn:
		POSS A -1;
		Loop;
	Xdeath:
		TNT1 A 4 A_PlaySound("HLCrossbow/Hitbody",7);
		Stop;
	Death:
		TNT1 A 4 A_PlaySound("HLCrossbow/Hitwall",7);
		Stop;
	}
}

class Missile : Rocket
{
//	Speed 40
}

//=============
// Gluon Gun
// Developed by Major Cooke for AeOD; modified by DBThanatos & Kappes Buur
//=============

class HLGluonGunPickup : ParanoidWeaponGiver
	{Default{DropItem "HLGluonGun";}}

class HLGluonGun : ParanoidWeapon
{
	int GluonSoundCountdown;
	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoType "HLGluonGunAmmo";
		Weapon.AmmoType2 "HLGluonGunMag";
		Weapon.AmmoGive 40;
		Weapon.SelectionOrder 10333;
		Inventory.PickupMessage "Picked up the Gluon Gun.";
		Tag "Gluon gun";
		Inventory.Icon "i_gluon1";
	}
	States
	{
	Deselect:
		HLD1 ABCDEFGHIJKLM 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select1:
		HLU1 ABCDEFGHIJKLM 1;
	Ready:
		HLI1 A 1 A_HLWeaponReady(40,1);
		Loop;
	Fire:
		HLF1 A 0 A_HLDryFireCheck;
		HLF1 A 0
		{
			invoker.GluonSoundCountdown=35;
			A_PlaySound("weapons/gluonstart",CHAN_WEAPON);
		}
	ShotLoop:
		HLF1 A 1 BRIGHT A_HLEgon1;
		HLF1 B 1 BRIGHT A_HLEgon2;
		HLF1 C 1 BRIGHT A_HLEgon3;
		HLF1 D 1 BRIGHT A_HLEgon2;
		HLF1 D 0 A_HLEgon4;
		HLF1 D 0 A_ReFire("ShotLoop2");
	ShotLoop2:
		HLF1 E 1 BRIGHT A_HLEgon1;
		HLF1 F 1 BRIGHT A_HLEgon2;
		HLF1 G 1 BRIGHT A_HLEgon3;
		HLF1 H 1 BRIGHT A_HLEgon2;
		HLF1 H 0 A_HLEgon4;
		HLF1 H 0 A_ReFire("ShotLoop3");
	ShotLoop3:
		HLF1 I 1 BRIGHT A_HLEgon1;
		HLF1 J 1 BRIGHT A_HLEgon2;
		HLF1 K 1 BRIGHT A_HLEgon3;
		HLF1 L 1 BRIGHT A_HLEgon2;
		HLF1 L 0 A_HLEgon4;
		HLF1 L 0 A_ReFire("ShotLoop4");
	ShotLoop4:
		HLF1 M 1 BRIGHT A_HLEgon1;
		HLF1 N 1 BRIGHT A_HLEgon2;
		HLF1 O 1 BRIGHT A_HLEgon3;
		HLF1 P 1 BRIGHT A_HLEgon2;
		HLF1 P 0 A_HLEgon4;
		HLF1 P 0 A_ReFire("ShotLoop");
	PowerDown:
		HLI1 A 6 A_PlaySound("weapons/gluonoff",CHAN_WEAPON);
		Goto Ready;
	Reload:
		HLI1 A 8;
		HLR1 ABCDEFGHIJKLMNOPQRSTU 1;
		HLR1 V 1 A_HLReload("halflife/weapons/reload1");
		HLR1 WXYZ[\] 1;
		HLR2 A 1;
		HLR2 B 1 A_PlaySound("halflife/weapons/reload2",CHAN_WEAPON);
		HLR2 CDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLR3 ABC 1;
		Goto Ready;
	}
	action state A_HLEgon1()
	{
		if(!CountInv("HLGluonGunMag"))
		{
			A_PlaySound("halflife/weapons/noammo",6);
			return resolvestate("PowerDown");
		}
		A_AlertMonsters();
		A_TakeInventory("HLGluonGunMag",1,TIF_NOTAKEINFINITE);
		A_FireProjectile("GluonPrimaryBurst",0,0,4,2);
		A_FireProjectile("GluonFire",0,0,4,2);
		A_FireProjectile("GluonFire2",0,0,4,2);
		A_FireProjectile("GluonFire3",0,0,4,2);
		return state(null);
	}
	action void A_HLEgon2()
		{A_FireProjectile("GluonFakeBurst",0,0,4,2);}
	action void A_HLEgon3()
	{
		A_FireProjectile("GluonPrimaryBurst",0,0,4,2);
		A_FireProjectile("GluonFire4",0,0,4,2);
		A_FireProjectile("GluonFire5",0,0,4,2);
	}
	action void A_HLEgon4()
	{
		invoker.GluonSoundCountdown--;
		if(!invoker.GluonSoundCountdown)
			A_PlaySound("weapons/gluonrun",CHAN_WEAPON,1,1);
	}
	action void A_HLEgonAnimate()
	{
		if(player==null)
			return;
		let weapon=player.ReadyWeapon;
		if(weapon==null)
			return;
		player.GetPSprite(PSP_WEAPON).frame++;
		if(player.GetPSprite(PSP_WEAPON).frame>15)
			player.GetPSprite(PSP_WEAPON).frame=0;
	}
}
class HLGluonGunMag : Ammo
{
	Default
	{
		+INVENTORY.IGNORESKILL
		Inventory.MaxAmount 40;
	}
}

//=========================================
//The main projectiles fired. Their all parents of GluonPrimaryBurst with the only difference
//being the outer-trail spawn locations. I'm going for a spiral after all...
//Skip past GluonPrimaryBurst12 for the effects.
//=========================================

class GluonPrimaryBurst : Actor
{
	int life[2];
	Default
	{
		Speed 2;
		DamageFunction (random(1,6));
		Radius 2;
		Height 2;
		Scale .75;
		Projectile;
		+DONTREFLECT
		+EXTREMEDEATH
		+FORCERADIUSDMG
		+NODAMAGETHRUST
		+SEEKERMISSILE
		+SKYEXPLODE
		+EXPLODEONWATER
		+NOTIMEFREEZE
		DamageType "PlasmaDisint";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("GluonSpiral",0,0,8,vel.x,vel.y,vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS);
		TNT1 A 0 A_Jump(256,"Movement");
	Movement:
		TNT1 A 0 A_JumpIf(life[0]>=25000,"Death");
		//This must be done multiple times, or else it will jump beyond walls. This could
		//easily cause an engine overload.
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0
		{
			if(life[0]<400&&!CheckIfSeen())
				A_SpawnItemEx("GluonTrailV2",vel.x+frandom(-1.0,1.0),0,vel.z+frandom(-1.0,1.0),flags:SXF_ABSOLUTEMOMENTUM);
			life[0]++;
		}
		Loop;
	MoveOut:
		TNT1 A 5;
		Goto Movement;
	Death:
		TNT1 A 1
		{
			A_Explode(random(40,60),75,0);
			A_SpawnItemEx("GluonTrail");
			A_SpawnProjectile("RipperEffect",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER);
		}
		Stop;
	}
}

class GluonFakeBurst : GluonPrimaryBurst
{
	Default
	{
		Damage 0;
		+PAINLESS
		+BLOODLESSIMPACT
		+DONTSPLASH
	}
	States
	{
	Movement:
		TNT1 A 0 A_JumpIf(life[0]>=20000,"Death");
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET,1);
		Goto MoveOut;
		TNT1 A 0
		{
			if(life[0]<4000&&!CheckIfSeen())
				A_SpawnItemEx("GluonTrailV2",vel.x+frandom(-1.0,1.0),0,vel.z+frandom(-1.0,1.0),flags:SXF_ABSOLUTEMOMENTUM);
			life[0]++;
		}
		Loop;
	MoveOut:
	Death:
		TNT1 A 1;
		Stop;
	}
}

class GluonSpiral : Actor
{
	int life;
	Default
	{
		Projectile;
		+DONTREFLECT
		+PAINLESS
		+BLOODLESSIMPACT
		+NOTIMEFREEZE
		Radius 5;
		Height 5;
		Speed 5;
		WeaveIndexZ 16;
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		{
			int token=target.CountInv("GluonTrailToken");
			if(token)
			{
				for(int i=0;i<token*5;i++)
					A_Weave(-1,-1,1,1);
				if(token>=14)
					A_TakeInventory("GluonTrailToken",giveto:AAPTR_TARGET);
			}
			A_GiveInventory("GluonTrailToken",1,AAPTR_TARGET);
		}
	Moving:
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,vel.x,vel.y,vel.z,0,WARPF_ABSOLUTEOFFSET|WARPF_INTERPOLATE,1);
		Goto Death;
		TNT1 A 0
		{
			A_Weave(1,1,1,1);
			A_SpawnItemEx("GluonTrailV3");
			life++;
		}
		TNT1 A 0 A_JumpIf(life>=350,"Death");
		Loop;
	Death:
		TNT1 A 1;
		Stop;
	}
}

class GluonTrailToken : Inventory{Default{Inventory.MaxAmount 14;}}

class GluonTrailV2 : Actor
{
	Default
	{
		+NOTIMEFREEZE
		+NOINTERACTION
		Scale .2;
		RenderStyle "Add";
		Translation "0:255=%[0,0,0]:[0.15,0.15,1]";
		Alpha .25;
	}
	States
	{
	Spawn:
		PLSE B 2 BRIGHT NODELAY
		{
			A_SetScale(frandom(0.2,0.35));
			self.frame=random(1,3);
		}
		Stop;
	}
}

class GluonTrailV3 : GluonTrailV2
{
	Default
	{
		Scale .2;
		Alpha .25;
	}
	States
	{
	Spawn:
		LPUF A 2;
		Stop;
	}
}

class GluonPrimaryBurst2 : GluonPrimaryBurst
{
	States
	{
	Spawn:
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,90,CMF_AIMDIRECTION,30);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,90,CMF_AIMDIRECTION,60);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,90,CMF_AIMDIRECTION,90);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,-90,CMF_AIMDIRECTION,60);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,-90,CMF_AIMDIRECTION,30);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,-90,CMF_AIMDIRECTION,0);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,-90,CMF_AIMDIRECTION,-30);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,-90,CMF_AIMDIRECTION,-60);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,-90,CMF_AIMDIRECTION,-90);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,90,CMF_AIMDIRECTION,-60);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,90,CMF_AIMDIRECTION,-30);
		TNT1 A 1 A_SpawnProjectile("GluonOutsideTrailSpawner",0,0,90,CMF_AIMDIRECTION,0);
		Loop;
	}
}

class GluonPrimaryBurst3 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+1;}}
class GluonPrimaryBurst4 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+2;}}
class GluonPrimaryBurst5 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+3;}}
class GluonPrimaryBurst6 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+4;}}
class GluonPrimaryBurst7 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+5;}}
class GluonPrimaryBurst8 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+6;}}
class GluonPrimaryBurst9 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+7;}}
class GluonPrimaryBurst10 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+8;}}
class GluonPrimaryBurst11 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+9;}}
class GluonPrimaryBurst12 : GluonPrimaryBurst2{States{Spawn:Goto Super::Spawn+10;}}

//The Gluon "flash", which up close, is deadly to those who get in front of the gun.
class GluonFire : Actor
{
	Default
	{
		Speed 20;
		DamageFunction (1);
		Radius 1;
		Height 1;
		Projectile;
		+RIPPER
		+DONTREFLECT
		+EXTREMEDEATH
		DamageType "PlasmaDisint";
	}
	States
	{
	Spawn:
		TNT1 A 1;
		Stop;
	}
}

class GluonFire2 : GluonFire
{
	States
	{
	Spawn:
		TNT1 A 2;
		Stop;
	}
}

class GluonFire3 : GluonFire
{
	States
	{
	Spawn:
		TNT1 A 3;
		Stop;
	}
}

class GluonFire4 : GluonFire{Default{Speed 30;}}
class GluonFire5 : GluonFire2{Default{Speed 30;}}

class GluonTrail : Actor
{
	Default
	{
		Scale 1.15;
		Projectile;
		+NOINTERACTION
		RenderStyle "Add";
		Alpha .45;
	}
	States
	{
	Spawn:
		LPUF ABC 2;
		Stop;
	}
}

class GluonExplosion : GluonTrail
{
	Default
	{
		+NOCLIP
		+EXTREMEDEATH
		+FORCERADIUSDMG
		DamageType "PlasmaDisint";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Explode(10,75,0);
		Stop;
	}
}

class GluonOutsideTrailSpawner : Actor
{
	Default
	{
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 1;
		TNT1 A 1 A_SpawnItemEx("GluonTrail");
		Stop;
	}
}

class RipperEffect : Actor
{
	Default
	{
		Projectile;
		+RIPPER
		+EXTREMEDEATH
		+DONTREFLECT
		+PAINLESS
		Speed 1;
		Damage 1;
		Radius 1;
		Height 1;
		DamageType "PlasmaDisint";
	}
	States
	{
	Spawn:
		TNT1 A 1;
		Stop;
	}
}

//***************************************************************
class HLGluonGunAmmo : Ammo
{
	Default
	{
		radius 8;
		height 8;
		inventory.pickupmessage "Picked up some depleted U-235.";
		inventory.amount 20;
		inventory.maxamount 100;
		inventory.pickupsound "misc/w_pkup";
		ammo.backpackamount 20;
		Scale .75;
		inventory.icon "i_uran1";
	}
	states
	{
	Spawn:
		POSS A -1;
		Loop;
	}
}

//****
// Hornet Gun (aka HiveHand)
//****

class HLHornetGun : ParanoidWeapon
{
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		-WEAPON.NOALERT
		-WEAPON.AMMO_CHECKBOTH
		Weapon.AmmoType1 "HLHornetGunAmmo";
		Weapon.AmmoType2 "HLHornetGunAmmo";
		Weapon.AmmoGive1 10;
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 1;
		Inventory.PickupMessage "Picked up an alien hornet gun.";
		Tag "Hornet Gun";
		Inventory.Icon "i_hhand1";
	}
	States
	{
	Deselect:
		HLD1 ABCDEFGHIJKLMNOPQR 2;
		TNT1 A 0 A_Lower;
		Wait;
	Select1:
		HLU1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLU2 A 1;
	Ready:
		HLI1 A 6
		{
			if(CountInv("HLHornetGunAmmo"))
				A_WeaponReady();
			else
				A_WeaponReady(WRF_NOFIRE);
		}
		HLI1 B 0 A_GiveInventory("HLHornetGunAmmo");
		Loop;
	Fire:
		HLF1 A 1 A_FireHornet;
		HLF1 BCEGHIJ 1;
		Goto Ready;
	AltFire:
		HLF1 A 1 A_FireHornet("HGunHornet2");
		HLF1 CHI 1;
		Goto Ready;
	Flash:
		TNT1 A 1 A_Light2;
		TNT1 A 1 A_Light1;
		Goto LightDone;
	}
	action void A_FireHornet(class<actor> hornet="HGunHornet")
	{
		A_PlaySound("halflife/monsters/hornetgrunt/flysight",CHAN_WEAPON,0.6,0);
		A_FireProjectile(hornet,0,1,8,0);
	}
}

class HLHornetGunAmmo : Ammo
{
	Default
	{
		Inventory.MaxAmount 10;
		+INVENTORY.IGNORESKILL
		inventory.icon "i_horn1";
	}
}

class HGunHornet : Actor
{
	class<actor> trail;
	Property trail:trail;
	Default
	{
		Radius 4;
		Height 4;
		Speed 20;
		Damage 5;
		+SEEKERMISSILE
		Projectile;
		HGunHornet.Trail "HGHornetTrail";
	}
	States
	{
	Spawn:
		HLI1 ABCD 1 BRIGHT A_HornetFly(SMF_LOOK);
		Loop;
	Death:
	Crash:
		TNT1 A 1
		{
			bool num;
			actor hornet;
			[num,hornet]=A_SpawnItemEx("HGHornetShooter1");
			if(bSEEKERMISSILE)
				hornet.bSEEKERMISSILE=1;
			A_PlaySound("halflife/monsters/hornetgrunt/flybuzz");
		}
		Stop;
	XDeath:
		TNT1 A 0 A_PlaySound("halflife/monsters/hornetgrunt/flyhit");
		Stop;
	}
	void A_HornetFly(int flg=0)
	{
		A_SpawnItemEx(trail);
		if(bSEEKERMISSILE)
			A_SeekerMissile(2,2,flg,90,3);
	}
}

class HGunHornet2 : HGunHornet
{
	Default
	{
		Speed 10;
		-SEEKERMISSILE
	}
	States
	{
	Spawn:
		HLI1 ABCD 1 BRIGHT A_HornetFly;
		Loop;
	}
}

class HGruntHornet : HGunHornet2
{
	Default
	{
		Radius 8;
		Height 12;
		Projectile;
		Speed 17;
		Damage 4;
		DamageType "Hornet";
		SeeSound "halflife/monsters/hornetgrunt/flysight";
		+SEEKERMISSILE
	}
}

class HGHornetShooter1 : Actor
{
	Default
	{
		+FRIENDLY
		+ISMONSTER
		Radius 1;
		Height 1;
		+NOGRAVITY
		+NOCLIP
		+LOOKALLAROUND
	}
	States
	{
	Spawn:
		TNT1 A 1 NODELAY A_Look;
		TNT1 A 0
		{
			if(CheckIfCloser(target,128))
			{
				A_FaceTarget();
				let hornet=HGruntHornetFake(A_SpawnProjectile("HGruntHornetFake",0));
				hornet.countdown=1;
				return;
			}
			int zvel=2,ang=90;
			if(random(0,1))
			{
				zvel=5;
				ang=-90;
			}
			bool num;
			actor hornet;
			[num,hornet]=A_SpawnItemEx("HGruntHornetFake",0,0,0,10,0,random(0-zvel,zvel),ang);
			if(!bSEEKERMISSILE)
			{
				hornet.frame=0;
				let thishorn=HGruntHornetFake(hornet);
				thishorn.trail="HGHornetTrail2";
			}
		}
		Stop;
	}
}

class HGruntHornetFake : HGruntHornet
{
	int countdown;
	Default
	{
		SeeSound "";
	}
	States
	{
	Spawn:
		HLI1 ABCD 1 NODELAY
		{
			if(countdown>=1)
			{
				countdown++;
				if(countdown>=21)
					return resolvestate("Death");
			}
			else
				A_HornetFly();
			return state(null);
		}
		Loop;
	Death:
	Crash:
		TNT1 A 1 A_PlaySound("halflife/monsters/hornetgrunt/flybuzz");
		Stop;
	}
}

class HGHornetTrail : Actor
{
	Default
	{
		radius 6;
		height 8;
		+NOINTERACTION
		Scale .015;
		RenderStyle "Add";
		Translation "80:111=160:167";
	}
	states
	{
	Spawn:
		X099 A 1 A_FadeOut(.03);
		Loop;
	}
}

class HGHornetTrail2 : HGHornetTrail
{
	Default
	{
		Alpha .5;
		Translation "80:111=192:223";
	}
}

//============
// Ammo Pack
//============

class AmmoPack : CustomInventory
{
	Default
	{
		Radius 6;
		Height 24;
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "Picked up an AmmoPack!";
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	Pickup:
		TNT1 A 0
		{
			A_GiveInventory("HLPistolAmmo",25);
			A_GiveInventory("HLShotgunAmmo",4);
			A_GiveInventory("HLMagnumAmmo",6);
			A_GiveInventory("HLRPGAmmo");
			A_GiveInventory("HLXBowAmmo",2);
			A_GiveInventory("HLGluonGunAmmo",10);
		}
		Stop;
	}
}