//=========================
// Zombie & Moaning Zombie
//=========================

class HLZombie : HLMonster
{
	Default
	{
		Health 150;
		Mass 100;
		Speed 1.4;
		Height 63;
		PainChance 128;
		PainChance "Crowbar",252;
		Scale .32;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/zombie/sight";
		PainSOund "halflife/monsters/zombie/pain";
		DeathSound "halflife/monsters/zombie/pain";
		ActiveSound "halflife/monsters/zombie/idle";
		maxdropoffheight 64;
		Species "HLZombie";
	}
	States
	{
	Spawn:
		HLEI A 3 NODELAY A_StopSound(7);
		HLEI A 3 A_Look;
		Wait;
	See:
		HLEW A 1 A_Chase;
		HLEW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW B 1 A_Chase;
		HLEW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW C 1 A_Chase;
		HLEW CCCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW D 1 A_Chase;
		HLEW DDDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW E 1 A_Chase;
		HLEW EEEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW F 1 A_Chase;
		HLEW FFFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLEW G 1 A_Chase;
		HLEW GGGG 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto See;
	Melee:
		HLEA AB 3 A_FaceTarget;
		HLEA C 3 A_ZombieWoosh;
		HLEA D 3 A_CustomMeleeAttack(2*random(1,8),"halflife/monsters/zombie/hit");
		HLEA E 3 A_FaceTarget;
		HLEA F 3 A_ZombieWoosh;
		HLEA G 3 A_CustomMeleeAttack(2*random(1,8),"halflife/monsters/zombie/hit");
		HLEA HIJ 3;
		Goto See;
	Pain:
		HLEP A 8
		{
			A_StopSound(6);
			A_Pain();
		}
		Goto See;
	Death:
		HLED A 3
		{
			A_StopSound(6);
			A_Scream();
		}
		HLED B 3 A_NoBlocking;
		HLED CDEFGH 2;
		HLED H 3 A_PlaySound("halflife/weapons/bodydrop");
		HLED IJ 3;
		HLED K -1;
		Stop;
	XDeath:
		TNT1 A 0
		{
			A_StopSound(6);
			A_XenGibs();
		}
		Stop;
	}
	void A_ZombieWoosh()
	{
		A_PlaySound("halflife/monsters/zombie/attswing");
		A_FaceTarget();
	}
}

class HLZombieMoan : HLZombie
{
	States
	{
	See:
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/moan",6,1,1,ATTN_STATIC);
		Goto Super::See;
	}
}