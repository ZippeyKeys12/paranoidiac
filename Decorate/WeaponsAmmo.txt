//March 30, 2010: 1630 hrs GMT
//Updated March 13, 2010 to add crossbow bolts & gluon gun ammo to ammo pack

Actor PistolHeldAmmo : Inventory
{	Inventory.MaxAmount 17	}

Actor ReloadCheck : Inventory
{	Inventory.MaxAmount 1	}

Actor MagnumHeldAmmo : Inventory
{	Inventory.MaxAmount 6	}

Actor ShotgunHeldAmmo : Inventory
{	Inventory.MaxAmount 8	}

Actor M16HeldAmmo : Inventory
{	Inventory.MaxAmount 50	}


//================
// Weapons & Ammo
//================

ACTOR Crowbar : Weapon 30001
{
   SpawnID 255
   Radius 20
   Height 4
   Weapon.Kickback 0
   Inventory.PickupSound "misc/i_pkup"
   MeleeSound "halflife/weapons/Crowbarhitbody"
   Inventory.PickupMessage "Picked up a Crowbar"
   Obituary "%k shoved %o's crowbar where the sun don't shine."
   +WEAPON.MELEEWEAPON      
   States
   {
   Ready:
      HW4I A 4 A_WeaponReady
      Loop
   Deselect:
      TNT1 A 0 A_Lower
      HW4I A 1 A_Lower
      Loop
   Select:
      TNT1 A 0 A_Raise
      HW4I A 1 A_Raise
      Loop
   Fire:
      TNT1 A 0 A_Jump(255,"Fire1","Fire2","Fire3")
   Fire1:
      TNT1 A 0 A_JumpIfCloser(80,"FireFast1")
      HW4F BC 2
      TNT1 A 0 A_PlayWeaponSound("halflife/weapons/CrowbarMiss")
      HW4F D 2 
      HW4F E 1 A_FireBullets(1,1,1,3,"CrowbarPuff2",0,80)	//to ensure the puff spawning attack hits first
      HW4F E 1 A_FireBullets(12,12,5,3,"Puffnone",0,80)
      TNT1 A 0 A_JumpIfCloser(80,"Fire1F")
      HW4F FGGGG 2
      HW4G H 2
      HW4I A 2
      Goto Ready
   Fire1F:
      HW4F F 1
      HW4F GGG 2
      HW4G H 2
      HW4I A 2
      Goto Ready
   Fire2:
      TNT1 A 0 A_JumpIfCloser(80,"FireFast2")
      HW4F B 2
      HW4G BC 2 
      TNT1 A 0 A_PlayWeaponSound("halflife/weapons/CrowbarMiss")
      HW4G D 1
      HW4G D 1 A_FireBullets(1,1,1,3,"CrowbarPuff2",0,80)
      HW4G E 1 A_FireBullets(12,12,5,3,"Puffnone",0,80)
      TNT1 A 0 A_JumpIfCloser(80,"Fire2F")
      HW4G FG 1
      TNT1 A 8
      HW4G H 2
      HW4I A 2
      Goto Ready
   Fire2F:
      HW4G FG 1
      TNT1 A 4
      HW4G H 1
      HW4I A 2
      Goto Ready
   Fire3:
      TNT1 A 0 A_JumpIfCloser(80,"FireFast3")
      HW4H BCD 2
      TNT1 A 0 A_PlayWeaponSound("halflife/weapons/CrowbarMiss")
      HW4H E 2 
      HW4H F 1 A_FireBullets(1,1,1,3,"CrowbarPuff2",0,80)
      HW4H F 1 A_FireBullets(12,12,5,3,"Puffnone",0,80)
      TNT1 A 0 A_JumpIfCloser(90,"Fire3F")
      HW4H G 2
      TNT1 A 8
      HW4H HIJKLM 1
      Goto Ready
   Fire3F:
      HW4H G 2
      TNT1 A 3
      HW4H HJKM 1
      Goto Ready
   FireFast1:
      HW4F BC 2
      TNT1 A 0 A_PlayWeaponSound("halflife/weapons/CrowbarMiss")
      HW4F D 1 A_FireBullets(1,1,1,3,"CrowbarPuff2",0,80)
      HW4F E 1 A_FireBullets(12,12,5,3,"Puffnone",0,80)
      HW4F F 1
      HW4F GG 2
      HW4G H 2
      HW4I A 2
      Goto Ready
   FireFast2:
      HW4F B 2
      HW4G BC 2 
      TNT1 A 0 A_PlayWeaponSound("halflife/weapons/CrowbarMiss")
      HW4G D 1 A_FireBullets(1,1,1,3,"CrowbarPuff2",0,80)
      HW4G E 1 A_FireBullets(12,12,5,3,"Puffnone",0,80)
      HW4G FG 1
      TNT1 A 4
      HW4G H 1
      HW4I A 2
      Goto Ready
   FireFast3:
      HW4H BC 2
      HW4H D 1
      TNT1 A 0 A_PlayWeaponSound("halflife/weapons/CrowbarMiss")
      HW4H E 1 A_FireBullets(1,1,1,3,"CrowbarPuff2",0,80)
      HW4H F 1 A_FireBullets(12,12,5,3,"Puffnone",0,80)
      HW4H G 2
      TNT1 A 3
      HW4H HJKM 1
      Goto Ready
   Spawn:
      POSS A 3
      Loop
   }
}

Actor Puffnone
{ +NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 4
		Stop
	}
}

Actor CrowbarPuff2
{
    +NOEXTREMEDEATH
    +NOBLOCKMAP
    +NOGRAVITY
    +PUFFONACTORS
    Speed 0
    Damage 0
    Scale 0.2
    RenderStyle Add
    states
    {
      Spawn:
		TNT1 AAA 0
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/Crowbarhitbody")
		TNT1 A 4
		Stop
	Crash:
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/Crowbarhit")
		Goto "Puffer"
	Puffer:
		TNT1 A 0 A_SpawnItemEx("PuffSmoke",0,0,0,0,0,0,0,32)
		TNT1 A 0 A_Jump(128,11)
		FX57 ABCDEFGHIJ 2 Bright A_FadeOut
		Stop
		FX57 KLMNOPQRST 2 Bright A_FadeOut
		Stop
    }
}

//===================
// Paranoid Shotgun
//===================

Actor ShotgunPickup : CustomInventory 30002
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the shotgun!"
	States
	{
	Spawn:
		POSS A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("PShotgun",1,"JustAmmo")
		TNT1 A 0 A_GiveInventory("ShotgunHeldAmmo",8)
		TNT1 A 0 A_GiveInventory("PShotgun",1)
		Stop
	JustAmmo:
		TNT1 A 0 A_GiveInventory("PShell",8)
		Stop
	}
}

ACTOR Pshotgun : Weapon 

{
  SpawnID 254
  Radius 20
  Height 4
	+NOALERT
	+INVENTORY.IGNORESKILL
	Weapon.SelectionOrder 900
	Weapon.AmmoUse 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "PShell"
	Weapon.AmmoType2 "PShell"
	Inventory.PickupMessage "You got the shotgun!"
	Obituary "%o swallowed %k's lead pellets."
	States
	{
	Ready:
		HW1I A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("ReloadCheck",1,"RealReload")
		Loop
	Deselect:
		TNT1 A 0 ACS_Terminate(870,0)
		TNT1 A 0 A_Lower
		HW1I A 1 A_Lower
		Goto Deselect+1
	Select:
		TNT1 A 0 ACS_Execute(870,0,3,0,0)
		TNT1 A 0 A_Raise
		HW1I A 1 A_Raise
		Goto Select+1

	Fire:
		TNT1 A 0 A_JumpIfInventory("ShotgunHeldAmmo",1,"Shot")
		HW1I A 5 A_PlayWeaponSound("halflife/weapons/noammo")
		Goto "RealReload"

	Reload:
	RealReload:
		TNT1 a 0 A_JumpIfInventory("ShotgunHeldAmmo",8,"CantReload")
		TNT1 a 0 A_JumpIfInventory("PShell",1,"ReloadPossible")
		Goto "CantReload"

	CantReload:
		HW1I A 5 A_TakeInventory("ReloadCheck",1)
		Goto Ready

	ReloadPossible:
		Hw1I A 5
		HW1Q ABCDE 3

		TNT1 A 0 A_JumpIfInventory("PShell",1,"ReloadContinue")
		Goto "ReloadDone"

	ReloadContinue:
		TNT1 A 0 A_JumpIfInventory("ShotgunHeldAmmo",8,"ReloadDone")

		HW1R ABC 2
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/reload1")
		TNT1 A 0 A_TakeInventory("PShell",1)
		TNT1 A 0 A_GiveInventory("ShotgunHeldAmmo",1)
		HW1R DEFG 2
		TNT1 A 0 A_TakeInventory("ReloadCheck",1)
		HW1R A 0 A_WeaponReady
		HW1R A 2
		Goto "ReloadPossible"+6

	ReloadDone:
		TNT1 A 0 A_TakeInventory("ReloadCheck",1)
		HW1S ABCD 3
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/shotgunpump")
		hw1I IJKLMNOPQRS 2
		Goto Ready
	Shot:
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/shotgunsingle")
		TNT1 A 0 ACS_Execute(869,0,350,0,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 AA 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,4,6)
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		hw1I B 2 A_FireBullets(3,3,8,5,"NewPuff1",1)
		TNT1 A 0 A_TakeInventory("ShotgunHeldAmmo",1)
		HW1I B 1 ACS_Execute(869,0,-50,0,0)
		HW1I CCCDDD 1 ACS_Execute(869,0,-50,0,0)
		hw1I EFGH 3
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/shotgunpump")
		hw1I IJKLMNOPQRS 2
		Goto Ready

	AltFire:
		TNT1 a 0 A_JumpIfInventory("ShotgunHeldAmmo",1,"AtLeastOneShell")
		HW1I A 5 A_PlayWeaponSound("halflife/weapons/noammo")
		Goto "RealReload"
	AtLeastOneShell:				//descriptive state names :P
		TNT1 A 0 A_JumpIfInventory("ShotgunHeldAmmo",2,"DoubleShot")	//if less than two, will shot only one
		Goto "ShotSingle"
	ShotSingle:
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/shotgunsingle")
		TNT1 A 0 ACS_Execute(869,0,350,0,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 AA 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,4,5)
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		hw1I B 2 A_FireBullets(6,6,7,5,"NewPuff1",0)
		TNT1 A 0 A_TakeInventory("ShotgunHeldAmmo",1)
		HW1I B 1 ACS_Execute(869,0,-50,0,0)
		HW1I CCCDDD 1 ACS_Execute(869,0,-50,0,0)
		hw1I EFGH 3
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/shotgunpump")
		hw1I IJKLMNOPQRS 2
		Goto Ready

	DoubleShot:
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/shotgundouble")
		TNT1 A 0 ACS_Execute(869,0,550,0,0)
		TNT1 AA 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,4,5)
		TNT1 A 0 A_AlertMonsters
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		hw1B A 2 A_FireBullets(10,10,20,5,"NewPuff1",1)
		TNT1 A 0 A_TakeInventory("ShotgunHeldAmmo",2)
		HW1B A 1 ACS_Execute(869,0,-50,0,0)
		HW1B BBCCDDEEFF 1 ACS_Execute(869,0,-50,0,0)
		HW1B HIJKLMN 2
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/shotgunpump")
		hw1I IJKLMNOPQRS 2
		Goto Ready
	Spawn:
		POSS A 3
		Loop
	}
}
//=========================
// Self explanatory
//=========================
Actor LighCheapHack
{
	+NOINTERACTION
	RenderStyle None
	States
	{
	Spawn:
		PLAY AB 3
		Stop
	}
}

//=========================
// Paranoid Shotgun Shells
//=========================

ACTOR PShell : Ammo 30003
{
  SpawnID 253
  Radius 4
  Height 4
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "Picked up 4 shotgun shells."
  Inventory.Amount 4
  Inventory.MaxAmount 125
  Ammo.BackpackAmount 4
  Ammo.BackpackMaxAmount 125
  Inventory.Icon "SHELA0"
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

//===========================
// Paranoid Shotgun Shellbox
//===========================

ACTOR PShellBox : PShell 30004
{
  SpawnID 252
  Radius 6
  Height 4
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "Picked up a box of shotgun shells."
  Inventory.Amount 12
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

//=============================
// Paranoid Shotgun (Vertical)
//=============================

ACTOR Pshotgun1 : ShotgunPickup 30005

{
  Radius 4
  Height 32
  SpawnID 251
}

//===================
// 9mm Assault Rifle
//===================

Actor M16Pickup : CustomInventory 30006
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the 9mm Assault Rifle"
	States
	{
	Spawn:
		POSS A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("ARifle",1,"JustAmmo")
		TNT1 A 0 A_GiveInventory("M16HeldAmmo",25)
		TNT1 A 0 A_GiveInventory("ARifle",1)
		Stop
	JustAmmo:
		TNT1 A 0 A_GiveInventory("ARClip",25)
		Stop
	}
}

actor ARifle : Weapon 
{
   SpawnID 250
   Radius 20
   Height 4
	+NOALERT
	+INVENTORY.IGNORESKILL
	Weapon.SelectionOrder 1000
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 1
	Weapon.AmmoType "ARClip"
	Weapon.AmmoType2 "M16Gren"
	Obituary "%o felt %k's m16."
	States
	{
	Ready:
		HW3I A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("ReloadCheck",1,"RealReload")
		Loop
	Deselect:
		TNT1 A 0 ACS_Terminate(870,0)
		TNT1 A 0 A_Lower
		HW3I A 1 A_Lower
		Goto Deselect+1
	Select:
		TNT1 A 0 ACS_Execute(870,0,4,0,0)
		TNT1 A 0 A_Raise
		HW3I A 1 A_Raise
		Goto Select+1
	Fire:
		TNT1 A 0 A_JumpIfInventory("M16HeldAmmo",1,"Successful")
		HW3I A 5 A_PlayWeaponSound("halflife/weapons/noammo")
		Goto "RealReload"

	Successful:
//		HW3F A 1
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		TNT1 a 0 A_PlayWeaponSound("halflife/weapons/machinegunshot")
		TNT1 a 0 A_FireBullets(3,3,-1,5,"NewPuff1",1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,6,4)
		HW3F B 1 ACS_Execute(869,0,100,0,0)
		TNT1 A 0 A_TakeInventory("M16HeldAmmo",1)
		HW3F CE 1 ACS_Execute(869,0,-50,0,0)
		HW3F F 1 
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("M16Gren",1,"AltSuccesful")
		HW3I A 12 A_PlayWeaponSound("halflife/weapons/noammo")
		Goto Ready
	AltSuccesful:
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/grenadelaunch")
		TNT1 AA 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,6,6)
		TNT1 A 0 A_TakeInventory("M16Gren",1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 ACS_Execute(869,0,400,0,0)
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		HW3G A 2 A_FireCustomMissile("M16Grenade",0,1,5,0)
		HW3G BBCCDDEE 1 ACS_Execute(869,0,-50,0,0)
		HW3G FGHIJKLMNOPQ 2
		Goto Ready
	Spawn:
		POSS A 3
		Loop

	Reload:
	RealReload:
			TNT1 A 0 A_JumpIfInventory("M16HeldAmmo",50,"NoNeed")
			TNT1 A 0 A_JumpIfInventory("ARClip",1,"ReloadPossible")
			HW3I A 5
			TNT1 A 0 A_TakeInventory("ReloadCheck",1)
			Goto Ready
	ReloadPossible:
		HW3I A 5 
		HW3R ABCDEF 3
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/m16cliprelease")

			TNT1 A 0 A_JumpIfInventory("M16HeldAmmo",50,"ReloadDone")
			TNT1 A 0 A_JumpIfInventory("ARClip",1,"StillPossible")
			Goto "ReloadDone"
	StillPossible:
			TNT1 A 0 A_TakeInventory("ARClip",1)
			TNT1 A 0 A_GiveInventory("M16HeldAmmo",1)
			Goto "ReloadPossible"+8
	ReloadDone:
		TNT1 A 0 A_TakeInventory("ReloadCheck",1)

		HW3R GHIJKLMNOPQ 2
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/m16clipinsert")
		HW3S ABCDEFGI 2
		Goto Ready
	NoNeed:
		HW3I A 5 A_TakeInventory("ReloadCheck",1)
		Goto Ready
   }
}

Actor M16Grenade
{
	Projectile
	Speed 20
	Damage 20
	Radius 10
	Height 20
	-NoGravity
	Gravity 0.25
	Scale 0.65
	DeathSound "halflife/weapons/randomexplo"
	States
	{
	Spawn:
		TNT1 A 0
		GRNL A 3 ThrustThingZ(0,8,0,1)
		TNT1 AA 0 A_SpawnItemEx("GrenadeTrail",random(1,-3),random(3,-3),2+random(3,-3),0,0,0,0,0)
		GRNL A 1 
		Goto Spawn+2
	Death:
		TNT1 A 0 A_Explode(170,140)
		TNT1 A 0 A_SpawnItemEx("GeneralExplosion",0,0,0,0,0,0,32,0)
		TNT1 A 10
		Stop
	}
}

Actor GrenadeTrail
{
	Projectile
	Speed 0
	Damage 0
	+NOINTERACTION
	Scale 0.35
	RenderStyle Add
	Alpha 0.67
	States
	{
	Spawn:
		TNT1 A 0
		WPXS ABCDEFGIHJKLMNOPQRSTUVWXYZ 1 A_FadeOut(0.03)
		Wait
	}
}

//====================
// Assault Rifle Clip
//====================

ACTOR ARClip : Ammo 30007
{
  Game Doom
  SpawnID 249
  Radius 8
  Height 4
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "Picked up a clip."
  Inventory.Amount 25
  Inventory.MaxAmount 250
  Ammo.BackpackAmount 25
  Ammo.BackpackMaxAmount 400
  Inventory.Icon "CLIPA0"
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

//========================
// Assault Rifle Ammo Box
//========================
actor ARAmmo : ARClip 30008
{
  Game Doom
  SpawnID 248
  Radius 12
  Height 8
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "Picked up a box of 9mm bullets."
  Inventory.Amount 50
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

//=========
// M16Gren
//=========

ACTOR M16Gren : Ammo 30017
{
  SpawnID 239
  Inventory.PickupMessage "Picked up two M16 Grenades."
  Inventory.Amount 2
  Inventory.MaxAmount 10
  Ammo.BackpackAmount 2
  Ammo.BackpackMaxAmount 20
  Inventory.Icon "ROCKA0"  //Placeholder icon for now
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

//=============================
// 9mm Assault Rifle (Vertical)
//=============================

ACTOR ARifle1 : M16Pickup 30009

{
  Radius 4
  Height 32
  SpawnID 247
}

//=========
// Beretta
//=========

Actor PistolPickup : CustomInventory 30010
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You picked up a Beretta."
	States
	{
	Spawn:
		POSS A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("Beretta",1,"JustAmmo")
		TNT1 A 0 A_GiveInventory("PistolHeldAmmo",17)
		TNT1 A 0 A_GiveInventory("Beretta",1)
		Stop
	JustAmmo:
		TNT1 A 0 A_GiveInventory("BClip",17)
		Stop
	}
}

ACTOR Beretta : Weapon 
{
	SpawnID 246
	Radius 4
	Height 4
	+NOALERT
	+INVENTORY.IGNORESKILL
	Weapon.SelectionOrder 2000
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "BClip"
	Weapon.AmmoType2 "BClip"
	Obituary "%o was out-gunned by %k's Beretta."
	States
	{
	Ready:
		HW2I A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("ReloadCheck",1,"RealReload")
		Loop
	Deselect:
		TNT1 A 0 ACS_Terminate(870,0)
		TNT1 A 0 A_Lower
		HW2I A 1 A_Lower
		Goto Deselect+1
	Select:
		TNT1 A 0 ACS_Execute(870,0,1,0,0)
		TNT1 A 0 A_Raise
		HW2I A 1 A_Raise
		Goto Select+1
	Fire:
		TNT1 A 0 A_JumpIfInventory("PistolHeldAmmo",1,"Successful")
		HW2I A 5 A_PlayWeaponSound("halflife/weapons/noammo")
		Goto "RealReload"
	Successful:
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/pistol")
		HW2F AB 1
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("PistolHeldAmmo",1)
		TNT1 A 0 ACS_Execute(869,0,100,0,0)
		TNT1 A 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,7,5)
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		HW2F C 2 A_FireBullets(1,1,-1,5,"NewPuff1",1)
		HW2F DD 1 ACS_Execute(869,0,-50,0,0)
		HW2F D 1
		HW2F EF 2
		TNT1 A 0 A_ReFire
		HW2F GHIJKL 2
		Goto Ready

	AltFire:
		TNT1 A 0 A_JumpIfInventory("PistolHeldAmmo",1,"AltSuccessful")
		HW2I A 5 A_PlayWeaponSound("halflife/weapons/noammo")
		Goto "RealReload"
	AltSuccessful:
		HW2F B 1
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/pistol")
		TNT1 A 0 A_TakeInventory("PistolHeldAmmo",1)
		TNT1 A 0 ACS_Execute(869,0,100,0,0)
		TNT1 A 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,7,5)
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		HW2F C 2 A_FireBullets(3,3,-1,5,"NewPuff1",1)
		HW2F DD 1 ACS_Execute(869,0,-50,0,0)
		HW2F D 1
		HW2F E 1
		TNT1 A 0 A_ReFire
		HW2F EFF 1
		HW2F GHIJKL 2
		Goto Ready

	Spawn:
		POSS A 3
		Loop
	Reload:
	RealReload:
			TNT1 A 0 A_JumpIfInventory("PistolHeldAmmo",17,"NoNeed")
			TNT1 A 0 A_JumpIfInventory("BClip",1,"ReloadPossible")
			HW2I A 10 A_PlayWeaponSound("halflife/weapons/noammo")
			TNT1 A 0 A_TakeInventory("ReloadCheck",1)
			Goto Ready
	ReloadPossible:
		HW2I A 5 
		HW2R ABCDEF 3
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/reload1")

			TNT1 A 0 A_JumpIfInventory("PistolHeldAmmo",17,"ReloadDone")
			TNT1 A 0 A_JumpIfInventory("BClip",1,"StillPossible")
			Goto "ReloadDone"
	StillPossible:
			TNT1 A 0 A_TakeInventory("BClip",1)
			TNT1 A 0 A_GiveInventory("PistolHeldAmmo",1)
			Goto "ReloadPossible"+8
	ReloadDone:
		TNT1 A 0 A_TakeInventory("ReloadCheck",1)
		HW2R GHIJKLM 2
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/reload2")
		HW2R NOPQRSTUV 2
		Goto Ready
	NoNeed:
		HW2I A 5 A_TakeInventory("ReloadCheck",1)
		Goto Ready
	}
}

//====================
// Beretta Clip
//====================

ACTOR BClip : Ammo 30011
{
  Game Doom
  SpawnID 245
  Radius 8
  Height 4
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "Picked up a clip."
  Inventory.Amount 17
  Inventory.MaxAmount 250
  Ammo.BackpackAmount 17
  Ammo.BackpackMaxAmount 400
  Inventory.Icon "CLIPA0"
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

//=====================
// Python (357 Magnum)
//=====================

Actor R357Pickup : CustomInventory 30012
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "A 357 Magnum! You Feelin' Lucky, Punk?"
	States
	{
	Spawn:
		POSS A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("Python",1,"JustAmmo")
		TNT1 A 0 A_GiveInventory("MagnumHeldAmmo",6)
		TNT1 A 0 A_GiveInventory("Python",1)
		Stop
	JustAmmo:
		TNT1 A 0 A_GiveInventory("PythonAmmo",6)
		Stop
	}
}

ACTOR Python : DoomWeapon 
{
	SpawnID 244
	Radius 4
	Height 4
	+NOALERT
	+INVENTORY.IGNORESKILL
	Weapon.SelectionOrder 1500
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "PythonAmmo"
	Obituary "%o's armor was pierced by %k's Python."
	States
	{
	Ready:
//		HWAT ABCDE 2
		HWAI A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("ReloadCheck",1,"RealReload")
		Loop
	Deselect:
		TNT1 A 0 ACS_Terminate(870,0)
		HWAI A 0 A_Lower
		HWAI A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 ACS_Execute(870,0,2,0,0)
		HWAI A 0 A_Raise
		HWAI A 1 A_Raise
		Goto Select+1
	Fire:
		TNT1 A 0 A_JumpIfInventory("MagnumHeldAmmo",1,"FireSuccesful")
		HWAI A 5 A_PlayWeaponSound("halflife/weapons/noammo")
		Goto "RealReload"
	FireSuccesful:
		HWAF A 2
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/magnumshot")
		TNT1 A 0 ACS_Execute(869,0,500,0,0)
		TNT1 A 0 A_AlertMonsters
		TNT1 AA 0 A_FireCustomMissile("GunSmallSmokeSpawner",0,0,6,5)
		TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
		HWAF B 0 A_FireBullets (0, 0, 1, 85, "BulletPuff", 1)	
		TNT1 A 0 A_TakeInventory("MagnumHeldAmmo",1)
		HWAF BBCCDDEEEE 1 ACS_Execute(869,0,-50,0,0)
		HWAF FGHIJKLMNOP 3
		Goto Ready
	Flash:
		TNT1 A 4 Bright A_Light1
		TNT1 A 3 Bright A_Light2
		Goto LightDone
	Spawn:
		POSS A 3
		Loop

	Reload:
	RealReload:
			TNT1 A 0 A_JumpIfInventory("MagnumHeldAmmo",6,"NoNeed")
			TNT1 A 0 A_JumpIfInventory("PythonAmmo",1,"ReloadPossible")
			HWAI A 10
			TNT1 A 0 A_TakeInventory("ReloadCheck",1)
			Goto Ready
	ReloadPossible:
		HWAR ABCDEFGHIJKLMNOPQRSTUVWXYZ 2
		HWAS ABCDEFGHIJKL 2

			TNT1 A 0 A_JumpIfInventory("MagnumHeldAmmo",6,"ReloadDone")
			TNT1 A 0 A_JumpIfInventory("PythonAmmo",1,"StillPossible")
			Goto "ReloadDone"
	StillPossible:
			TNT1 A 0 A_TakeInventory("PythonAmmo",1)
			TNT1 A 0 A_GiveInventory("MagnumHeldAmmo",1)
			Goto "ReloadPossible"+38
	ReloadDone:
		TNT1 A 0 A_TakeInventory("ReloadCheck",1)
		TNT1 A 0 A_PlayWeaponSound("halflife/weapons/m357reload")
		HWAS MNOPQRSTUV 2
		Goto Ready
	NoNeed:
		HWAI A 5 A_TakeInventory("ReloadCheck",1)
		Goto Ready
	}
}

//=============
// Python Ammo 
//=============

ACTOR PythonAmmo : Ammo 30013
{
  Game Doom
  SpawnID 243
  Radius 6
  Height 4
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "Picked up a box of 357 calibers."
  Inventory.Amount 12
  Inventory.MaxAmount 36
  Ammo.BackpackAmount 6
  Ammo.BackpackMaxAmount 50
  Inventory.Icon "CLIPA0"
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

ACTOR PythonAmmo1 : PythonAmmo 30018
{
  SpawnID 238
  Radius 4
  Height 4
  Inventory.PickupMessage "Picked up six 357 caliber bullets."
  Inventory.Amount 6
}

//=====================
// RPG Launcher
//=====================

ACTOR RPGLauncher : Weapon 30014
{
	SpawnID 242
	Radius 32
	Height 8
	+FLOORCLIP
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "You just got an RPG Launcher! Let's blow things up!"
   Weapon.AmmoType "RPGAmmo"
	//+CHEATNOTWEAPON
   Weapon.AmmoGive 2
   Weapon.AmmoUse 1
   States
   {
   Spawn:
      POSS A 3
      Loop
   Ready:
      HW5I A 1 A_WeaponReady
      Loop
   Deselect:
      HW5I A 0 A_Lower
      HW5I A 1 A_Lower
      Loop
   Select:
      HW5I A 0 A_Raise
      HW5I A 1 A_Raise
      Loop
   Fire:
	TNT1 A 0 ACS_Execute(869,0,800,0,0)
	TNT1 A 0 A_PlayWeaponSound("halflife/weapons/RPGFire")
	TNT1 a 0 A_FireCustomMissile("LighCheapHack",0,0,0)
      HW5F A 3 A_FireCustomMissile("HLRocket",0,1,5,5)
	TNT1 A 0 A_Recoil(7)
	HW5F BBCCCCCC 1 ACS_Execute(869,0,-100,0,0)
      HW5F DEFGHI 2 
	TNT1 A 0 A_CheckReload
	HW5R ABCDEFGHIJKLMNOPQRSTUVWXYZ 2
	HW5S ABCDE 2
      Goto Ready
   }
}

ACTOR HLRocket
{
   Radius 5
   Height 5
   Speed 35
   Scale 0.7
   Damage 70
   PROJECTILE
   +EXTREMEDEATH
   +ROCKETTRAIL
   +FORCERADIUSDMG
   DeathSound "halflife/weapons/randomexplo"
   States
   {
   Spawn:
      MI13 A 1 Bright
	TNT1 A 0 A_CustomMissile("RPGTrail2",random(3,-3),random(3,-3),0)
	TNT1 AA 0 A_CustomMissile("RPGTrail1",random(3,-3),random(3,-3),0)
      Loop
   Death:
      TNT1 A 0 Bright A_Explode(512,256,1)
      TNT1 A 0 Bright A_CustomMissile("HLRocketExplo",0,0,0,0)
	TNT1 A 20 Radius_Quake(8,20,0,35,0)
      Stop
   }
}

Actor RPGTrail1
{
	+NOCLIP
	+NOGRAVITY
	Scale 0.3
	RenderStyle Add
	Alpha 0.6
	Scale 0.4
	States
	{
	Spawn:
		TNT1 A 2
	 	TNT1 A 0 ThrustThingZ(0,random(1,5),0,1)
		SPIR KLMNO 3
		Stop
	}
}

Actor RPGTrail2
{
	+NOCLIP
	+NOGRAVITY
	Scale 0.3
	RenderStyle Add
	Alpha 0.6
	States
	{
	Spawn:
		TNT1 A 2
	 	TNT1 A 0 ThrustThingZ(0,random(1,5),0,1)
		PUF2 ACEGIKMOQSUWY 1
		PUF3 AC 1
		Stop
	}
}

Actor HLRocketExplo
{
	Projectile
	Speed 0
	Damage 0
	+NOCLIP
	SeeSound "halflife/weapons/randomexplo"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(255,"Exp1","Exp2","Exp3","Exp4")
	Exp1:
		TNT1 A 0 A_CustomMissile("HLRocketExplo1",0,0,0)
		TNT1 A 10
		Stop
	Exp2:
		TNT1 A 0 A_CustomMissile("HLRocketExplo2",0,0,0)
		TNT1 A 10
		Stop
	Exp3:
		TNT1 A 0 A_CustomMissile("HLRocketExplo3",0,0,0)
		TNT1 A 10
		Stop
	Exp4:
		TNT1 A 0 A_CustomMissile("HLRocketExplo4",0,0,0)
		TNT1 A 10
		Stop
	}
}

Actor HLRocketExplo1 : GeneralExplosion1
{
	RenderStyle Add
	Scale 1.8
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeBig",0,0,0,random(3,-3),random(3,-3),random(3,-3),random(0,359),0)
		X005 ABCDEFGHIJKLM 1 Bright A_FadeOut(0.01)
		X005 NOPQRSTUVWX 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor HLRocketExplo2 : GeneralExplosion2
{
	Scale 1.8
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeBig",0,0,0,random(3,-3),random(3,-3),random(3,-3),random(0,359),0)
		X003 ABCDEFGHIJKLMNO 1 Bright A_FadeOut(0.01)
		X003 PQRSTUVWXYZ 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor HLRocketExplo3 : GeneralExplosion3
{
	Scale 1.8
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeBig",0,0,0,random(3,-3),random(3,-3),random(3,-3),random(0,359),0)
		X004 ABCDE 1 Bright A_FadeOut(0.01)
		X004 FGHIJKLMNOPQ 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor HLRocketExplo4 : GeneralExplosion4
{
	Scale 2.8
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeBig",0,0,0,random(3,-3),random(3,-3),random(3,-3),random(0,359),0)
		X002 ABCDEFGHIJKLMN 1 Bright A_FadeOut(0.01)
		X002 OPQRSTU 2 Bright A_FadeOut(0.01)
		Stop
	}
}

Actor GeneralExplosionSmokeBig
{
	Scale 0.7
	RenderStyle Add
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetTranslucent(0.6)
		VAPB ABCDEFGHI 3 Bright A_FadeOut(0.06)
		Wait
	}
}

//===================
// RPG Launcher Ammo 
//===================

ACTOR RPGAmmo : Ammo 30015
{
  SpawnID 241
  Radius 4
  Height 20
  Inventory.PickupSound "misc/w_pkup"
  Inventory.PickupMessage "Picked up an RPG round."
  Inventory.Amount 1
  Inventory.MaxAmount 10
  Ammo.BackpackAmount 2
  Ammo.BackpackMaxAmount 20
  Inventory.Icon "ROCKA0"
  States
  {
  Spawn:
    POSS A 3
    Loop
  }
}

//============
// Ammo Pack
//============

ACTOR AmmoPack : CustomInventory 30016
{
	SpawnID 240
	Radius 6
	Height 24
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Picked up an AmmoPack!"
	+FLOORCLIP
	States
	{
	Pickup:
      	TNT1 A 0 A_GiveInventory("BClip",17)
      	TNT1 A 0 A_GiveInventory("ARClip",25)
		TNT1 A 0 A_GiveInventory("PShell",4)
      	TNT1 A 0 A_GiveInventory("PythonAmmo",6)
		TNT1 A 0 A_GiveInventory("RPGAmmo",1)
		TNT1 A 0 A_GiveInventory("Xbowbolts",2)
 		TNT1 A 0 A_GiveInventory("AECell",10)
     	Stop
	Spawn:
		POSS A 3
		Loop
	}
}