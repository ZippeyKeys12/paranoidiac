class HLBat : Actor
{
	Default
	{
		Monster;
		//spawnid 172;
		Health 200;
		Speed 12;
		Height 76;
		Radius 60;
		Mass 400;
		PainChance 160;
		Scale 0.6;
		+FLOAT
		+NOGRAVITY
		Translation "16:47=64:79", "64:79=96:111";
		BloodColor "DE FE 72";
		SeeSound "stukabat/sight";
		PainSound "stukabat/pain";
		DeathSound "stukabat/death";
		ActiveSound "stukabat/active";
		AttackSound "stukabat/attack";
		Obituary "%o was fried by a StukaBat.";
	}
	States
	{
	Spawn:
		HLSX ABCDEFG 2 A_Look;
		Loop;
	See:
		HLSW ABCDEF 4 A_Chase;
		Loop;
	Missile:
		HLSM ABC 4 A_FaceTarget;
		TNT1 AAAAAAA 0 A_SpawnItemEx("SBFireExtra2",0,0,20,random(2,5),random(2,-2),random(1,3),0,0);
		HLSM D 4 A_SpawnProjectile("StukaFireBall",32,0,0);
		HLSM EF 4;
		Goto See;
	Pain:
		HLSX B 8;
		Goto See;
	Death:
		HLSD A -1 A_Scream;
		Stop;
	Crash:
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop");
		HLSD B 2 A_Fall;
		HLSD CDEFGHIJK 2;
		HLSD K -1;
		Stop;
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath");
	  	TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		Stop;
	}
}

class StukaFireBall : Actor
{
	Default
	{
		Projectile;
		Speed 25;
		Scale 0.35;
		Damage 10;
		RenderStyle "Add";
		Alpha 0.9;
		Translation "80:111=16:47";
	}
	States
	{
	Spawn:
		X022 ABCD 4;
		Loop;
	Death:
		TNT1 A 0 A_PlaySound("halflife/weapons/hl2explo1");
		TNT1 AAAA 0 A_SpawnItemEx("SBFireExtra",0,0,0,random(1,-1),random(1,-1),random(0,2),0,0);
		Stop;
	}
}

class SBFireExtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Scale 0.15;
		RenderStyle "Add";
		Alpha 0.9;
		-NOGRAVITY
		Gravity .125;
		Translation "80:111=26:47";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(128,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

class SBFireExtra2 : SBFireExtra
{
	Default
	{
		Scale 0.26;
	}
}
