#library "PARANOID"
#include "zcommon.acs"

//On-screen messages for in-game prompts and events
script "ParanoidHUDMessage"(int msg,int bold)
{
	SetHUDSize(640,480,1);
	SetFont("smallfont");
	if(bold)
		HUDMessageBold(l:msg;HUDMSG_PLAIN|HUDMSG_LOG,3,CR_GOLD,0.1,300.1,6.0);
	else
		HUDMessage(l:msg;HUDMSG_PLAIN|HUDMSG_LOG,3,CR_GOLD,0.1,300.1,6.0);
}

//Hint messages that are run when player uses the inventory item "MessageItem**"
int playingmessage[8];
str hintsnd[40][2]=
{
	{"P1M2VOX1","HINTMAN"},{"P1M2VOX2","HINTMAN"},{"P1M2VOX3","HINTMAN"},{"P1M2VOX4","HINTMAN"},{"P1M3VOX1","HINTMAN"},
	{"P1M3VOX2","HINTMAN"},{"P1M3VOX3","HINTMAN"},{"P1M4VOX1","HINTMAN"},{"P1M4VOX2","HINTMAN"},{"P1M4VOX3","HINTMAN"},
	{"P1M5VOX1","HINTMAN"},{"P1M5VOX2",""},{"P1M5VOX3",""},{"P1M6VOX1","HINTMAN"},{"P1M6VOX2","HINTMAN"},
	{"P1M6VOX3","HINTMAN"},{"P1M6VOX4",""},{"P1M6VOX5",""},{"P1M6VOX6",""},{"P1M6VOX7",""},
	{"P1M7VOX1","HINTMAN"},{"P1M7VOX2","HINTMAN"},{"P1M7VOX3",""},{"P1M8VOX1","HINTMAN"},{"P1M8VOX2","HINTMAN"},
	{"P2M1VOX1","STINSON"},{"P2M2VOX1","STINSON"},{"P2M2VOX2",""},{"P2M3VOX1","STINSON"},{"P2M3VOX2","UNDERNET"},
	{"P2M4VOX1","STINSON"},{"P2M4VOX2","UNDERNET"},{"P2M5VOX1","STINSON"},{"P2M5VOX2","STINSON"},{"P2M6VOX1","STINSON"},
	{"P2M7VOX1","STINSON"},{"P2M7VOX2","UNDERNET"},{"P2M7VOX3","STINSON"},{"P2M8VOX1","STINSON"}
};
int delays[40][2]=
{
	//duration of soundbyte
	//# of characters
	{8,94},{6,54},{7,78},{9,116},{16,178},
	{14,208},{10,101},{10,146},{15,265},{10,170},
	{8,87},{30,332},{3,41},{6,79},{13,215},
	{8,120},{25,321},{2,14},{3,20},{17,181},
	{5,44},{8,97},{17,178},{5,43},{7,90},
	{42,604},{17,260},{21,274},{29,468},{36,581},
	{28,434},{37,561},{11,163},{26,410},{18,270},
	{19,322},{28,430},{23,345},{15,228}
};
script "HintMessage"(int msg,int source)
{
	SetHUDSize(640,480,1);
	SetFont("smallfont");
	if(!msg)
	{
		HUDMessage(s:"";HUDMSG_PLAIN,1,CR_UNTRANSLATED,0.0,0.0,0.0);
		HUDMessage(l:"NEWMESSAGE";HUDMSG_PLAIN|HUDMSG_LOG,2,CR_GOLD,80.1,30.1,6.0);
		terminate;
	}
	str message;
	int i,dur,typetime;
	if(playingmessage[PlayerNumber()]==2)
		terminate;
	if(source)
	{
		for(i=0;i<8;i++)
		{
			SetActivator(0,AAPTR_PLAYER1>>i);
			if(playingmessage[PlayerNumber()]==1)
				StopMessage();
			playingmessage[PlayerNumber()]=2;
		}
		message=hintsnd[msg-1][0];
		dur=delays[msg-1][0];
		typetime=FixedDiv(dur-1,delays[msg-1][1]);
		AmbientSound(message,127);
		HUDMessageBold(l:message;HUDMSG_TYPEON|HUDMSG_LOG,2,CR_GOLD,80.1,30.1,10.0,typetime,0.05);
		for(i=3+dur*35;i;i--)
			delay(1);
		HUDMessageBold(s:"";HUDMSG_PLAIN,2,CR_UNTRANSLATED,0,0,0);
		for(i=0;i<8;i++)
		{
			SetActivator(0,AAPTR_PLAYER1>>i);
			playingmessage[PlayerNumber()]=0;
		}
		terminate;
	}
	if(playingmessage[PlayerNumber()]==1)
		StopMessage();
	else
	{
		playingmessage[PlayerNumber()]=1;
		message=hintsnd[msg-1][0];
		dur=delays[msg-1][0];
		typetime=FixedDiv(dur-1,delays[msg-1][1]);
		LocalPlaySound("radiostatic",6);
		LocalPlaySound(message,7);
		HUDMessage(l:message;HUDMSG_TYPEON|HUDMSG_LOG,2,CR_GOLD,80.1,30.1,10.0,typetime,0.05);
		SetFont(hintsnd[msg-1][1]);
		HUDMessage(s:"A";HUDMSG_PLAIN,1,CR_UNTRANSLATED,0.1,30.1,(10+dur)*1.0);
	}
	for(i=3+dur*35;i;i--)
	{
		if(playingmessage[PlayerNumber()])
			delay(1);
	}
	StopMessage();
}
function void StopMessage(void)
{
	LocalPlaySound("radiostatic",6);
	playingmessage[PlayerNumber()]=0;
	StopSound(0,7);
	HUDMessage(s:"";HUDMSG_PLAIN,1,CR_UNTRANSLATED,0,0,0);
	HUDMessage(s:"";HUDMSG_PLAIN,2,CR_UNTRANSLATED,0,0,0);
}
function void LocalPlaySound(str snd,int chan)
	{PlaySound(0,snd,chan,1.0,0,ATTN_STATIC);}