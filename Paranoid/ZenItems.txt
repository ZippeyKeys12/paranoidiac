class ZenHair : Actor
{
	Default
	{
		Radius 4;
		Height 20;
		Mass 0x7FFFFFFF;
		+FLOORCLIP
		+RANDOMIZE
	}
	States
	{
	Spawn:
		POSS A 35;
		POSS ABCDEFGHI 6;
		Goto Spawn+1;
	}
}

class ZenLight : ZenHair
{
	Default
	{
		Radius 8;
		Height 32;
		Health 10000;
		PainChance 192;
		PainChance "Crowbar",256;
		PainSound "misc/chat";
		+SHOOTABLE
		+NOTAUTOAIMED
		+NOBLOOD
		+NORADIUSDMG
		+NODAMAGE
		+SOLID
	}
	States
	{
	Spawn:
		POSS A 24 Bright;
		POSS AB 6 Bright LIGHT("ZENLIGHT1") ZenLightLook;
		POSS CD 6 Bright LIGHT("ZENLIGHT2") ZenLightLook;
		POSS EFG 6 Bright LIGHT("ZENLIGHT3") ZenLightLook;
		POSS HI 6 Bright LIGHT("ZENLIGHT2") ZenLightLook;
		POSS JK 6 Bright LIGHT("ZENLIGHT1") ZenLightLook;
		POSS A 0 A_JumpIfCloser(256,"Shrink");
		Goto Spawn+1;
	Shrink:
		POSS LM 3 Bright LIGHT("ZENLIGHTFADE1");
		POSS NO 3 Bright LIGHT("ZENLIGHTFADE2");
		POSS P 3;
	Shrunk1:
		POSS Q 8;
		POSS Q 0 A_JumpIfCloser(256,"Shrunk1");
		Goto Grow;
	Pain:
		POSS Q 24;
		Goto Shrunk1;
	Grow:
		POSS Q 70;
		POSS P 3;
		POSS ONML 3 Bright;
		Goto Spawn;
	}
	action void ZenLightLook()
		{A_LookEx(LOF_NOSOUNDCHECK,0,264,264,45,"Shrink");}
}

class ZenTree : Actor
{
	Default
	{
		Height 144;
		Health 10000;
		Mass 0x7FFFFFFF;
		meleerange 128;
		PainChance 128;
		PainChance "Crowbar",252;
		PainSound "tree/pain";
		+NOTAUTOAIMED
		+SHOOTABLE
		+NOBLOOD
		+NORADIUSDMG
		+NODAMAGE
		+SOLID
		+FLOORCLIP
	}
	States
	{
	Spawn:
		POSS A 8 A_LookEx(LOF_NOSOUNDCHECK,0,136,136,72,"See");
		Loop;
	See:
		POSS A 8 A_Chase;
		Goto Spawn;
	Melee:
		POSS BC 3;
		POSS DEF 1 A_FaceTarget;
		POSS G 1 A_CustomMeleeAttack(10,"tree/attack","tree/swipe");
		POSS HI 4;
		Goto Spawn;
	Pain:
		POSS BC 1 A_Pain;
		POSS D 6;
		POSS CB 3;
		Goto Spawn;
	}
}

class Fungus : ModelBase
{
	Default
	{
		Radius 32;
		Height 72;
		Mass 0x7FFFFFFF;
		+NOTAUTOAIMED
		+FLOORCLIP
		+SHOOTABLE
		+NOBLOOD
		+NODAMAGE
	}
}

class HeadCrab : Fungus //Dissected HeadCrab Model
{
	Default
	{
		Radius 8;
		Height 16;
		Mass 100;
	}
}