class HLScientistWandererSpawner : RandomSpawner
{
	Default
	{
		+FRIENDLY
		DropItem "HLScientist1Path";
		DropItem "HLScientist2Path";
		DropItem "HLScientist3Path";
		DropItem "HLScientist4Path";
	}
	override void PostSpawn(Actor spawned)
	{
		let me=HLScientist1Path(spawned);
		me.wanderer=1;
	}
}

class HLScientistImmovilSpawner : HLScientistWandererSpawner
{
	override void PostSpawn(Actor spawned)
	{
		let me=HLScientist1Path(spawned);
		me.still=1;
	}
}

//**************************************************************************
//**************************************************************************

class HLScientist1Path : HLMonster
{
	bool wanderer,still;
	int limit,limit2,boredom,stay,panic;
	string spawnsprite,walksprite,runsprite,painsprite,deathsprite;
	Property Limits:limit,limit2;
	Property sprites:spawnsprite,walksprite,runsprite,painsprite,deathsprite;
	override bool Used(Actor user)
	{
		boredom=0;
		return false;
	}
	Default
	{
		Health 25;
		GibHealth -100;
		Speed 1.666667;
		Height 56;
		PainChance 255;
		Scale .36;
		+FRIENDLY
		-CANUSEWALLS
		-COUNTKILL
		SeeSound "halflife/monsters/scientist/sight";
		PainSound "halflife/monsters/scientist/pain";
		DeathSound "halflife/monsters/scientist/death";
		HLScientist1Path.Limits 35,30;
		HLScientist1Path.Sprites "HLRIA0","HLRWA0","HLRRA0","HLRPA0","HLRDA0";
		Species "HLScientist";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		{
			if(wanderer)
				return ResolveState("See");
			if(!still)
				boredom=1;
			return State(null);
		}
	Pondering:
		TNT1 A 0
		{
			self.sprite=GetSpriteIndex(spawnsprite);
			self.frame=random(0,2);
		}
		"####" # 5
		{
			if(wanderer)
			{
				A_SetTics(15);
				Chatter(4);
				if(random(0,255<6))
					return ResolveState("See");
			}
			if(still)
			{
				A_SetTics(10);
				Chatter(2);
			}
			else
			{
				if(stay>=80)
				{
					stay=0;
					if(random(0,1))
					{
						boredom=0;
						return ResolveState("See");
					}
					return State(null);
				}
				stay++;
				if(!boredom)
				{
					A_PlaySound("scientist/randomphrase");
					return ResolveState("See");
				}
				Chatter(2);
			}
			return state(null);
		}
		Wait;
	See:
		TNT1 A 0
		{
			if(bFRIGHTENED)//scream the 1st time they enter here
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("SeeDecidePanic");
			}
			if(still)
				return ResolveState("Pondering");
			self.sprite=GetSpriteIndex(walksprite);
			A_SetSpeed(5.0/3.0);
			return ResolveState("Wandering");
		}
	Wandering:
		"####" AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1
		{
			if(boredom)
				return ResolveState("Spawn");
			A_Chase();
			return state(null);
		}
		"####" A 0
		{
			if(wanderer&&random(0,255)<32)
				return ResolveState("Pondering");
			return state(null);
		}
		Loop;
	Panic:
		TNT1 A 0
		{
			A_ClearTarget();
			bFRIGHTENED=1;
			if(random(0,1))
				return ResolveState("WanderRunStartPanic");
			return state(null);
		}
	WanderPanic:
		TNT1 A 0
		{
			self.sprite=GetSpriteIndex(walksprite);
			A_SetSpeed(5.0/3.0);
		}
		"####" AAAAAA 1 A_Chase;
		"####" A 0 A_Look;
		"####" BBBBBB 1 A_Chase;
		"####" A 0 A_Look;
		"####" CCCCCC 1 A_Chase;
		"####" A 0 A_Look;
		"####" DDDDDD 1 A_Chase;
		"####" A 0 A_Look;
		"####" EEEEEE 1 A_Chase;
		"####" A 0 A_Look;
		"####" FFFFFF 1 A_Chase;
		"####" A 0 A_Look;
		"####" A 0	//how often they'll say something while moving
		{
			if(random(0,255)<8)
			{
				A_PlaySound("scientist/panic");
				return ResolveState("WanderPanic");
			}
			if(random(0,255)<4)
			{
				if(random(0,255)<235)
					return ResolveState("IdleRegPanic");	//stopmov, ironically also randomly starts the run seq
				return ResolveState("WanderRunStartPanic");	//kind of rare to see them running just because of nothing.
			}
			return state(null);
		}
		Loop;
	IdleRegPanic:
		TNT1 A 0
		{
			self.sprite=GetSpriteIndex(spawnsprite);
			if(random(0,255)<3)
				A_PlaySound("scientist/panic");
		}
		"####" "###" 5 A_Look;
		"####" # 0 A_Jump(5,"WanderPanic");
		Loop;
	SeeDecidePanic:		//decide if they calm down or not
		"####" # 0 A_JumpIf(panic>=limit,"SeeCalmDownPanic");
	SeeLoopPanic:
		TNT1 A 0
		{
			self.sprite=GetSpriteIndex(runsprite);
			A_SetSpeed(5);
		}
		"####" AAABBBCCCDDDEEEFFF 1 A_Chase;
		"####" A 0
		{
			panic++;
			if(random(0,255)<4)//every once in a while it will say something
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("SeeLoopPanic");
			}
			return state(null);
		}
		"####" # 0 A_Jump(256,"SeeDecidePanic");
	SeeCalmDownPanic:
		TNT1 A 0
		{
			panic-=8;	//If decides to go running again, will last a bit more
			if(random(0,255)<160)
				return ResolveState("WanderRunPanic");
			panic=0;
			A_ClearTarget();
			return ResolveState("WanderPanic");
		}
	WanderRunStartPanic:		//scream the 1st time they enter here
		TNT1 A 0 A_PlaySound("halflife/monsters/scientist/sight");
	WanderRunDecidePanic:	//decide if they calm down or not
		TNT1 A 0
		{
			if(panic>=limit)
			{
				panic-=8;	//If decides to go running again, will last a bit more
				if(random(0,255)<160)
					return State(null);
				panic=0;
				return ResolveState("WanderPanic");
			}
			return State(null);
		}
	WanderRunPanic:		//not calmed at all
		TNT1 A 0
		{
			self.sprite=GetSpriteIndex(runsprite);
			A_SetSpeed(5);
		}
		"####" AAA 1 A_Chase;
		"####" A 0 A_Look;
		"####" BBB 1 A_Chase;
		"####" A 0 A_Look;
		"####" CCC 1 A_Chase;
		"####" A 0 A_Look;
		"####" DDD 1 A_Chase;
		"####" A 0 A_Look;
		"####" EEE 1 A_Chase;
		"####" A 0 A_Look;
		"####" FFF 1 A_Chase;
		"####" A 0 A_Look;
		"####" A 0
		{
			panic++;
			if(random(0,255)<4)//every once in a while it will say something
			{
				A_PlaySound("halflife/monsters/scientist/sight");
				return ResolveState("WanderRunPanic");
			}
			return state(null);
		}
		Goto WanderRunDecidePanic;
	Pain:
		TNT1 A 8
		{
			self.sprite=GetSpriteIndex(painsprite);
			A_Pain();
		}
		Goto See;
	Death:
		TNT1 A 3
		{
			self.sprite=GetSpriteIndex(deathsprite);
			A_Scream();
		}
		"####" B 3 A_NoBlocking;
		"####" CDEFGHIJ 3;
		"####" K 3 A_PlaySound("halflife/weapons/bodydrop");
		"####" L 3;
		"####" M -1;
		Stop;
	XDeath:
		TNT1 A 0 A_XenGibs(1);
		Stop;
	Dud:
		HLRI A 0;HLRW A 0;HLRR A 0;HLRP A 0;HLRD A 0;
		SC1I A 0;SC1W A 0;SC1R A 0;SC1P A 0;SC1D A 0;
		SC2I A 0;SC2W A 0;SC2R A 0;SC2P A 0;SC2D A 0;
		SC3I A 0;SC3W A 0;SC3R A 0;SC3P A 0;SC3D A 0;
		Stop;
	}
	void Chatter(int chance)
	{
		if(random(0,255)<chance)
			A_PlaySound("halflife/monsters/scientist/idle");
	}
}

class HLScientist2Path : HLScientist1Path
{
	Default
	{
		Scale .3526;
		HLScientist1Path.Limits 45,35;
		HLScientist1Path.Sprites "SC1IA0","SC1WA0","SC1RA0","SC1PA0","SC1DA0";
	}
}

class HLScientist3Path : HLScientist1Path
{
	Default
	{
		Scale .355;
		HLScientist1Path.Limits 55,65;
		HLScientist1Path.Sprites "SC2IA0","SC2WA0","SC2RA0","SC2PA0","SC2DA0";
	}
}

class HLScientist4Path : HLScientist1Path
{
	Default
	{
		HLScientist1Path.Limits 65,65;
		HLScientist1Path.Sprites "SC3IA0","SC3WA0","SC3RA0","SC3PA0","SC3DA0";
	}
}

//////////////////////////////////////////////////////////
//Previous code for scientists; only used for dead ones //
//////////////////////////////////////////////////////////

class HLScientistUpdate : Actor
{
	Default
	{
		Scale .36;
	}
	States
	{
	Spawn:
		HLRD M -1;
		Stop;
	}
}

class HLScientist1 : Actor
{
	Default
	{
		Scale .3526;
	}
	States
	{
	Spawn:
		SC1D M -1;
		Stop;
	}
}

class HLScientist2 : HLScientistUpdate
{
	Default
	{
		Scale .355;
	}
	States
	{
	Spawn:
		SC2D I -1;
		Stop;
	}
}

class HLScientist3 : HLScientistUpdate
{
	States
	{
	Spawn:
		SC3D N -1;
		Stop;
	}
}