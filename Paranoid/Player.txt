class RolfBellmer : PlayerPawn
{
	override void CheckCrouch(bool totallyfrozen)
	{
		//Prevent crouching when in disguise
		if(!CountInv("ScientistDisguise"))
			super.CheckCrouch(totallyfrozen);
	}
	Default
	{
		+NOSKIN
		Height 60;
		Scale .28;
		Player.DisplayName "Rolf Bellmer";
		Player.RunHealth 15;
		Player.ViewHeight 56;
		Player.AttackZOffset 24;
		Player.WeaponSlot 1,"Crowbar";
		Player.WeaponSlot 2,"Beretta","Python";
		Player.WeaponSlot 3,"HornetGun","PShotgun";
		Player.WeaponSlot 4,"Xbow","ARifle";
		Player.WeaponSlot 5,"RPGLauncher";
		Player.WeaponSlot 6,"GluonGun";
		Player.StartItem "Crowbar";
		Player.StartItem "HEVSuit";
	}
	States
	{
	Spawn:
		HPLM A 1 A_JumpIf(CountInv("ScientistDisguise"),"Spawn2");
		Loop;
	Spawn2:
		HP2I A 1 A_JumpIf(vel.x!=0||vel.y!=0,"See");
		Goto Spawn;
	See:
		HPLW A 4 A_JumpIf(CountInv("ScientistDisguise"),"See2");
		HPLW BCDEF 4;
		Loop;
	See2:
		HP2W ABCDEF 4 A_JumpIf(vel.x==0&&vel.y==0,"Spawn");
		Goto See;
	Melee:
		HPLM A 0 A_JumpIf(CountInv("ScientistDisguise"),"Melee2");
	Missile:
		HPLM A 3 A_JumpIf(CountInv("ScientistDisguise"),"Missile2");
		HPLM ABC 3;
		Goto Spawn;
	Melee2:
		HP2I B 12;
		Goto Spawn;
	Missile2:
		HP2I C 12;
		Goto Spawn;
	Pain:
		HPLP A 4 A_JumpIf(CountInv("ScientistDisguise"),"Pain2");
		HPLP A 4 A_Pain;
		Goto Spawn;
	Pain2:
		HP2P A 4;
		HP2P A 4 A_PlaySound("HLPlayerScientistPain",CHAN_VOICE);
		Goto Spawn;
	Death:
		HPLD A 1 A_JumpIf(CountInv("ScientistDisguise"),"Death2");
		HPLD A 1 A_PlaySound("HLPlayerHEVFail",CHAN_VOICE);
		HPLD A 2 A_PlayerScream;
		HPLD B 3 A_NoBlocking;
		HPLD CDEFGHIJKLMNOP 3;
		HPLD Q -1;
		Stop;
	Death2:
		HP2D A 1;
		HP2D A 3 A_PlaySound("HLPlayerScientistDeath",CHAN_VOICE);
		HP2D B 3 A_NoBlocking;
		HP2D CDEFGHIJKL 3;
		HP2D M -1;
		Stop;
	}
}
class ScientistDisguise : Inventory
	{Default{+INVENTORY.UNDROPPABLE}}