class RolfBellmer : PlayerPawn
{
	bool Scientist,Otis;
	override void Tick()
	{
		if(CountInv("ScientistDisguise")&&!scientist)
			Scientist=1;
		if(Scientist&&!CountInv("ScientistDisguise"))
			Scientist=0;
		if(InStateSequence(CurState,SpawnState))
		{
			if(Otis)
				SetStateLabel("OtisSpawn");
			else if(Scientist)
				SetStateLabel("ScientistSpawn");
		}
		else if(InStateSequence(CurState,FindState("ScientistSpawn"))&&!Scientist)
			SetState(SpawnState);
		else if(InStateSequence(CurState,FindState("ScientistSee"))&&!Scientist)
			SetState(SeeState);
		super.tick();
	}
	override void CheckCrouch(bool totallyfrozen)
	{
		//Prevent crouching when in disguise
		if(!Scientist)
			super.CheckCrouch(totallyfrozen);
	}
	Default
	{
		+NOSKIN
		Height 60;
		Player.DisplayName "Rolf Bellmer";
		Player.ViewHeight 56;
		Player.AttackZOffset 24;
		Player.WeaponSlot 1,"HLCrowbar";
		Player.WeaponSlot 2,"HLPistol","HLDesertEagle","HLMagnum";
		Player.WeaponSlot 3,"HLHornetGun","HLShotgun";
		Player.WeaponSlot 4,"HLXBow","HLAssaultRifle";
		Player.WeaponSlot 5,"HLRPG";
		Player.WeaponSlot 6,"HLGluonGun";
		Player.StartItem "HLCrowbar";
		Player.StartItem "HEVSuit";
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2;
		HLI2 AB 2;
		Loop;
	See:
		HLR1 ABCDEFGHIJKLMNOPQRSTUVW 1;
		Loop;
	Missile:
		HLA1 YZ[\] 1;
		HLA2 AB 1;
		HLA1 VUTRSQPONMLKJIHGFEDCBA 1;
		Goto Spawn;
	Pain:
		HLP1 A 0 A_HLPainSelect;
		HLP1 A 1 A_Pain;
		HLP1 DGJMPSU 1;
		Goto Spawn;
	Death:
		HLD1 A 0 A_HLDeathSelect;
		HLD1 A 2
		{
			A_PlaySound("HLPlayerHEVFail",CHAN_VOICE);
			A_NoBlocking();
		}
		HLD1 BCDEFG 2;
		HLD1 H 2 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 IJKL 2;
		HLD1 M -1;
		Stop;
	ScientistSpawn:
		HLI2 CDEFGHIJKLMNOPQ 5;
		Loop;
	ScientistSee:
		HLW2 ABCDEFGHIJKLMNOP 2;
		Loop;
	ScientistMissile:
		HLI2 C 12;
		Goto ScientistSpawn;
	ScientistPain:
		HLP2 A 1 A_PlaySound("HLPlayerScientistPain",CHAN_VOICE);
		HLP2 BCDEFGHI 1;
		Goto ScientistSpawn;
	ScientistDeath:
		HLD2 A 1 A_PlaySound("HLPlayerScientistDeath",CHAN_VOICE);
		HLD2 BC 1;
		HLD2 D 1 A_NoBlocking;
		HLD2 EFGHIJKLMNOPQRS 1;
		HLD2 T 1 A_PlaySound("halflife/weapons/bodydrop");
		HLD2 UVW 1;
		HLD2 X -1;
		Stop;
	OtisSpawn:
		BSI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2;
		BSI2 AB 2;
		Loop;
	OtisSee:
		BSR1 ABCDEFGHIJKLMNOPQRSTUVW 1;
		Loop;
	OtisMissile:
		BSA1 YZ[\] 1;
		BSA2 AB 1;
		BSA1 VUTRSQPONMLKJIHGFEDCBA 1;
		Goto OtisSpawn;
	OtisPain:
		BSP1 A 1
		{
			if(random(0,3))
				A_PlaySound("halflife/monsters/otis/pain",CHAN_VOICE);
		}
		BSP1 DGJMPSU 1;
		Goto OtisSpawn;
	OtisDeath:
		BSD1 A 2
		{
			A_PlaySound("halflife/monsters/otis/death",CHAN_VOICE);
			A_NoBlocking();
		}
		BSD1 BCDEFG 2;
		BSD1 H 2 A_PlaySound("halflife/weapons/bodydrop");
		BSD1 IJKL 2;
		BSD1 M -1;
		Stop;
	}
	override void PlayIdle()
	{
		if(otis&&InStateSequence(CurState,FindState("OtisSee")))
			SetStateLabel("OtisSpawn");
		else if(Scientist&&InStateSequence(CurState,FindState("ScientistSee")))
			SetStateLabel("ScientistSpawn");
		else if(InStateSequence(CurState,SeeState))
			SetState(SpawnState);
	}
	override void PlayRunning()
	{
		if(otis&&InStateSequence(CurState,FindState("OtisSpawn")))
			SetStateLabel("OtisSee");
		else if(Scientist&&InStateSequence(CurState,FindState("ScientistSpawn")))
			SetStateLabel("ScientistSee");
		else if(InStateSequence(CurState,SpawnState))
			SetState(SeeState);
	}
	override void PlayAttacking()
	{
		if(otis)
			SetStateLabel("OtisMissile");
		else if(Scientist)
			SetStateLabel("ScientistMissile");
		else
			SetState(MissileState);
	}

	void A_HLPainSelect()
	{
		if(otis)
			SetStateLabel("OtisPain");
		else if(Scientist)
			SetStateLabel("ScientistPain");
	}
	void A_HLDeathSelect()
	{
		if(otis)
			SetStateLabel("OtisDeath");
		else if(Scientist)
			SetStateLabel("ScientistDeath");
	}
}
class ScientistDisguise : Inventory
	{Default{+INVENTORY.UNDROPPABLE}}