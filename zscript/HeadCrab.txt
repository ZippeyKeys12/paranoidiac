//===========
// Head Crab
//===========

class HLHeadcrab : Actor
{
	bool crabjump;
	int drowncounter;
	Default
	{
		Monster;
		Health 10;
		Speed 8;
		Height 25;
		Radius 8;
		PainChance 200;
		Scale .25;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/headcrab/sight";
		PainSound "halflife/monsters/headcrab/pain";
		DeathSound "halflife/monsters/headcrab/death";
		ActiveSound "halflife/monsters/headcrab/idle";
		maxdropoffheight 384;
		+DONTHARMCLASS;
		DamageFactor "Gonarch",0;
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 3 A_Look;
		Loop;
	See:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		TNT1 A 0 A_JumpIf(crabjump,"SeeMelee");
		HLBW ABCDEFG 5 A_Chase(null,"Missile");
		Loop;
	SeeMelee:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLBW A 5 A_Chase("Melee",null);
		HLBW B 5
		{
			crabjump=0;
			A_Chase("Melee",null);
		}
		Goto See+4;
	Melee:
		HLBW A 10 A_CustomMeleeAttack(random(1,8),"halflife/monsters/headcrab/meleehit");
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 4 A_FaceTarget;
		HLBA A 3 A_PlaySound("halflife/monsters/headcrab/jump");
		HLBA B 3;
		HLBA C 1
		{
			crabjump=1;
			A_SkullAttack();
		}
		HLBA C 2 ThrustThingZ(0,random(20,35),0,1);
		HLBA D 3;
		HLBA E 3 A_CheckFloor(1);
		wait;
		WLBA F 30 A_Stop;
		WLBA G 30;
		Goto See;
	Pain:
		HLBP A 8
		{
			crabjump=0;
			A_Pain();
		}
		Goto See;
	Drown:
		TNT1 a 0 A_JumpIf(drowncounter>=3,"DieDrowned");
		TNT1 A 0 A_jump(230,1,2,3);
		HLBP A 0 A_Pain;
		HLBP AAA 4;
		HLBP A 8 {drowncounter++;}
		Loop;
	DieDrowned:
		HLBD A 1 A_Die("DrownDMG");
		Wait;
	Death.DrownDMG:
		HLBD A 2 A_Scream;
		HLBD B 2 A_NoBlocking;
		HLBD CDEFGHIJ 3;
		HLBD K -1 A_JumpIf(waterlevel==3,"DrownUp");
		Stop;
	DrownUp:
		TNT1 A 0 A_JumpIf(waterlevel<3,"DrownStay");
		HLBD K 2 ThrustThingZ(0,3,0,1);
		Loop;
	DrownStay:
		HLBD K 16 A_JumpIf(waterlevel==3,9);
		Loop;
		HLBD K 2 ThrustThingZ(0,2,0,1);
		Loop;
	Death:
		HLBD A 2 A_Scream;
		HLBD B 2 A_NoBlocking;
		HLBD CDEFGHIJ 3;
		HLBD K -1;
		Stop;
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath");
	  	TNT1 AAAA 0 A_SpawnItemEx("XenGibsMini",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
	  	TNT1 AA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		Stop;
	}
}