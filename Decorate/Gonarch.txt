actor HLGonarch
{
	Monster
	spawnid 174
	Health 5000
	Mass 0x7FFFFFFFF
	+DONTHURTSPECIES
	Speed 8
	Height 90
	Radius 55
	PainChance 10
	+BOSS
	+NORADIUSDMG
	+MISSILEEVENMORE
	BloodColor "DE FE 72"
	SeeSound "gonarch/sight"
	PainSOund "gonarch/pain"
	DeathSound "gonarch/death"
	ActiveSound "gonarch/idle"
	MeleeSound "zombie/hit"
	Obituary "%o was melted by the gonarch's acid."
	HitObituary "%o suffered internal bleedings, courtesy of the gonarch."
	MeleeRange 80
	MeleeDamage 25
	MaxTargetRange 1100
	Scale 0.8
	States
	{
	Spawn:
		HLNI A 0
		HLNI A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNI A 3 A_Look
		Loop
	See:
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW A 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW A 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW B 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW B 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW C 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW C 3 A_Chase
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/step","SoundSlot6")
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW D 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW D 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW E 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW E 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW F 3 A_Chase
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNW F 3 A_Chase
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/step","SoundSlot6")
		TNT1 A 0 A_Jump(210,2)
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/sight","SoundSlot5")
		TNT1 AA 0
		Loop
	Melee:
		TNT1 A 0 A_Jump(100,"AcidSimple")
		TNT1 A 0 A_Jump(80,"MeleeSightSound")
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/attack","SoundSlot5")
		Goto "MeleeAttack"
	MeleeSightSound:
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/sight","SoundSlot4")
	MeleeAttack:
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,0,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32)
		HLNM A 5 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,0,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32)
		HLNM B 5 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,0,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32)
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/zombie/attswing","SoundSlot6")
		HLNM C 5 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32)
		HLNM D 4 A_MeleeAttack
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32)
		HLNM E 5
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32)
		HLNM F 5
		Goto See
	Missile:
		TNT1 A 0 A_Jump(145,"AcidSimple")
		Goto "SpawnCrab"
	AcidSimple:
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/attack","SoundSlot5")
		TNT1 A 0 A_Jump(80,"AcidHeavy")
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO AA 4 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO BB 4 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO BB 4 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcidExtra",0,0,80,random(2,-2),random(2,-2),random(1,8),0,0)
		HLNO C 4 A_CustomMissile("GonarchAcid",80,0,0)
		HLNO C 4
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO DD 4
		Goto See

	AcidHeavy:
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO AA 4 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO BB 4 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO BB 4 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Extra",0,0,80,random(2,-2),random(2,-2),random(1,8),0,0)
		HLNO C 4 A_CustomMissile("GonarchAcid2",80,0,0)
		HLNO C 4
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		HLNO DD 4
		Goto See
	SpawnCrab:
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/attack","SoundSlot5")
		HLNN ABC 8 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner5Tics",0,0,0,0,0,0,0,32)
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/spawnchild","SoundSlot4")
		HLNN DD 2 A_SpawnItemEx("HLBabyHeadCrab2",random(10,-10),random(10,-10),0,0,0,0,32,0)
		HLNN D 1 A_SpawnItemEx("HLBabyHeadCrab2",random(10,-10),random(10,-10),0,0,0,0,32,0)
		HLNN EEEE 8 A_SpawnItemEx("ShieldSpawner5Tics",0,0,0,0,0,0,0,32)
		Goto See
	Pain:
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32)
		HLNI A 4 A_Pain
		Goto See
	Death:
		HLND A 3 A_Scream
		HLND B 3 A_Fall
		HLND CD 4
		TNT1 A 0 A_PlaySoundEx("halflife/monsters/gonarch/step","SoundSlot6")
		HLND EF 4
		HLND G 100
		TNT1 A 0 A_PlaySound("halflife/weapons/explode2")
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosion",random(30,-30),random(30,-30),random(10,90),random(1,2),random(1,2),random(1,3),0,0)
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("gargantuagibs",random(30,-30),random(30,-30),random(10,60),random(1,7),random(1,7),random(5,18),random(0,359))
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("gargantuagibs",random(30,-30),random(30,-30),random(10,60),random(1,7),random(1,7),random(5,18),random(0,359))
	  	TNT1 AAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359))
		TNT1 A 10
		Stop
	}
}


//-------------------------------------------------------------
actor GonarchAcid
{
	Projectile
	Radius 12
	Height 8
	Speed 24
	Scale 0.6
	Damage 25
	RenderStyle Add
	Alpha 0.9
//	Translation "80:111=118:127"
	DeathSound ""
	-NOGRAVITY
	Gravity 0.18
	PoisonDamage 50
	+THRUGHOST
	+FORCERADIUSDMG
	DamageType "Gonarch"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 A 4 ThrustThingZ(0,12,0,1)
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 B 4
		TNT1 A 0 A_Changeflag("THRUGHOST",0)
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 C 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 D 4

		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 A 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 B 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 C 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 D 4
		Goto Spawn+14
	Death:
		TNT1 A 0 A_Explode(128,128,0)
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit")
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GonarchAcidExtra",0,0,0,random(2,-2),random(2,-2),random(0,4),0,0)
		Stop
	}
}

Actor GonarchAcidExtra
{
	Projectile
	Speed 0
	Damage 0
	+NOCLIP
	Scale 0.3
	RenderStyle Add
	Alpha 0.9
	-NOGRAVITY
	Gravity 0.2
//	Translation "80:111=118:127"
	States
	{
	Spawn:
		X023 AB 3
		TNT1 A 0 A_Jump(100,"FadeOut")
		Loop
	FadeOut:
		X023 AB 3 A_FadeOut(0.1)
		Loop
	}
}

//-------------------------------------------------------------
actor GonarchAcid2 : GonarchAcid
{
	Projectile
	Radius 12
	Height 8
	Speed 19
	Scale 0.9
	Damage 30
	RenderStyle Translucent
	Alpha 0.8
	Translation "80:111=152:159"
	DeathSound ""
	-NOGRAVITY
	PoisonDamage 50
	Gravity 0.18
	DamageType "Gonarch"
	+FORCERADIUSDMG
	+THRUGHOST
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 A 4 ThrustThingZ(0,12,0,1)
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 B 4
		TNT1 A 0 A_Changeflag("THRUGHOST",0)
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 C 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 D 4

		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 A 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 B 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 C 4
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0)
		X022 D 4
		Goto Spawn+14
	Death:
		TNT1 A 0 A_Explode(256,256,0)
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit")
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Extra",0,0,0,random(2,-2),random(2,-2),random(0,4),0,0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GonarchAcid2MiniExplosive",0,0,5,random(-3,3),random(-3,3),random(2,7),random(0,359),32)
		Stop
	}
}

Actor GonarchAcid2Extra : GonarchAcidExtra
{
	RenderStyle Translucent
	Alpha 0.8
	Scale 0.3
	Translation "80:111=152:159"
}
Actor GonarchAcid2MiniExplosive
{
	Projectile
	Damage 6
	Speed 10
	Radius 8
	Height 12
	Scale 0.55
	DamageType "Gonarch"
	RenderStyle Translucent
	Alpha 0.8
	-NOGRAVITY
	Gravity 0.3
	PoisonDamage 15
	Translation "80:111=152:159"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 //ThrustThing(0,random(1,180),0,1)
		X022 ABCD 3
		Goto Spawn+2
	Death:
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Mini",0,0,0,random(3,-3),random(3,-3),random(1,5),random(0,359),32)
		TNT1 A 1
		Stop
	}
}

Actor GonarchAcid2Mini : GonarchAcidExtra
{
	RenderStyle Translucent
	Alpha 0.8
	DamageType "Gonarch"
	PoisonDamage 3
	-NOCLIP
	Damage 1
	Scale 0.18
	Translation "80:111=152:159"
}

//*********************************************
//	For 3 tics use
//*********************************************
Actor ShieldSpawner3Tics
{
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("LegShield",48,48,0,0,0,0,0,32)	//front right
		TNT1 A 0 A_SpawnItemEx("LegShield",48,-48,0,0,0,0,0,32)	//front left
		TNT1 A 0 A_SpawnItemEx("LegShield",-48,48,0,0,0,0,0,32)	//back right
		TNT1 A 0 A_SpawnItemEx("LegShield",-48,-48,0,0,0,0,0,32)	//back left
		TNT1 A 0 A_SpawnItemEx("TopShield",0,0,70,0,0,0,0,32)		//"head" shield
		TNT1 A 1
		Stop
	}
}


Actor LegShield
{
	+GHOST
	health 999999
	radius 14
	height 90
	+NOCLIP
	+SHOOTABLE
	+INVULNERABLE
	states
	{
	Spawn:
		TNT1 A 4
		stop
	}
}

Actor TopShield : LegShield
{
	Radius 64
	Height 30
}


//*********************************************
//	For 5 tics use
//*********************************************
Actor ShieldSpawner5Tics
{
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",48,48,0,0,0,0,0,32)	//front right
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",48,-48,0,0,0,0,0,32)	//front left
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-48,48,0,0,0,0,0,32)	//back right
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-48,-48,0,0,0,0,0,32)	//back left
		TNT1 A 0 A_SpawnItemEx("TopShield5Tics",0,0,68,0,0,0,0,32)		//"head" shield
		TNT1 A 1
		Stop
	}
}


Actor LegShield5Tics : LegShield
{
	States
	{
	Spawn:
		TNT1 A 6
		stop
	}
}

Actor TopShield5Tics : LegShield5Tics
{
	Radius 70
	Height 30
}

//*********************************************
//	For 8 tics use
//*********************************************
Actor ShieldSpawner8Tics
{
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",48,48,0,0,0,0,0,32)	//front right
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",48,-48,0,0,0,0,0,32)	//front left
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",-48,48,0,0,0,0,0,32)	//back right
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",-48,-48,0,0,0,0,0,32)	//back left
		TNT1 A 0 A_SpawnItemEx("TopShield8Tics",0,0,68,0,0,0,0,32)		//"head" shield
		TNT1 A 1
		Stop
	}
}


Actor LegShield8Tics : LegShield
{
	States
	{
	Spawn:
		TNT1 A 9
		stop
	}
}

Actor TopShield8Tics : LegShield8Tics
{
	Radius 64
	Height 30
}

