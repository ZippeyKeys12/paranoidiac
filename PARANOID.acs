#library "PARANOID"
#include "zcommon.acs"

script "AltHudAmmo" ENTER
{
	if(GetCVar("screenblocks")==11&&GetCVar("hud_althud"))
	{
		str heldtype="";
		if(CheckWeapon("HLPistol"))
			heldtype="HLPistolMag";
		else if(CheckWeapon("HLMagnum"))
			heldtype="HLMagnumMag";
		else if(CheckWeapon("HLShotgun"))
			heldtype="HLShotgunMag";
		else if(CheckWeapon("HLAssaultRifle"))
			heldtype="HLAssaultRifleMag";
		if(StrCmp(heldtype,""))
		{
			setfont("CONFONT");
			HudMessage(d:CheckInventory(heldtype);HUDMSG_PLAIN,3,CR_RED,0.9,1.0,0.35);
		}
	}
	delay(1);
	restart;
}

//Hint messages that are run when player uses the inventory item "MessageItem**"
int playingmessage[8];
str hintsnd[40][2]=
{
	{"MAP2VOX1","HINTMAN"},{"MAP2VOX2","HINTMAN"},
	{"MAP2VOX3","HINTMAN"},{"MAP2VOX4","HINTMAN"},
	{"MAP3VOX1","HINTMAN"},{"MAP3VOX2","HINTMAN"},
	{"MAP3VOX3","HINTMAN"},{"MAP4VOX1","HINTMAN"},
	{"MAP4VOX2","HINTMAN"},{"MAP4VOX3","HINTMAN"},
	{"MAP8VOX1","HINTMAN"},{"MAP5VOX1","HINTMAN"},
	{"MAP5VOX2","HINTMAN"},{"MAP5VOX3","HINTMAN"},
	{"MAP6VOX1","HINTMAN"},{"MAP6VOX2","HINTMAN"},
	{"MAP7VOX1","HINTMAN"},{"MAP7VOX2","HINTMAN"},
	{"MAP2VOX2","HINTMAN"},
	{"MAP10VX1","STINSON"},{"MAP11VX1","STINSON"},
	{"MAP12VX1","STINSON"},{"MAP12VX2","UNDERNET"},
	{"MAP13VX1","STINSON"},{"MAP13VX2","UNDERNET"},
	{"MAP14VX1","STINSON"},{"MAP14VX2","STINSON"},
	{"MAP15VX1","STINSON"},{"MAP16VX1","STINSON"},
	{"MAP16VX2","UNDERNET"},{"MAP16VX3","STINSON"},
	{"MAP17VX1","STINSON"},
	{"MAP7VOX3",""},{"MAP11VX2",""},
	{"MAP8VOX1",""},{"MAP8VOX2",""},
	{"MAP5VOX4",""},{"MAP5VOX5",""},
	{"MAP5VOX6",""},{"MAP5VOX7",""}
};
int delays[40][2]=
{
	//duration of soundbyte
	//# of characters
	{8,94},{6,54},
	{7,78},{9,116},
	{16,178},{14,208},
	{10,101},{10,146},
	{15,265},{10,170},
	{8,87},{6,79},
	{13,215},{8,120},
	{5,43},{7,90},
	{5,44},{8,97},
	{6,54},
	{42,604},{17,260},
	{29,468},{36,581},
	{28,434},{37,561},
	{11,163},{26,410},
	{18,270},{19,322},
	{28,430},{23,345},
	{15,228},
	{17,178},{21,274},
	{32,334},{3,41},
	{25,321},{2,14},
	{3,20},{17,181}
};
script "HintMessage"(int msg,int source)
{
	str message;
	int i,dur,typetime;
	if(playingmessage[PlayerNumber()]==2)
		terminate;
	SetHUDSize(640,480,1);
	SetFont("smallfont");
	if(source)
	{
		for(i=0;i<8;i++)
		{
			SetActivator(0,AAPTR_PLAYER1>>i);
			if(playingmessage[PlayerNumber()]==1)
				StopMessage();
			playingmessage[PlayerNumber()]=2;
		}
		message=hintsnd[msg-1][0];
		dur=delays[msg-1][0];
		typetime=FixedDiv(dur-1,delays[msg-1][1]);
		AmbientSound(message,127);
		SetFont("smallfont");
		HUDMessageBold(l:message;HUDMSG_TYPEON|HUDMSG_LOG,2,CR_GOLD,80.1,30.1,10.0,typetime,0.05);
		for(i=3+dur*35;i;i--)
			delay(1);
		HUDMessageBold(s:"";HUDMSG_PLAIN,2,CR_UNTRANSLATED,0,0,0);
		for(i=0;i<8;i++)
		{
			SetActivator(0,AAPTR_PLAYER1>>i);
			playingmessage[PlayerNumber()]=0;
		}
		terminate;
	}
	if(playingmessage[PlayerNumber()]==1)
		StopMessage();
	else
	{
		playingmessage[PlayerNumber()]=1;
		message=hintsnd[msg-1][0];
		dur=delays[msg-1][0];
		typetime=FixedDiv(dur-1,delays[msg-1][1]);
		LocalPlaySound("radiostatic",6);
		LocalPlaySound(message,7);
		HUDMessage(l:message;HUDMSG_TYPEON|HUDMSG_LOG,2,CR_GOLD,80.1,30.1,10.0,typetime,0.05);
		SetFont(hintsnd[msg-1][1]);
		HUDMessage(s:"A";HUDMSG_PLAIN,1,CR_UNTRANSLATED,0.1,30.1,(10+dur)*1.0);
	}
	for(i=3+dur*35;i;i--)
	{
		if(playingmessage[PlayerNumber()])
			delay(1);
	}
	StopMessage();
}
function void StopMessage(void)
{
	LocalPlaySound("radiostatic",6);
	playingmessage[PlayerNumber()]=0;
	StopSound(0,7);
	HUDMessage(s:"";HUDMSG_PLAIN,1,CR_UNTRANSLATED,0,0,0);
	HUDMessage(s:"";HUDMSG_PLAIN,2,CR_UNTRANSLATED,0,0,0);
}
function void LocalPlaySound(str snd,int chan)
	{PlaySound(0,snd,chan,1.0,0,ATTN_STATIC);}