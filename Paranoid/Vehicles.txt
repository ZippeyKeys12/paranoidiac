//Osprey
class OspreyBellmer : Actor
{
	int bobstate;
	override void tick()
	{
		if(pos.z!=floorz)
		{
			double newz=pos.z-BobSin(bobstate)/2.;
			bobstate=(bobstate+1)&63;
			newz+=BobSin(bobstate)/2.;
			SetZ(newz);
		}
		super.tick();
	}
	Default
	{
		Radius 3;
		Height 56;
		Mass 0x7FFFFFFF;
		+NOCLIP
		+NOBLOCKMAP
		+NOGRAVITY
		+NOTARGET
		+SHOOTABLE
		+NONSHOOTABLE
		+NOBLOOD
		+NOTRIGGER
		+NOTELEPORT
		+ISMONSTER
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2;
		HLI2 AB 2;
		Loop;
	}
}

class Osprey : OspreyBellmer
{
	int crashloop;
	override void postbeginplay()
	{
		A_PlaySound("play/osprey",looping:1);
		super.postbeginplay();
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJ 2;
		Loop;
	Land:
		HLI1 LMNOPQRSTU 2;
		Loop;
	Death:
		HLD1 A 0
		{
			A_LowGravity();
			A_Quake(9,16,0,1024);
		}
	DeathLoop:
		HLD1 A 1
		{
			A_HLOspreyPitch();
			for(int i=0;i<2;i++)
			{
				A_HLOspreyExplode();
				A_HLOspreyExplode("NewExplosionBig");
			}
			A_PlaySound("halflife/weapons/randomexplo",CHAN_AUTO,1.0,false,ATTN_NONE);
			A_HLOspreyBigSmoke();
		}
		HLD1 BCDE 1 A_HLOspreyPitch;
		HLD1 F 1
		{
			A_HLOspreyPitch();
			for(int i=0;i<2;i++)
			{
				A_HLOspreyExplode();
				A_HLOspreyExplode("NewExplosionBig");
			}
			A_HLOspreyBigSmoke();
		}
		HLD1 GHIJK 1 A_HLOspreyPitch;
		Loop;
	Crash:
		HLD1 L 1
		{
			pitch=0;
			A_PlaySound("halflife/weapons/randomexplo",CHAN_AUTO,1.0,false,ATTN_NONE);
			A_Quake(9,40,0,1024);
			A_PlaySound("Osprey/Crash",CHAN_AUTO,1.0,false,ATTN_NONE);
		}
		HLD1 MNO 1 A_HLOspreyExplode;
		HLD1 P 0 A_HLOspreySpark;
		HLD1 PQR 1 A_HLOspreyExplode;
		HLD1 S 0 A_HLOspreySpark;
		HLD1 STU 1 A_HLOspreyExplode;
		HLD1 V 1 A_HLOspreyBigSmoke;
		HLD1 WX 1 A_HLOspreySpark;
		HLD1 Y 1 A_HLOspreyBigSmoke;
		HLD1 Z[ 1 A_HLOspreySpark;
		HLD1 \ 1 A_HLOspreyBigSmoke;
		HLD1 ] 1 A_HLOspreySpark;
		HLD2 A 1 A_HLOspreySpark;
		HLD2 B 1 A_HLOspreyBigSmoke;
		HLD2 CD 1 A_HLOspreySpark;
		HLD2 E 1 A_HLOspreyBigSmoke;
		HLD2 FG 1 A_HLOspreySpark;
		HLD2 H 1 A_HLOspreyBigSmoke;
		HLD2 IJ 1 A_HLOspreySpark;
		HLD2 K 1 A_HLOspreyBigSmoke;
		HLD2 LM 1 A_HLOspreySpark;
		HLD2 N 1 A_HLOspreyBigSmoke;
		HLD2 OP 1 A_HLOspreySpark;
		HLD2 Q 1 A_HLOspreyBigSmoke;
		HLD2 RS 1 A_HLOspreySpark;
		HLD2 T 1 A_HLOspreyBigSmoke;
		HLD2 UV 1 A_HLOspreySpark;
		HLD2 W 1 A_HLOspreyBigSmoke;
		HLD2 XY 1 A_HLOspreySpark;
		HLD2 Z 1 A_HLOspreyBigSmoke;
	CrashLoop:
		HLD2 [ 9 A_HLOspreyBigSmoke;
		HLD2 [ -1
		{
			crashloop++;
			if(crashloop<12)
				return ResolveState("CrashLoop");
			return state(null);
		}
		Stop;
	}
	void A_HLOspreyPitch()
	{
		if(pitch<=22.5)
			pitch+=0.25;
	}
	void A_HLOspreySpark()
	{
		for(int i=0;i<8;i++)
			A_SpawnItemEx("Spark_O1",frandom(-96,96),frandom(-96,96),frandom(32,96),frandom(-4,4),frandom(-4,4),frandom(-4,4));
	}
	void A_HLOspreyExplode(name boom="NewExplosionMedium")
		{A_SpawnItemEx(boom,frandom(-96,96),frandom(-96,96),frandom(0,96));}
	void A_HLOspreyBigSmoke()
		{A_SpawnItemEx("NewExploBigSmoke",frandom(-96,96),frandom(-96,96),frandom(0,96),0,0,frandom(5,8));}
}

//SUV
class SUV : ModelBase
{
	actor glass;
	override void postbeginplay()
	{
		glass=Spawn("SUVGlass",pos);
		glass.angle=angle;
		glass.ChangeTid(tid);
		glass.master=self;
		super.postbeginplay();
	}
	Default
	{
		Radius 38;
		Height 66;
		Mass 0x7FFFFFFF;
		DeathHeight 66;
		Health 400;
		PainChance 256;
		+ISMONSTER
		+SHOOTABLE
		+NOBLOOD
		+FRIENDLY
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	Pain:
		HLI1 A -1 A_PlaySound("SUV/Hit",CHAN_AUTO);
		Stop;
	Death:
		HLI1 B 1
		{
			A_KillChildren();
			A_Explode(128,192);
			A_PlaySound("SUV/Explode");
		}
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",56,32,40);
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",-64,-40,56);
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",frandom(-16,16),-40,32);
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",frandom(-16,16),40,32);
		HLI1 B 8
		{
			for(int i=0;i<16;i++)
				A_SpawnItemEx("Spark_O1",frandom(-96,96),frandom(-96,96),frandom(32,96),frandom(-4,4),frandom(-4,4),frandom(-4,4));
		}
		HLI1 BBBB 6 A_SpawnItemEx("NewExploBigSmoke",frandom(-64,64),frandom(-48,48),frandom(32,64),0,0,frandom(1,4));
		HLI1 B -1;
		Stop;
	}
}

class SUVGlass : ModelBase
{
	Default
	{
		Renderstyle "Translucent";
		Alpha .5;
		Radius 16;
		Height 56;
		Mass 0x7FFFFFFF;
		-SOLID
		+NOTARGET
		+NOBLOOD
		+SHOOTABLE
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	Death:
		HLI1 BCDEFGHI 1;
		HLI1 JKLM 1 A_FadeOut;
		Stop;
	}
}

//MTM 8 Loader
class Loader : ModelBase
{
	actor box;
	override void postbeginplay()
	{
		bool num;
		[num,box]=A_SpawnItemEx("LoaderBox",112,0,32,flags:SXF_SETMASTER,tid:tid);
		super.postbeginplay();
	}
	override void tick()
	{
		if(box)
			box.A_Warp(AAPTR_MASTER,112,0,32);
		super.tick();
	}
	Default
	{
		Radius 64;
		Height 96;
		Mass 0x7FFFFFFF;
		Scale .6;
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	See:
		HLW1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLW2 ABCDEFGHIJKLMNOPQRST 1;
		Loop;
	}
}
class LoaderNoBox : Loader
{
	actor lclaw,rclaw;
	override void postbeginplay()
	{
		bool num;
		[num,lclaw]=A_SpawnItemEx("LoaderClaw",96,-32,32,flags:SXF_SETMASTER,tid:tid);
		[num,rclaw]=A_SpawnItemEx("LoaderClaw",96,32,32,flags:SXF_SETMASTER,tid:tid);
		actor.postbeginplay();
	}
	States
	{
	Spawn:
		HLI1 B -1;
		Stop;
	}
}
class LoaderBox : Actor
{
	Default
	{
		Radius 32;
		Height 64;
		+SOLID
		+NOGRAVITY
		+NOLIFTDROP
		+ACTLIKEBRIDGE
	}
}
class LoaderClaw : LoaderBox
{
	Default
	{
		Radius 16;
		Height 32;
	}
}

//Tram
class Tram : ModelBase
{
	Default
	{
		Radius 40;
		Height 32;
		Mass 0x7FFFFFFF;
	}
}
class TramBattery : Tram
{
	Default
	{
		-SOLID
		+NOCLIP
	}
}
class TramBellmer : TramBattery
{
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTU 7;
		Loop;
	}
}
class TramBarney : TramBattery
{
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2;
		HLI2 AB 2;
		Loop;
	}
}