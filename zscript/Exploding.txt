//============================
// Explosive barrels & crates
//============================

class ExplosiveItemBang : Actor
{
	Default
	{
		Radius 4;
		Height 4;
		Renderstyle "Translucent";
		Alpha 0.9;
		+dontsplash
		+noclip
	}
	States
	{
	Spawn:
		CBNG ABCDEFGHIJKLMNOPQRS 2 Bright;
		CBNG TU 2;
		stop;
	}
}
class ExplosiveItemBangCrate : ExplosiveItemBang
{
	Default
	{
		Scale 0.6;
	}
}
class ExplosiveItemBangBarrel : ExplosiveItemBang
{
	Default
	{
		Scale 0.4;
	}
}

class Xbarrel1 : Actor
{
	Default
	{
		Health 50;
		Radius 12;
		Height 32;
		Mass 100;
		DeathSound "world/barrelx";
		Obituary "%o stood too close to an exploding barrel.";
		+PUSHABLE
		+SHOOTABLE
		+SOLID
		+NOBLOOD
	}
	States
	{
	Spawn:
		POSS A -1;
		Stop;
	Death:
		POSS B 0 A_SpawnItemEx ("ExplosiveItemBangBarrel", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0);
		POSS B 2 A_AlertMonsters;
		POSS C 2 A_Scream;
		POSS D 1 A_Explode;
		POSS D 1 A_SetTranslucent(0.5, 0);
		POSS E 1 A_SetTranslucent(0.4, 0);
		POSS E 1 A_SetTranslucent(0.3, 0);
		POSS F 1 A_NoBlocking;
		POSS F 1 A_SetTranslucent(0.3, 0);
		POSS G 1 A_SetTranslucent(0.2, 0);
		POSS G 1 A_SetTranslucent(0.1, 0);
		Stop;
	}
}
class Xbarrel2 : Xbarrel1
{
	Default
	{
		Health 20;
	}
}
class Xbarrel3 : Xbarrel1
{
	Default
	{
		Health 30;
	}
}

class Xcrate01 : Xbarrel1
{
	Default
	{
		-PUSHABLE
		Health 30;
		Radius 32;
		Height 64;
		Mass 200;
		Obituary "%o stood too close to an exploding crate.";
	}
	States
	{
	Death:
		POSS B 0 A_SpawnItemEx ("ExplosiveItemBangCrate", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0);
		Goto Super::Death+1;
	}
}
class Xcrate02 : Xcrate01{}

//====================
// General Explosions
//====================

class GeneralExplosion : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		SeeSound "halflife/weapons/randomexplo";
		RenderStyle "Add";
		Scale 0.7;
	}
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExploXtraSparks",0,0,0,random(1,3),random(1,3),random(-1,5),random(1,360));
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosionSmokeMedium",0,0,0,random(3,-3),random(3,-3),random(3,-3),random(0,359),0);
		TNT1 A 0 A_Jump(255,"Explosion1","Explosion2","Explosion3","Explosion4");
	Explosion1:
		X005 ABCDEFGHIJKLM 1 Bright A_FadeOut(0.01);
		X005 NOPQRSTUVWX 2 Bright A_FadeOut(0.01);
		Stop;
	Explosion2:
		X003 ABCDEFGHIJKLMNO 1 Bright A_FadeOut(0.01);
		X003 PQRSTUVWXYZ 2 Bright A_FadeOut(0.01);
		Stop;
	Explosion3:
		X004 ABCDE 1 Bright A_FadeOut(0.01);
		X004 FGHIJKLMNOPQ 2 Bright A_FadeOut(0.01);
		Stop;
	Explosion4:
		X002 A 0 A_SetScale(1.4);
		X002 ABCDEFGHIJKLMN 1 Bright A_FadeOut(0.01);
		X002 OPQRSTU 2 Bright A_FadeOut(0.01);
		Stop;
	}
}

class GeneralExplosionSmokeMedium : Actor
{
	Default
	{
		Scale 0.45;
		RenderStyle "Add";
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetTranslucent(0.5);
		VAPB ABCDEFGHI 3 Bright A_FadeOut(0.08);
		Wait;
	}
}

class RocketSmoke : GeneralExplosionSmokeMedium
{
	Default
	{
		Scale 0.2;
	}
}

class GeneralExploXtraSparks : Actor
{
	Default
	{
		Height 4;
		Radius 4;
		Speed 3;
		Scale 0.5;
		+NOCLIP
		PROJECTILE;
		-NOGRAVITY
		+DONTBLAST
		Gravity .125;
		//Gravity 0.5;
		+DONTSPLASH
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		WRBL K 18 Bright;
		WRBL K 1 Bright A_FadeOut;
		Wait;
	}
}