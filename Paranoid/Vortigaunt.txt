///====================
// Vortigaunt (Slave)
//====================

class HLVortigaunt : HLMonster
{
	Default
	{
		Health 175;
		Mass 200;
		Speed 3.5;
		Height 63;
		PainChance 100;
		PainChance "Crowbar",243;
		Scale .423;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/slave/sight";
		PainSound "halflife/monsters/slave/pain";
		DeathSound "halflife/monsters/slave/death";
		ActiveSound "halflife/monsters/slave/idle";
		maxdropoffheight 64;
		Species "HLVortigaunt";
	}
	States
	{
	Spawn:
		HLFI A 3 A_Look;
		Loop;
	See:
		HLFW A 1 A_Chase;
		HLFW AAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLFW B 1 A_Chase;
		HLFW BBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLFW C 1 A_Chase;
		HLFW CCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLFW D 1 A_Chase;
		HLFW DDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLFW E 1 A_Chase;
		HLFW EEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLFW F 1 A_Chase;
		HLFW FFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Melee:
		HLFI A 4 A_FaceTarget;
		HLFM AB 4 A_FaceTarget;
		HLFM C 3 A_ZombieMelee(3);
		HLFM D 4 A_FaceTarget;
		HLFM E 3 A_ZombieMelee(3);
		HLFM F 4 A_FaceTarget;
		HLFM G 3 A_ZombieMelee(3);
		HLFM H 4;
		Goto See;
	Missile:
		TNT1 A 0
		{
			A_FaceTarget();
			A_PlaySound("halflife/weapons/zap4",5);
		}
		HLFI AAAAAA 4 A_SpawnItemEx("VortiLightning",flags:SXF_SETMASTER|SXF_NOCHECKPOSITION);
		HLFN B 4
		{
			A_SpawnItemEx("VortiLightning",flags:SXF_SETMASTER|SXF_NOCHECKPOSITION);
			A_FaceTarget();
		}
		HLFN B 2 A_SpawnItemEx("VortiLightning",flags:SXF_SETMASTER|SXF_NOCHECKPOSITION);
		HLFN C 2 A_PlaySound("halflife/weapons/zap4",5);
		HLFN C 4
		{
			for(int i=0;i<2;i++)
				A_SpawnItemEx("VortiLightning",flags:SXF_SETMASTER|SXF_NOCHECKPOSITION);
		}
		HLFN D 4 A_FaceTarget;
		HLFN D 3 A_SpawnItemEx("VortiLightning",flags:SXF_SETMASTER|SXF_NOCHECKPOSITION);
	  	HLFN E 3
		{
			A_PlaySound("halflife/weapons/gausshit1",5);
			A_KillChildren(flags:KILS_KILLMISSILES);
			A_SpawnProjectile("VortiPlasma",32,9);
			A_SpawnProjectile("VortiPlasma",32,-9);
		}
		HLFN F 6;
		Goto See;
	Pain:
		HLFP A 8
		{
			A_KillChildren(flags:KILS_KILLMISSILES);
			A_Pain();
		}
		Goto See;
	Death:
		HLFD A 2
		{
			A_KillChildren(flags:KILS_KILLMISSILES);
			A_Scream();
		}
		HLFD B 2 A_NoBlocking;
		HLFD CDEF 2;
		HLFD F 2 A_PlaySound("halflife/weapons/bodydrop");
		HLFD GH 2;
		HLFD IJKLM 4;
		HLFD N -1;
		Stop;
	XDeath:
		TNT1 A 0
		{
			A_XenGibs(14);
			A_KillChildren(flags:KILS_KILLMISSILES);
		}
		Stop;
	}
}

//	The regular non model attack
class VortiLightning : Actor
{
	Default
	{
		Height 1;
		Radius 1;
		+MISSILE
		+NOCLIP
		RenderStyle "Add";
		Alpha .7;
		Scale 1.05;
	}
	States
	{
	Spawn:
		X098 A 2 Bright LIGHT("GreenMediumFlicker2") NODELAY {self.frame=random(0,7)*2;}
		XO98 A 2 Bright LIGHT("GreenMediumFlicker2") {self.frame++;}
		Stop;
	Death:
		TNT1 A 1;
		Stop;
	}
}

//========================================
//  Captain Toenail gets credits for this
//========================================

class VortigauntShot : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 36;
		DamageFunction (random(1,4)*10);
		Projectile;
		RenderStyle "Add";
		Scale .02;
		seesound "Dalek/Fire";
		+SPAWNSOUNDSOURCE
		Translation "80:111=112:127";
	}
	States
	{
	Spawn:
		X099 A 1 bright
		{
			for(int i=-2;i>=-36;i-=2)
				A_SpawnItemEx("VortigauntShotTrail",i);
		}
		loop;
	XDeath:
		TNT1 A 1;
		stop;
	}
}

class VortigauntShotTrail : Actor
{
	Default
	{
		radius 6;
		height 8;
		RenderStyle "Add";
		Scale .02;
		+NOBLOCKMAP
		+NOGRAVITY
		+MISSILE
		+NOTELEPORT
		+DONTSPLASH
		+NOCLIP
		Translation "80:111=112:127";
	}
	states
	{
	Spawn:
		X099 A 1 bright A_FadeOut;
		loop;
	}
}

//====================
// The model attack
//====================

class VortiPlasma_Explode : Actor
{
	Default
	{
		+NOGRAVITY
		RenderStyle "Add";
		Alpha .55;
		scale .3;
	}
	States
	{
	Spawn:
		VPLS A 3 bright LIGHT("GreenMediumFlicker2");
		VPLS B 3 bright LIGHT("GreenMediumFlicker3");
		VPLS C 3 bright LIGHT("GreenSmallFlicker1");
		VPLS D 3 bright LIGHT("GreenSmallFlicker2");
		VPLS E 3 bright LIGHT("GreenSmallFlicker3");
		stop;
	}
}

class VortiPlasma : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 36;
		DamageFunction (random(1,4)*10);
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha .55;
		Scale .3;
		+SPAWNSOUNDSOURCE
		DeathSound "halflife/weapons/gaussshot";
	}
	States
	{
	Spawn:
		VPLS FG 6 bright LIGHT("GreenMediumFlicker3");
		loop;
	Death:
		VPLS A 1 LIGHT("GreenMediumFlicker3") A_SpawnItemEx("VortiPlasma_Explode") ;
		VPLS A 1 LIGHT("GreenMediumFlicker3") A_SpawnItemEx("VortiPlasma_Bolt");
		Stop;
	}
}

class VortiPlasma_Bolt : Actor
{
	Default
	{
		+NOGRAVITY
		RenderStyle "Add";
		Alpha 0.75;
		Scale 0.3;		// turn this up for more EXTREME plasma
	}
	States
	{
	Spawn:
		VPLS A 0 NODELAY {self.frame=random(0,3);}
		VPLS # 1 A_FadeOut;
		Wait;
	}
}