//==============
// Hornet Grunt
//==============

actor HLHornetGrunt 7416
{
    spawnid 163
	Monster
	Health 500
	Mass 800
	Speed 10
	Height 64
	Radius 20
	PainChance 40
	Scale 0.47
	MeleeDamage 12
	BloodColor "DE FE 72"
	DamageFactor "Hornet", 0.0
	SeeSound "halflife/monsters/hornetgrunt/sight"
	PainSound "halflife/monsters/hornetgrunt/pain"
	DeathSound "halflife/monsters/hornetgrunt/death"
	ActiveSound "halflife/monsters/hornetgrunt/idle"
	MeleeSound "halflife/monsters/zombie/hit"
	States
	{
	Spawn:
		HLOI A 3 A_Look
		Loop
	See:
		HLOW ABCDEF 4 A_Chase
		Loop
	Melee:
		HLOM ABCD 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/attswing")
		HLOM E 5 A_MeleeAttack
		HLOM F 5
		Goto See
	Missile:
		HLON AB 5 A_FaceTarget
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget 
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
		TNT1 A 0 A_Jump(80,"MisEnd")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		TNT1 A 0 A_Jump(100,"MisEnd")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		TNT1 A 0 A_Jump(120,"MisEnd")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		TNT1 A 0 A_Jump(140,"MisEnd")
		HLON C 4 A_CustomMissile("HGruntHornet",36,5,0)
		HLON B 4 A_FaceTarget
	MisEnd:
		TNT1 A 0
		Goto See
	Pain:
		HLOP A 8 A_Pain
		Goto See
	Death:
		HLOD A 2 A_Scream
		HLOD B 2 A_Fall
		HLOD CDE 3
		TNT1 A 0 A_PlaySound("halflife/weapons/bodydrop")
		HLOD FGH 3
		HLOD I -1
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath")
		TNT1 A 0 A_Fall
	  	NULL AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359))
		Stop
	}
}

Actor HGruntHornet
{
	Radius 8
	Height 12
	Projectile
	Speed 17
	Damage 4
	Scale 0.085
	DamageType "Hornet"
	+SEEKERMISSILE
	SeeSound "halflife/monsters/hornetgrunt/flysight"
	States
	{
	Spawn:
		TNT1 A 0 
		HLOH B 1 A_SeekerMissile(2,2)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -2, 0, 0, 0, 0, 0, 0, 160)
/*		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -4, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -6, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -8, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -10, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -12, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -14, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -16, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -18, 0, 0, 0, 0, 0, 0, 160)*/
		Loop
	Death:
	Crash:
		TNT1 A 0 A_SpawnItemEx("HGHornetShooter1",0,0,0,0,0,0,0,0)
		TNT1 A 1 A_PlaySound("halflife/monsters/hornetgrunt/flybuzz")
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("halflife/monsters/hornetgrunt/flyhit")
		Stop
	}
}

actor HGHornetTrail
{
  radius 6
  height 8
/*  speed 0
  damage 0
  +NOBLOCKMAP
  +NOGRAVITY
  +MISSILE
  +NOTELEPORT
  +DONTSPLASH
  +NOCLIP*/
  +NOINTERACTION
  Scale 0.015
  RenderStyle Add
  Alpha 1
  Translation "80:111=160:167"
  states
  {
  Spawn:
    X099 A 1 A_FadeOut (0.03)
    loop
  }
}

Actor HGHornetShooter1
{  
    +FRIENDLY
    MONSTER
    Radius 1
    Height 1
    Speed 0
    +NOGRAVITY
    +NOCLIP
    +LOOKALLAROUND
    +MISSILEMORE
    +MISSILEEVENMORE
	-SOLID
	-SHOOTABLE
	-COUNTKILL
    MinMissileChance 0
    RenderStyle None
    ReactionTime 25
    States
    {
    Spawn:
	NULL A 0 A_CountDown
       PLAY A 1 A_Look
//	 loop
    See:
       PLAY A 0 A_Chase
    Missile:
	 TNT1 A 0 A_JumpIfCloser(128,"SuccessfulChase")
	 TNT1 A 0 A_Jump(128,"NinetyNeg")
	 PLAY A 0 A_SpawnItemEx("HGruntHornetFake",0,0,0,10,0,random(2,-2),90,0)
	 Stop
    NinetyNeg:
	 PLAY A 0 A_SpawnItemEx("HGruntHornetFake",0,0,0,10,0,random(5,-5),-90,0)
	 Stop
    SuccessfulChase:
       PLAY A 0 A_FaceTarget
       PLAY A 0 A_CustomMissile("HGruntHornetNoSightSound",0,0,0)
	Stop
    }
}

Actor HGruntHornetNoSightSound : HGruntHornet
{
	SeeSound ""
	States
	{
	Death:
	Crash:
//		TNT1 A 0 A_SpawnItemEx("HGHornetShooter1",0,0,0,0,0,0,0,32)
		TNT1 A 1 A_PlaySound("halflife/monsters/hornetgrunt/flybuzz")
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("halflife/monsters/hornetgrunt/flyhit")
		Stop
	}
}

Actor HGruntHornetFake : HGruntHornet
{
	SeeSound ""
	States
	{
	Spawn:
		TNT1 A 0
		HLOH B 1 A_JumpIfInventory("HornetCounter",20,"Death")
		TNT1 A 0 A_GiveInventory("HornetCounter",1)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -2, 0, 0, 0, 0, 0, 0, 160)
/*		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -4, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -6, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -8, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -10, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -12, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -14, 0, 0, 0, 0, 0, 0, 160)
		X099 A 0 bright A_SpawnItemEx (HGHornetTrail, -16, 0, 0, 0, 0, 0, 0, 160)*/
		Loop
	Death:
		TNT1 A 1 A_PlaySound("halflife/monsters/hornetgrunt/flybuzz")
		Stop
	}
}

Actor HornetCounter : Inventory
{ Inventory.MaxAmount 20 }