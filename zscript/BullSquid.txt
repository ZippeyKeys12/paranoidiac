//===========
// BullSquid
//===========

class HLBullSquid : Actor
{
	Default
	{
		Monster;
		Health 150;
		Mass 350;
		Speed 20;
		Height 32;
		Radius 28;
		PainChance 40;
		Scale 0.365;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/bullsquid/sight";
		PainSound "halflife/monsters/bullsquid/pain";
		DeathSound "halflife/monsters/bullsquid/death";
		ActiveSound "halflife/monsters/bullsquid/idle";
	}
	States
	{
	Spawn:
		HLDI A 3 A_Look;
		Loop;
	See:
		HLDW ABCD 4 A_Chase;
		Loop;
	Melee:
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/growl");
		HLDA ABC 4 A_FaceTarget;
		HLDA D 4 A_CustomMeleeAttack(8*random(1,8));
		HLDA EF 4;
		HLDW ABCD 4 A_ExtChase(0,0);
		Goto See;
	Missile:
		HLDB ABC 6 A_FaceTarget;
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spit");
		HLDB D 6 A_FaceTarget;
		TNT1 AAAAAAA 0 A_SpawnItemEx("BSSpitExtra2",0,0,20,random(2,5),random(2,-2),random(1,3));
		HLDB E 6 A_SpawnProjectile("BullSquidSpit",20,0,0);
		HLDB FG 6;
		Goto See;
	Pain:
		HLDP A 8 A_Pain;
		Goto See;
	Death:
		HLDD A 5 A_Scream;
		HLDD B 4 A_NoBlocking;
		HLDD C 3 A_PlaySound("halflife/weapons/bodydrop");
		HLDD DEFG 3;
		HLDD H -1;
		Stop;
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath");
		TNT1 A 0 A_NoBlocking;
	  	TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		Stop;
	}
}

class BullSquidSpit : Actor
{
	Default
	{
		Projectile;
		Speed 30;
		Scale 0.35;
		Damage 7;
		RenderStyle "Add";
		Alpha 0.9;
		Translation "80:111=118:127";
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		X022 ABCD 4;
		Loop;
	Death:
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit");
		TNT1 AAAA 0 A_SpawnItemEx("BSSpitExtra",0,0,0,random(1,-1),random(1,-1),random(0,2),0,0);
		Stop;
	}
}

class BSSpitExtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Scale 0.15;
		RenderStyle "Add";
		Alpha 0.9;
		-NOGRAVITY
		Gravity .125;
		+FORCEXYBILLBOARD
		Translation "80:111=118:127";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(128,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

class BSSpitExtra2 : BSSpitExtra
{
	Default
	{
		Scale 0.26;
	}
}