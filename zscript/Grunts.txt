//====================================
// Government Troops: Random Selector
//====================================

class HLGruntAny : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1";
		DropItem "HLGrunt2";
		DropItem "HLGrunt3";
		DropItem "HLGrunt4";
		DropItem "HLGrunt5";
		DropItem "HLGrunt6";
	}
}

class HLGruntM16 : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1";
		DropItem "HLGrunt2";
		DropItem "HLGrunt3";
	}
}

class HLGruntShotgun : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt4";
		DropItem "HLGrunt5";
		DropItem "HLGrunt6";
	}
}

class HLGruntNoGrenadesAny : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1NoGren";
		DropItem "HLGrunt2NoGren";
		DropItem "HLGrunt3NoGren";
		DropItem "HLGrunt4NoGren";
		DropItem "HLGrunt5NoGren";
		DropItem "HLGrunt6NoGren";
	}
}

class HLGruntM16NoGrenades : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt1NoGren";
		DropItem "HLGrunt2NoGren";
		DropItem "HLGrunt3NoGren";
	}
}

class HLGruntShotgunNoGrenade : RandomSpawner
{
	Default
	{
		DropItem "HLGrunt4NoGren";
		DropItem "HLGrunt5NoGren";
		DropItem "HLGrunt6NoGren";
	}
}

//==========================================
// Government Troops: M16 & Grenade Attack
//==========================================

class HLGrunt1 : Actor
{
	bool closegrencheck;
	int GrenTimeCounter;
	Default
	{
		Monster;
		Health 150;
		Mass 150;
		Speed 3.5;
		Height 63;
		PainChance 70;
		Scale .343;
		maxdropoffheight 128;
		DropItem "M16Pickup";
		DamageFactor "SoldierDamage",0;
		SeeSound "halflife/monsters/humangrunt/sight";
		PainSound "halflife/monsters/humangrunt/pain";
		DeathSound "halflife/monsters/humangrunt/death";
		ActiveSound "halflife/monsters/humangrunt/active";
	}
	States
	{
	Spawn:
		HK1I A 3 A_Look;
		Loop;
	See:
		HK1R A 0;
		"####" A 0
		{
			if(closegrencheck)
			{
				bFRIGHTENED=1;
				if(GrenTimeCounter>=4)
				{
					closegrencheck=0;
					GrenTimeCounter-=4;
					bFRIGHTENED=0;
				}
				else
				{
					GrenTimeCounter++;
					return ResolveState("Flee");
				}
			}
			if(CheckIfCloser(target,300))
				return ResolveState("Walking");
			A_SetSpeed(3.5);
			return state(null);
		}
		"####" A 1 A_Chase;
		"####" A 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" A 1 A_Chase;
		"####" A 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" B 1 A_Chase;
		"####" B 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" B 1 A_Chase;
		"####" B 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" C 1 A_Chase;
		"####" C 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" C 1 A_Chase;
		"####" C 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" D 1 A_Chase;
		"####" D 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" D 1 A_Chase;
		"####" D 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" E 1 A_Chase;
		"####" E 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" E 1 A_Chase;
		"####" E 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" F 1 A_Chase;
		"####" F 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" F 1 A_Chase;
		"####" F 1 A_Chase(null,null,HLSMOOTHCHASE);
		Goto See+1;
	Walking:
		HK1W A 0;
		"####" A 0
		{
			bMISSILEEVENMORE=1;
			bMISSILEMORE=1;
			A_SetSpeed(1.75);
		}
		"####" A 1 A_Chase;
		"####" AAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" B 1 A_Chase;
		"####" BBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" C 1 A_Chase;
		"####" CCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" D 1 A_Chase;
		"####" DDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" E 1 A_Chase;
		"####" EEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" F 1 A_Chase;
		"####" FFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" A 0
		{
			bMISSILEEVENMORE=0;
			bMISSILEMORE=0;
		}
		"####" A 0 A_Jump(256,"See");
	Flee:
		"####" A 0 A_SetSpeed(3.5);
		"####" A 1 A_Chase(null,null);
		"####" A 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" A 1 A_Chase(null,null);
		"####" A 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" B 1 A_Chase(null,null);
		"####" B 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" B 1 A_Chase(null,null);
		"####" B 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" C 1 A_Chase(null,null);
		"####" C 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" C 1 A_Chase(null,null);
		"####" C 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" D 1 A_Chase(null,null);
		"####" D 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" D 1 A_Chase(null,null);
		"####" D 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" E 1 A_Chase(null,null);
		"####" E 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" E 1 A_Chase(null,null);
		"####" E 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" F 1 A_Chase(null,null);
		"####" F 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" F 1 A_Chase(null,null);
		"####" F 1 A_Chase(null,null,HLSMOOTHCHASE);
		"####" A 0 A_Jump(256,"See");
	Missile:
		TNT1 A 0 A_Jump(200,"Assault");
		TNT1 A 0 A_JumpIfCloser(600,"Grenade");
		TNT1 A 0 A_JumpIfCloser(1000,"GrenadeGun");
		TNT1 A 0 A_Jump(256,"Assault");
	Assault:
		HK1O A 0;
		"####" AAAA 3 A_FaceTarget;
		"####" A 2 A_FaceTarget;
		"####" A 0
		{
			A_PlaySound("halflife/weapons/machinegunshot",5);
			for(int i=0;i<2;i++)
				A_SpawnItemEx("GunSmallSmoke",15,3,49);
		}
		"####" B 2 Light("ImShootingAGun") A_CustomBulletAttack(7,7,1,4,"NewPuff2");
		"####" A 0 A_Jump(15,"See");
		"####" A 0 A_CposRefire;
		Goto Assault+5;
	GrenadeGun:
		HK1M A 0;
		"####" AAAAAAAAA 3 A_FaceTarget;
		"####" B 4;
		"####" A 0 A_PlaySound("halflife/weapons/grenadelaunch",5);
		"####" AA 0 A_SpawnItemEx("GunSmoke",15,3,49);
		"####" A 0 A_JumpIfCloser(500,"GrenadeGunClose");
		"####" B 5 Light("ImShootingAGun") A_SpawnProjectile("HLGruntGunGrenade",38,0,0);
		"####" BA 5;
		"####" A 0 A_Jump(256,"See");
	GrenadeGunClose:
		"####" B 5 Light("ImShootingAGun") A_SpawnProjectile("HLGruntGunGrenadeClose",38,0,0);
		"####" BA 5;
		"####" A 0 A_Jump(256,"See");
	Grenade:
		HK1N A 0;
		"####" A 0 A_JumpIfCloser(256,"Assault");
		"####" A 0 A_Jump(64,"GrenadeGun");
		"####" A 3 A_FaceTarget;
		"####" B 17;
		"####" CD 5 A_FaceTarget;
		"####" E 6 A_SpawnProjectile("GruntHandGrenade",40);
		"####" F 7;
		"####" A 0 A_Jump(256,"See");
	Melee:
		HK1N A 0;
		"####" A 3 A_FaceTarget;
		"####" B 10;
		"####" C 5 A_FaceTarget;
		"####" D 6
		{
			closegrencheck=1;
			A_SpawnProjectile("GruntHandGrenadeClose",40);
		}
		"####" F 7;
		"####" A 0 A_Jump(256,"See");
	Pain:
		HK1P A 8 A_Pain;
		"####" A 0
		{
			if(closegrencheck)
				GrenTimeCounter++;
		}
		"####" A 0 A_Jump(256,"See");
	Death:
		HK1B A 2 A_Scream;
		HK1B B 2 A_Fall;
		HK1D ABCDEFGHIJ 2;
		HK1D K -1 A_PlaySound("halflife/weapons/bodydrop");
		Stop;
	XDeath:
		TNT1 A 0 A_PlaySound("other/gibdeath");
		TNT1 A 0 A_SpawnItemEx("BloodGibDropper",0,0,random(20,40));
		TNT1 A 1 A_Fall;
		Stop;
	}
}

//==========================================
// Government Troops: M16 & Grenade Attack
//==========================================

class HLGrunt2 : HLGrunt1
{
	States
	{
	Spawn:
		HK2I A 3 A_Look;
		Loop;
	See:
		HK2R A 0;
		Goto Super::See+1;
	Walking:
		HK2W A 0;
		Goto Super::Walking+1;
	Assault:
		HK2O A 0;
		Goto Super::Assault+1;
	GrenadeGun:
		HK2M A 0;
		Goto Super::GrenadeGun+1;
	Grenade:
		HK2N A 0;
		Goto Super::Grenade+1;
	Melee:
		HK2N A 0;
		Goto Super::Melee+1;
	Pain:
		HK2P A 8 A_Pain;
		Goto Super::Pain+1;
	Death:
		HK2B A 3 A_Scream;
		HK2B B 3 A_Fall;
		HK2B CDE 3;
		HK2D ABCD 3;
		HK2D E -1 A_PlaySound("halflife/weapons/bodydrop");
		Stop;
	}
}

//==========================================
// Government Troops: M16 & Grenade Attack
//==========================================

class HLGrunt3 : HLGrunt1
{
	States
	{
	Spawn:
		HK3I A 3 A_Look;
		Loop;
	See:
		HK3R A 0;
		Goto Super::See+1;
	Walking:
		HK3W A 0;
		Goto Super::Walking+1;
	Assault:
		HK3O A 0;
		Goto Super::Assault+1;
	GrenadeGun:
		HK3M A 0;
		Goto Super::GrenadeGun+1;
	Grenade:
		HK3N A 0;
		Goto Super::Grenade+1;
	Melee:
		HK3N A 0;
		Goto Super::Melee+1;
	Pain:
		HK3P A 8 A_Pain;
		Goto Super::Pain+1;
	Death:
		HK3B A 2 A_Scream;
		HK3B B 2 A_Fall;
		HK3B C 2;
		HK3D ABCDEFGH 2;
		HK3D I -1 A_PlaySound("halflife/weapons/bodydrop");
		Stop;
	}
}

//============================================
// Government Troops: Shotgun & Grenade Attack
//============================================

class HLGrunt4 : HLGrunt1
{
	Default
	{
		DropItem "ShotgunPickup";
	}
	States
	{
	Spawn:
		HK4I A 3 A_Look;
		Loop;
	See:
		HK4R A 0;
		Goto Super::See+1;
	Walking:
		HK4W A 0;
		Goto Super::Walking+1;
	Missile:
		TNT1 A 0 A_Jump(200,"Shotgun");
		TNT1 A 0 A_JumpIfCloser(600,"Grenade");
		TNT1 A 0 A_Jump(256,"Shotgun");
	Shotgun:
		HK4N A 0;
		"####" A 0 A_Jump(64,"ShotgunDouble");
		"####" AAAA 3 A_FaceTarget;
		"####" AAA 4 A_FaceTarget;
		"####" A 0 A_PlaySound("halflife/weapons/shotgunsingle",5);
		"####" AA 0 A_SpawnItemEx("GunSmoke",15,4,47);
		"####" B 4 Light("ImShootingAGun") A_CustomBulletAttack(7,7,5,4,"NewPuff2");
		"####" BCBA 4;
		"####" A 0 A_Jump(40,"See");
		"####" A 0 A_CposRefire;
		Goto Shotgun+6;
	ShotgunDouble:
		"####" AAAA 3 A_FaceTarget;
		"####" AAAAA 4 A_FaceTarget;
		"####" A 0 A_PlaySound("halflife/weapons/shotgundouble",5);
		"####" AA 0 A_SpawnItemEx("GunSmoke",15,4,47);
		"####" B 4 Light("ImShootingAGun") A_CustomBulletAttack(7,7,10,4,"NewPuff2");
		"####" BCBA 4;
		"####" AAA 4 A_FaceTarget;
		"####" A 0 A_Jump(100,"See");
		"####" A 0 A_CposRefire;
		Goto ShotgunDouble+4;
	Grenade:
		HK4M A 0;
		"####" A 0 A_JumpIfCloser(256,"Shotgun");
		Goto Super::Grenade+3;
	Melee:
		HK4N A 0;
		Goto Super::Melee+1;
	Pain:
		HK4P A 8 A_Pain;
		Goto Super::Pain+1;
	Death:
		HK1C A 2 A_Scream;
		HK1C B 2 A_Fall;
		HK1D ABCDEFGHIJ 2;
		HK1D K -1 A_PlaySound("halflife/weapons/bodydrop");
		Stop;
	}
}

//============================================
// Government Troops: Shotgun & Grenade Attack
//============================================

class HLGrunt5 : HLGrunt4
{
	States
	{
	Spawn:
		HK5I A 3 A_Look;
		Loop;
	See:
		HK5R A 0;
		Goto HLGrunt1::See+1;
	Walking:
		HK5W A 0;
		Goto HLGrunt1::Walking+1;
	Shotgun:
		HK5N A 0;
		Goto Super::Shotgun+1;
	Grenade:
		HK5M A 0;
		Goto Super::Grenade+1;
	Melee:
		HK5N A 0;
		Goto HLGrunt1::Melee+1;
	Pain:
		HK5P A 8 A_Pain;
		Goto HLGrunt1::Pain+1;
	Death:
		HK2C A 3 A_Scream;
		HK2C B 3 A_Fall;
		HK2C CDE 3;
		HK2D ABCD 3;
		HK2D E -1 A_PlaySound("halflife/weapons/bodydrop");
		Stop;
	}
}

//============================================
// Government Troops: Shotgun & Grenade Attack
//============================================

class HLGrunt6 : HLGrunt4
{
	States
	{
	Spawn:
		HK6I A 3 A_Look;
		Loop;
	See:
		HK6R A 0;
		Goto HLGrunt1::See+1;
	Walking:
		HK6W A 0;
		Goto HLGrunt1::Walking+1;
	Shotgun:
		HK6N A 0;
		Goto Super::Shotgun+1;
	Grenade:
		HK6M A 0;
		Goto Super::Grenade+1;
	Melee:
		HK6M A 0;
		Goto HLGrunt1::Melee+1;
	Pain:
		HK6P A 8 A_Pain;
		Goto HLGrunt1::Pain+1;
	Death:
		HK3C A 2 A_Scream;
		HK3C B 2 A_Fall;
		HK3C C 2;
		HK3D ABCDEFGH 2;
		HK3D I -1 A_PlaySound("halflife/weapons/bodydrop");
		Stop;
	}
}

//===============================
// Government Troops: M16 Attack
//===============================

class HLGrunt1NoGren : HLGrunt1
{
	Default
	{
		Mass 200;
		PainChance 64;
	}
	States
	{
	Missile:
		Goto Assault;
	Melee:
		Stop;
	}
}

//===============================
// Government Troops: M16 Attack
//===============================

class HLGrunt2NoGren : HLGrunt2
{
	Default
	{
		Mass 200;
		PainChance 64;
	}
	States
	{
	Missile:
		Goto Assault;
	Melee:
		Stop;
	}
}

//===============================
// Government Troops: M16 Attack
//===============================

class HLGrunt3NoGren : HLGrunt3
{
	Default
	{
		Mass 200;
		PainChance 64;
	}
	States
	{
	Missile:
		Goto Assault;
	Melee:
		Stop;
	}
}

//==================================
// Government Troops: Shotgun Attack
//==================================

class HLGrunt4NoGren : HLGrunt4
{
	Default
	{
		Mass 200;
		PainChance 64;
	}
	States
	{
	Missile:
		Goto Shotgun;
	Melee:
		Stop;
	}
}

//==================================
// Government Troops: Shotgun Attack
//==================================

class HLGrunt5NoGren : HLGrunt5
{
	Default
	{
		Mass 200;
		PainChance 64;
	}
	States
	{
	Missile:
		Goto Shotgun;
	Melee:
		Stop;
	}
}

//==================================
// Government Troops: Shotgun Attack
//==================================

class HLGrunt6NoGren : HLGrunt6
{
	Default
	{
		Mass 200;
		PainChance 64;
	}
	States
	{
	Missile:
		Goto Shotgun;
	Melee:
		Stop;
	}
}

//********************************************
class GruntHandGrenade : Actor
{
	int GrenTimeCounter;
	Default
	{
		Mass 1;
		Radius 3;
		Height 6;
		Speed 20;
		Scale 0.3;
		Damage 1;
		Damagetype "SoldierDamage";
		PROJECTILE;
		BounceType "Doom";
		+CANBOUNCEWATER
		BounceFactor 0.6;
		-NOGRAVITY
		Gravity 0.5;
	}
	States
	{
	Spawn:
		HGN1 ABCDE 2;
		TNT1 A 0 {GrenTimeCounter++;}
		TNT1 A 0 A_JumpIf(GrenTimeCounter>=9,"Explode");
		Loop;
	Death:
		HGN1 A 10;
		TNT1 A 0 {GrenTimeCounter++;}
		TNT1 A 0 A_JumpIf(GrenTimeCounter>=9,"Explode");
		Loop;
	Explode:
		TNT1 A 0 A_PlaySound("halflife/weapons/explode1");
		TNT1 A 0 Bright A_Explode(128,64,1);
		TNT1 A 0 A_SpawnItemEx("NewExplosionMedium");
		TNT1 A 10;
		Stop;
	}
}
class GruntHandGrenadeClose : GruntHandGrenade
{
	Default
	{
		Speed 10;
	}
}

//********************************************
class HLGruntGunGrenade : Actor
{
	Default
	{
		Projectile;
		Speed 20;
		Damage 20;
		Radius 10;
		Height 20;
		Damagetype "SoldierDamage";
		-NoGravity
		Gravity .2;
		Scale .65;
		DeathSound "halflife/weapons/explode1";
	}
	States
	{
	Spawn:
		GRNL A -1 NODELAY ThrustThingZ(0,14,0,1);
		Wait;
	Death:
		TNT1 A 10
		{
			A_Explode(64);
			A_SpawnItemEx("NewExplosionMedium");
		}
		Stop;
	}
}

class HLGruntGunGrenadeClose : HLGruntGunGrenade
{
	States
	{
	Spawn:
		GRNL A -1 NODELAY ThrustThingZ(0,5,0,1);
		Wait;
	}
}