//==================
// Alien Controller
//==================

class HLController : HLMonster
{
	Default
	{
		Health 200;
		Mass 200;
		Speed 2.666667;
		FloatSpeed 1.333333;
		Height 56;
		Radius 23;
		PainChance 64;
		PainChance "Crowbar",210;
		+NOGRAVITY
		+FLOAT
		Scale .45;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/controller/Sight";
		PainSound "halflife/monsters/controller/Pain";
		DeathSound "halflife/monsters/controller/Death";
		ActiveSound "halflife/monsters/controller/idle";
		Species "HLController";
	}
	States
	{
	Spawn:
		HLCW A 3 A_Look;
		Loop;
	See:
		HLCW A 1 A_Chase;
		HLCW AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		TNT1 A 0
		{
			A_PlaySound("halflife/monsters/controller/attack");
			switch(random(0,2))		//equal chance to go right, left or not move at all
			{
			case 1:
				vel.y+=random(5,8);
				break;
			case 2:
				vel.y-=random(5,8);
				break;
			}
		}
		TNT1 A 0 A_Jump(128,"Missile2");
		HLCA ABCD 3 A_FaceTarget;
		HLCA EEEEEE 5 A_SpawnProjectile("ControllerFireball1",32,0,random(10,-10));
		HLCA F 4 A_Stop;
		Goto See;
	Missile2:
		HLCB AB 4 A_FaceTarget;
		HLCB CCCCC 3 A_FaceTarget;
		HLCB D 7;
		HLCB E 14;
		HLCB F 15 A_SpawnProjectile("ControllerFireball2",42);
		HLCB G 9;
		HLCB H 5 A_Stop;
		Goto See;
	Pain:
		HLCP A 8;
		Goto See;
	Death:
		HLCD A 3 A_Scream;
		HLCD BC 3;
		Wait;
	Crash:
		HLCD D 2 A_PlaySound("halflife/weapons/bodydrop");
		HLCD EFGHI 2;
		HLCD J 2 A_NoBlocking;
		HLCD LMN 2;
		HLCD O -1;
		Stop;
	XDeath:
		TNT1 A 0 A_XenGibs(17);
		Stop;
	}
}

class ControllerFireball1 : Actor
{
	Default
	{
		Projectile;
		Speed 18;
		Damage 3;
		Radius 8;
		Height 12;
		Scale .5;
		RenderStyle "Add";
		DeathSound "halflife/weapons/gausshit2";
		Alpha .9;
	}
	States
	{
	Spawn:
		X029 ABCDEFGHIJK 2 Bright LIGHT("YellowMediumPoint3");
		Loop;
	Death:
		TNT1 A 5 LIGHT("YellowSmallFlicker1")
		{
			for(int i=0;i<6;i++)
				A_SpawnItemEx("ControllerFireballSpark",0,0,0,frandom(-1.1,1.1),frandom(-1.1,1.1),frandom(-1.1,1.1),random(0,359));
		}
		TNT1 A 5 LIGHT("YellowSmallFlicker2");
		TNT1 A 5 LIGHT("YellowSmallFlicker3");
		Stop;
	}
}
class ControllerFireballSpark : Actor
{
	Default
	{
		radius 2;
		height 2;
		+NOINTERACTION
		Scale 0.02;
		RenderStyle "Add";
		+FORCEXYBILLBOARD
		Translation "80:111=160:167";
	}
	states
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(255,1,2,3);
		X099 A 1 A_FadeOut(.03);
		Wait;
		X099 A 1 A_FadeOut(.04);
		Wait;
		X099 A 1 A_FadeOut(.05);
		Wait;
	}
}

class ControllerFireball2 : ControllerFireball1
{
	Default
	{
		Speed 10;
		Damage 12;
		Scale .4;
		DeathSound "halflife/weapons/gausshit1";
		+SeekerMissile
	}
	States
	{
	Spawn:
		X025 BCDEFGHIJKLMNOPQRSTUVWXY 2 Bright LIGHT("OrangeMediumPoint2") A_SeekerMissile(5,5);
		Loop;
	Death:
		TNT1 A 5 LIGHT("OrangeMediumFlicker3")
		{
			for(int i=0;i<8;i++)
				A_SpawnItemEx("ControllerFireball2Spark",0,0,0,frandom(-0.7,0.7),frandom(-0.7,0.7),frandom(-0.7,0.7),random(0,359));
		}
		TNT1 A 5 LIGHT("OrangeSmallFlicker1");
		TNT1 A 5 LIGHT("OrangeSmallFlicker2");
		Stop;
	}
}
class ControllerFireball2Spark : ControllerFireballSpark
{
	Default
	{
		Scale 0.3;
	}
	states
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(255,1,3,5,7,9,11,13,15);
	Looper:
		X025 BCDEFGHIJKLMNOPQRSTUVWXY 2 Bright A_FadeOut(0.15);
		loop;
	}
}