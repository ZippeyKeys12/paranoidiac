//Osprey
class OspreyBellmer : Actor
{
	int bobstate;
	override void tick()
	{
		if(pos.z!=floorz)
		{
			double newz=pos.z-BobSin(bobstate)/2.;
			bobstate=(bobstate+1)&63;
			newz+=BobSin(bobstate)/2.;
			SetZ(newz);
		}
		super.tick();
	}
	Default
	{
		Radius 3;
		Height 16;
		Mass 0x7FFFFFFF;
		+NOCLIP
		+NOBLOCKMAP
		+NOGRAVITY
		+NOTARGET
		+SHOOTABLE
		+NONSHOOTABLE
		+NOBLOOD
		+NOTRIGGER
		+NOTELEPORT
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2;
		HLI2 AB 2;
		Loop;
	}
}

class Osprey : OspreyBellmer
{
	int crashloop;
	actor lblade,rblade;
	override void postbeginplay()
	{
		lblade=Spawn("OspreyBlade",pos);
		rblade=Spawn("OspreyBlade",pos);
		A_PlaySound("play/osprey",looping:1);
		super.postbeginplay();
	}
	override void tick()
	{
		super.tick();
		double a;
		if(lblade)
		{
			a=angle+90;
			lblade.SetOrigin((pos.x+cos(a)*340.0,pos.y+sin(a)*340.0,pos.z),true);
		}
		if(rblade)
		{
			a=angle+270;
			rblade.SetOrigin((pos.x+cos(a)*340.0,pos.y+sin(a)*340.0,pos.z),true);
		}
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJ 2;
		Loop;
	Death:
		SPOS A 1
		{
			if(lblade)
				lblade.SetStateLabel("Null");
			if(rblade)
				rblade.SetStateLabel("Null");
			A_SpawnItemEx("NewExplosionMedium",-176,280,128);
			A_PlaySound("halflife/weapons/explode1",CHAN_AUTO,1.0,false,ATTN_NONE);
			A_Quake(9,16,0,1024);
			A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExplosionBig",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExplosionBig",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExploBigSmoke");
		}
		SPOS B 1
		{
			A_SpawnItemEx("NewExplosionMedium",-176,280,80);
			A_SpawnItemEx("NewExploBigSmoke",-176,280,132);
			A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExploBigSmoke");
		}
		SPOS C 1
		{
			A_SpawnItemEx("NewExplosionMedium",-176,280,64);
			A_PlaySound("halflife/weapons/explode2",CHAN_AUTO,1.0,false,ATTN_NONE);
			A_SpawnItemEx("NewExploBigSmoke",-176,280,132);
			A_SpawnItemEx("NewExplosionBig",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExploBigSmoke");
		}
		SPOS D 1
		{
			A_SpawnItemEx("NewExploBigSmoke",-176,280,132);
			A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExploBigSmoke");
		}
		SPOS E 1
		{
			A_PlaySound("halflife/weapons/explode3",CHAN_AUTO,1.0,false,ATTN_NONE);
			A_SpawnItemEx("NewExploBigSmoke",-176,280,132);
			A_SpawnItemEx("NewExplosionBig",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExploBigSmoke");
		}
	DeathLoop:
		SPOS FGHIJKLMNOPQRSTUVWXABCDE 1
		{
			A_SpawnItemEx("NewExploBigSmoke",-176,280,132);
			A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
			A_SpawnItemEx("NewExploBigSmoke");
		}
		Loop;
	Crash:
		HLI1 I 1
		{
			A_PlaySound("halflife/weapons/explode1",CHAN_AUTO,1.0,false,ATTN_NONE);
			A_Quake(9,40,0,1024);
			A_PlaySound("Osprey/Crash",CHAN_AUTO,1.0,false,ATTN_NONE);
		}
		HLI1 III 1 A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
		HLI1 I 0 A_HLOspreySpark(8);
		HLI1 JJJ 1 A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
		HLI1 J 0 A_HLOspreySpark(8);
		HLI1 KKK 3 A_SpawnItemEx("NewExplosionMedium",frandom(-96,96),frandom(-96,96),frandom(0,96));
		HLI1 KKK 3 A_HLOspreyBigSmoke;
		HLI1 K 0 A_HLOspreySpark(2);
		HLI1 K 3 A_HLOspreyBigSmoke;
		HLI1 K 0 A_HLOspreySpark(4);
		HLI1 K 3 A_HLOspreyBigSmoke;
		HLI1 K 0 A_HLOspreySpark(2);
		HLI1 K 1 A_HLOspreyBigSmoke;
		HLI1 KK 1 A_HLOspreySpark;
		HLI1 K 2 A_HLOspreyBigSmoke;
		HLI1 KK 1 A_HLOspreySpark;
		HLI1 K 3 A_HLOspreyBigSmoke;
		HLI1 KK 1 A_HLOspreySpark;
		HLI1 K 4 A_HLOspreyBigSmoke;
		HLI1 KK 1 A_HLOspreySpark;
		HLI1 K 5 A_HLOspreyBigSmoke;
		HLI1 KK 1 A_HLOspreySpark;
	CrashLoop:
		HLI1 K 4 A_HLOspreyBigSmoke;
		HLI1 KKKK 1 A_HLOspreySpark;
		HLI1 K 0
		{
			crashloop++;
			if(crashloop<12)
				return ResolveState("CrashLoop");
			return state(null);
		}
		HLI1 KKKKKKKK 8 A_HLOspreyBigSmoke;
		HLI1 K -1;
		Stop;
	}
	void A_HLOspreySpark(int num=1)
	{
		for(int i=0;i<num;i++)
			A_SpawnItemEx("Spark_O1",frandom(-96,96),frandom(-96,96),frandom(32,96),frandom(-4,4),frandom(-4,4),frandom(-4,4));
	}
	void A_HLOspreyBigSmoke()
		{A_SpawnItemEx("NewExploBigSmoke",frandom(-96,96),frandom(-96,96),frandom(0,96),0,0,frandom(5,8));}
}

class OspreyBlade : ModelBase
{
	Default
	{
		-SOLID
		+NOCLIP
		+NOBLOCKMAP
		+NOGRAVITY
	}
}

//SUV
class SUV : ModelBase
{
	actor glass;
	override void postbeginplay()
	{
		glass=Spawn("SUVGlass",pos);
		glass.angle=angle;
		glass.ChangeTid(tid);
		glass.master=self;
		super.postbeginplay();
	}
	Default
	{
		Radius 38;
		Height 66;
		Mass 0x7FFFFFFF;
		DeathHeight 66;
		Health 400;
		PainChance 256;
		+ISMONSTER
		+SHOOTABLE
		+NOBLOOD
		+FRIENDLY
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	Pain:
		HLI1 A -1 A_PlaySound("SUV/Hit",CHAN_AUTO);
		Stop;
	Death:
		HLI1 B 1
		{
			A_KillChildren();
			A_Explode(128,192);
			A_PlaySound("SUV/Explode");
		}
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",56,32,40);
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",-64,-40,56);
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",frandom(-16,16),-40,32);
		HLI1 B 1 A_SpawnItemEx("NewExplosionMedium",frandom(-16,16),40,32);
		HLI1 B 8
		{
			for(int i=0;i<16;i++)
				A_SpawnItemEx("Spark_O1",frandom(-96,96),frandom(-96,96),frandom(32,96),frandom(-4,4),frandom(-4,4),frandom(-4,4));
		}
		HLI1 BBBB 6 A_SpawnItemEx("NewExploBigSmoke",frandom(-64,64),frandom(-48,48),frandom(32,64),0,0,frandom(1,4));
		HLI1 B -1;
		Stop;
	}
}

class SUVGlass : ModelBase
{
	Default
	{
		Renderstyle "Translucent";
		Alpha .5;
		Radius 16;
		Height 56;
		Mass 0x7FFFFFFF;
		-SOLID
		+NOTARGET
		+NOBLOOD
		+SHOOTABLE
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	Death:
		HLI1 BCDEFGHI 1;
		HLI1 JKLM 1 A_FadeOut;
		Stop;
	}
}

//MTM 8 Loader
class Loader : ModelBase
{
	actor box;
	override void postbeginplay()
	{
		bool num;
		[num,box]=A_SpawnItemEx("LoaderBox",112,0,32,flags:SXF_SETMASTER,tid:tid);
		super.postbeginplay();
	}
	override void tick()
	{
		if(box)
			box.A_Warp(AAPTR_MASTER,112,0,32);
		super.tick();
	}
	Default
	{
		Radius 64;
		Height 96;
		Mass 0x7FFFFFFF;
		Scale .6;
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	See:
		HLW1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLW2 ABCDEFGHIJKLMNOPQRST 1;
		Loop;
	}
}
class LoaderNoBox : Loader
{
	actor lclaw,rclaw;
	override void postbeginplay()
	{
		bool num;
		[num,lclaw]=A_SpawnItemEx("LoaderClaw",96,-32,32,flags:SXF_SETMASTER,tid:tid);
		[num,rclaw]=A_SpawnItemEx("LoaderClaw",96,32,32,flags:SXF_SETMASTER,tid:tid);
		actor.postbeginplay();
	}
	States
	{
	Spawn:
		HLI1 B -1;
		Stop;
	}
}
class LoaderBox : Actor
{
	Default
	{
		Radius 32;
		Height 64;
		+SOLID
		+NOGRAVITY
		+NOLIFTDROP
		+ACTLIKEBRIDGE
	}
}
class LoaderClaw : LoaderBox
{
	Default
	{
		Radius 16;
		Height 32;
	}
}