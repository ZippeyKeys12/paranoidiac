actor HLBabyHeadCrab 7418
{
	Monster
	Health 10
	Speed 8
	Height 8
	Radius 8
	-Solid
	RenderStyle Translucent
	Alpha 0.9
	PainChance 200
	MaxTargetRange 128
	Scale 0.09
	BloodColor "DE FE 72"
	SeeSound "halflife/monsters/headcrab/sight"
	PainSound "halflife/monsters/headcrab/pain"
	DeathSound "halflife/monsters/headcrab/death"
	ActiveSound "halflife/monsters/headcrab/idle"
	MeleeSound "halflife/monsters/headcrab/meleehit"
	MeleeDamage 1
	+DONTHURTSPECIES 
	DamageFactor "Gonarch", 0.0
	States
	{
	Spawn:
		HLQI A 3 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIfInventory("CrabJump",1,"SeeMelee")
		HLQW ABCDE 5 A_ExtChase(0,1)
		Loop
	SeeMelee:
		HLQW A 5 A_ExtChase(1,0)
		TNT1 A 0 A_TakeInventory("CrabJump",1)
		HLQW B 5 A_ExtChase(1,0)
		Goto See+3
	Melee:
		HLQW A 10 A_MeleeAttack
		Goto See
	Missile:
		HLQI A 4 A_FaceTarget
		HLQM AB 3
		TNT1 A 0 A_GiveInventory("CrabJump",1)
		HLQM C 1 A_SkullAttack
		HLQM C 2 ThrustThingZ(0,random(10,25),0,1)
		HLQM C 3 
		HLQM C 3 A_CheckFloor(1)
		Wait
		HLQM D 30 A_stop
		HLQM D 30 
		Goto See
	Pain:
		TNT1 A 0 A_TakeInventory("CrabJump",1)
		HLQP A 8
		Goto See
	Death:
		TNT1 A 0 A_Fall
	  	NULL AAAA 0 A_SpawnItemEx("XenGibsMini",random(2,-2),random(2,-2),random(0,20),random(1,4),random(1,4),random(2,6),random(0,359))
		Stop
	}
}

Actor HLBabyHeadCrab2 : HLBabyHeadCrab	//for the gonarch
{
	Speed 10
	States
	{
	See:
		TNT1 A 0 A_JumpIfInventory("CrabJump",1,"SeeMelee")
		HLQW ABCDE 3 A_ExtChase(0,1)
		Loop
	SeeMelee:
		HLQW A 3 A_ExtChase(1,0)
		TNT1 A 0 A_TakeInventory("CrabJump",1)
		HLQW B 3 A_ExtChase(1,0)
		Goto See+3
	}
}