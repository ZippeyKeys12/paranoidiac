#library "BUBBLES"
#include "zcommon.acs"

//////////////////////////////
// Script 729-730: Bubbles	//
//////////////////////////////

#define SHAPE_SQUARE    0
#define SHAPE_CIRCLE    1
#define SHAPE_LINE      2

str Bubble[5] = 
{
  "Bubble1",
  "Bubble2",
  "Bubble3",
  "Bubble4",
  "Bubble5"
};

str OldBubble[3] =
{
  "OldSchoolBubble1",
  "OldSchoolBubble2",
  "OldSchoolBubble3"
};

script 730 (int area, int intensity, int shape)
{
  int i, a, r;
  area += random(0, 8);
  intensity += 1;
  switch (shape)
  {
  case SHAPE_SQUARE:
    area /= 2;
    area <<= 16;
    for (i=0; i<intensity; i++)
    {
      Spawn(Bubble[random(0, 4)],
        GetActorX(0) + random(-area, area),
        GetActorY(0) + random(-area, area),
        GetActorZ(0),
        0,
        random(0, 255));
    }
    break;
  case SHAPE_CIRCLE:
    area /= 2;
    area <<= 16;
    for (i=0; i<intensity; i++)
    {
      r = random(0, area);
      a = random(0, 65535);
      Spawn(Bubble[random(0, 4)],
        GetActorX(0) + FixedMul(cos(a), r),
        GetActorY(0) + FixedMul(sin(a), r),
        GetActorZ(0),
        0,
        a >> 8);
    }
    break;
  case SHAPE_LINE:
    area <<= 16;
    for (i=0; i<intensity; i++)
    {
      r = random(0, area);
      Spawn(Bubble[random(0, 4)],
        GetActorX(0) + FixedMul(cos(GetActorAngle(0)), r),
        GetActorY(0) + FixedMul(sin(GetActorAngle(0)), r),
        GetActorZ(0),
        0,
        GetActorAngle(0) >> 8);
    }
    break;
  }
}

script 729 (void)
{
  int r;
  r = random(0, 1.0);
  SetActorAngle(0, GetActorAngle(0) + 0.125);
  SetActorPosition(0,
    GetActorX(0) + FixedMul(cos(GetActorAngle(0)), r),
    GetActorY(0) + FixedMUl(sin(GetActorAngle(0)), r),
    GetActorZ(0),
    0);
}
