class OMGSteam : SwitchableDecoration
{
	Default
	{
		height 2;
		radius 1;
		+NOCLIP
		+NOSECTOR
		+NOGRAVITY
		+NOBLOCKMAP
		+LOOKALLAROUND
	}
	states
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY
		{
			A_PlaySound("Steam/Loop",7,1,1);
			scale.x=(args[0]+1)/10.0;
			scale.y=scale.x;
		}
		TNT1 A 0 A_CheckSightOrRange(2560,"IdleNoSee");
		TNT1 A 1 A_SpawnItemEx("Steam1",0,0,0,
			(0.1)*steamscript(args[0],args[1],0),
			(0.1)*steamscript(args[0],args[1],1),
			(0.1)*steamscript(args[0],args[1],2),
			0,SXF_TRANSFERSCALE);
		Loop;
	IdleNoSee:
		TNT1 A 10;
		TNT1 A 0 A_CheckSightOrRange(2560,"IdleNoSee");
		goto Active;
	Inactive:
		TNT1 A -1 A_StopSound(7);
		stop;
	}
	action int steamscript(int size,int thispitch,int plane)
	{
		int momentum;
		thispitch+=random(-4,4);
		float r=frandom(30.0,35.0);
		switch(plane)
		{
		case 0:
			momentum=cos(angle)*(cos(thispitch)*r);
			break;
		case 1:
			momentum=sin(angle)*(cos(thispitch)*r);
			break;
		case 2:
			momentum=-sin(thispitch)*r;
			break;
		}
		return (momentum*(size+1)/5.0);
	}
}

class Steam1 : Actor
{
	Default
	{
		height 2;
		radius 1;
		+MISSILE
		+NOGRAVITY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		alpha 0.65;
	}
	States
	{
	Spawn:
		STEM ABCDEFGHIJKL 2 A_FadeOut(0.05);
		Stop;
	}
}