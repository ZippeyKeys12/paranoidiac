class HLGonarch : Actor
{
	Default
	{
		Monster;
		//spawnid 174
		Health 5000;
		Mass 0x7FFFFFFF;
		+DONTHARMCLASS
		Speed 8;
		Height 90;
		Radius 55;
		PainChance 10;
		+BOSS
		+NORADIUSDMG
		+MISSILEEVENMORE
		BloodColor "DE FE 72";
		SeeSound "gonarch/sight";
		PainSound "gonarch/pain";
		DeathSound "gonarch/death";
		ActiveSound "gonarch/idle";
		Obituary "%o was melted by the gonarch's acid.";
		HitObituary "%o suffered internal bleedings, courtesy of the gonarch.";
		MeleeRange 80;
		MaxTargetRange 1100;
		Scale 0.8;
	}
	States
	{
	Spawn:
		HLNI A 0 NODELAY A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32);
		HLNI A 3 A_Look;
		Loop;
	See:
		HLNW AABBCC 3
		{
			A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32);
			A_Chase();
		}
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		HLNW DDEEFF 3
		{
			A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32);
			A_Chase();
		}
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		TNT1 A 0 A_Jump(210,"See");
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/sight",5);
		Loop;
	Melee:
		TNT1 A 0 A_Jump(100,"AcidSimple");
		TNT1 A 0 A_Jump(80,"MeleeSightSound");
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/attack",5);
		Goto MeleeAttack;
	MeleeSightSound:
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/sight",4);
	MeleeAttack:
		HLNM AB 5
		{
			A_SpawnItemEx("LegShield5Tics",44,0,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32);
			A_FaceTarget();
		}
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/attswing",6);
		HLNM C 5
		{
			A_SpawnItemEx("LegShield5Tics",44,0,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32);
			A_FaceTarget();
		}
		HLNM D 4
		{
			A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32);
			A_CustomMeleeAttack(random(1,8)*25,"zombie/hit");
		}
		HLNM EF 5
		{
			A_SpawnItemEx("LegShield5Tics",44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,44,0,0,0,0,0,32);
			A_SpawnItemEx("LegShield5Tics",-44,-44,0,0,0,0,0,32);
			A_SpawnItemEx("TopShield5Tics",0,0,60,0,0,0,0,32);
		}
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(145,"AcidSimple");
		Goto SpawnCrab;
	AcidSimple:
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/attack",5);
		TNT1 A 0 A_Jump(80,"AcidHeavy");
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO AA 4 A_FaceTarget;
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO BB 4 A_FaceTarget;
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO BB 4 A_FaceTarget;
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcidExtra",0,0,80,random(2,-2),random(2,-2),random(1,8),0,0);
		HLNO C 8 A_SpawnProjectile("GonarchAcid",80,0,0);
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO D 8;
		Goto See;
	AcidHeavy:
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO AA 4 A_FaceTarget;
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO BB 4 A_FaceTarget;
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO BB 4 A_FaceTarget;
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Extra",0,0,80,random(2,-2),random(2,-2),random(1,8),0,0);
		HLNO C 8 A_SpawnProjectile("GonarchAcid2",80,0,0);
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		HLNO D 8;
		Goto See;
	SpawnCrab:
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/attack",5);
		HLNN ABC 8 A_SpawnItemEx("ShieldSpawner8Tics",0,0,0,0,0,0,0,32);
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner5Tics",0,0,0,0,0,0,0,32);
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/spawnchild",4);
		HLNN DD 2 A_SpawnItemEx("HLBabyHeadCrab2",random(10,-10),random(10,-10),0,0,0,0,32,0);
		HLNN D 1 A_SpawnItemEx("HLBabyHeadCrab2",random(10,-10),random(10,-10),0,0,0,0,32,0);
		HLNN EEEE 8 A_SpawnItemEx("ShieldSpawner5Tics",0,0,0,0,0,0,0,32);
		Goto See;
	Pain:
		TNT1 A 0 A_SpawnItemEx("ShieldSpawner3Tics",0,0,0,0,0,0,0,32);
		HLNI A 4 A_Pain;
		Goto See;
	Death:
		HLND A 3 A_Scream;
		HLND B 3 A_Fall;
		HLND CD 4;
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		HLND EF 4;
		HLND G 100;
		TNT1 A 0 A_PlaySound("halflife/weapons/explode2");
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GeneralExplosion",random(30,-30),random(30,-30),random(10,90),random(1,2),random(1,2),random(1,3),0,0);
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("gargantuagibs",random(30,-30),random(30,-30),random(10,60),random(1,7),random(1,7),random(5,18),random(0,359));
	  	TNT1 AAAAAAAAA 0 A_SpawnItemEx("XenGibs",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		TNT1 A 10;
		Stop;
	}
}

//-------------------------------------------------------------
class GonarchAcid : Actor
{
	Default
	{
		Projectile;
		Radius 12;
		Height 8;
		Speed 24;
		Scale 0.6;
		Damage 25;
		RenderStyle "Add";
		Alpha 0.9;
	//	Translation "80:111=118:127";
		-NOGRAVITY
		Gravity 0.18;
		PoisonDamage 50;
		+THRUGHOST
		+FORCERADIUSDMG
		DamageType "Gonarch";
	}
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 A 4 ThrustThingZ(0,12,0,1);
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 B 4;
		TNT1 A 0 {bTHRUGHOST=0;}
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 C 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 D 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 A 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 B 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 C 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcidExtra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 D 4;
		Goto Spawn+14;
	Death:
		TNT1 A 0 A_Explode(128,128,0);
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit");
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("GonarchAcidExtra",0,0,0,random(2,-2),random(2,-2),random(0,4),0,0);
		Stop;
	}
}

class GonarchAcidExtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Scale .3;
		RenderStyle "Add";
		Alpha .9;
		-NOGRAVITY
		Gravity .2;
	//	Translation "80:111=118:127";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(100,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

//-------------------------------------------------------------
class GonarchAcid2 : GonarchAcid
{
	Default
	{
		Projectile;
		Radius 12;
		Height 8;
		Speed 19;
		Scale 0.9;
		Damage 30;
		RenderStyle "Translucent";
		Alpha 0.8;
		Translation "80:111=152:159";
		DeathSound "";
		-NOGRAVITY
		PoisonDamage 50;
		Gravity 0.18;
		+FORCERADIUSDMG
		+THRUGHOST
	}
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 A 4 ThrustThingZ(0,12,0,1);
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 B 4;
		TNT1 A 0 {bTHRUGHOST=0;}
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 C 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 D 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 A 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 B 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 C 4;
		TNT1 AA 0 A_SpawnItemEx("GonarchAcid2Extra",random(5,-5),random(5,-5),random(5,-5),0,0,0,0,0);
		X022 D 4;
		Goto Spawn+14;
	Death:
		TNT1 A 0 A_Explode(256,256,0);
		TNT1 A 0 A_PlaySound("halflife/monsters/bullsquid/spitehit");
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Extra",0,0,0,random(2,-2),random(2,-2),random(0,4),0,0);
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GonarchAcid2MiniExplosive",0,0,5,random(-3,3),random(-3,3),random(2,7),random(0,359),32);
		Stop;
	}
}

class GonarchAcid2Extra : GonarchAcidExtra
{
	Default
	{
		RenderStyle "Translucent";
		Alpha 0.8;
		Scale 0.3;
		Translation "80:111=152:159";
	}
}
class GonarchAcid2MiniExplosive : Actor
{
	Default
	{
		Projectile;
		Damage 6;
		Speed 10;
		Radius 8;
		Height 12;
		Scale 0.55;
		DamageType "Gonarch";
		RenderStyle "Translucent";
		Alpha 0.8;
		-NOGRAVITY
		Gravity 0.3;
		PoisonDamage 15;
		Translation "80:111=152:159";
	}
	States
	{
	Spawn:
		X022 ABCD 3;
		Loop;
	Death:
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Mini",0,0,0,random(3,-3),random(3,-3),random(1,5),random(0,359),32);
		Stop;
	}
}

class GonarchAcid2Mini : GonarchAcidExtra
{
	Default
	{
		RenderStyle "Translucent";
		Alpha 0.8;
		DamageType "Gonarch";
		PoisonDamage 3;
		-NOCLIP
		Damage 1;
		Scale 0.18;
		Translation "80:111=152:159";
	}
}

//*********************************************
//	For 3 tics use
//*********************************************
class ShieldSpawner3Tics : Actor
{
	Default
	{
		+NOCLIP
	}
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_SpawnItemEx("LegShield",48,48,0,0,0,0,0,32);	//front right
		TNT1 A 0 A_SpawnItemEx("LegShield",48,-48,0,0,0,0,0,32);	//front left
		TNT1 A 0 A_SpawnItemEx("LegShield",-48,48,0,0,0,0,0,32);	//back right
		TNT1 A 0 A_SpawnItemEx("LegShield",-48,-48,0,0,0,0,0,32);	//back left
		TNT1 A 0 A_SpawnItemEx("TopShield",0,0,70,0,0,0,0,32);		//"head" shield
		TNT1 A 1;
		Stop;
	}
}

class LegShield : Actor
{
	Default
	{
		+GHOST
		health 999999;
		radius 14;
		height 90;
		+NOCLIP
		+SHOOTABLE
		+INVULNERABLE
	}
	states
	{
	Spawn:
		TNT1 A 4;
		stop;
	}
}

class TopShield : LegShield
{
	Default
	{
		Radius 64;
		Height 30;
	}
}


//*********************************************
//	For 5 tics use
//*********************************************
class ShieldSpawner5Tics : ShieldSpawner3Tics
{
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",48,48,0,0,0,0,0,32);	//front right
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",48,-48,0,0,0,0,0,32);	//front left
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-48,48,0,0,0,0,0,32);	//back right
		TNT1 A 0 A_SpawnItemEx("LegShield5Tics",-48,-48,0,0,0,0,0,32);	//back left
		TNT1 A 1 A_SpawnItemEx("TopShield5Tics",0,0,68,0,0,0,0,32);		//"head" shield
		Stop;
	}
}


class LegShield5Tics : LegShield
{
	States
	{
	Spawn:
		TNT1 A 6;
		stop;
	}
}

class TopShield5Tics : LegShield5Tics
{
	Default
	{
		Radius 70;
		Height 30;
	}
}

//*********************************************
//	For 8 tics use
//*********************************************
class ShieldSpawner8Tics : ShieldSpawner3Tics
{
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",48,48,0,0,0,0,0,32);	//front right
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",48,-48,0,0,0,0,0,32);	//front left
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",-48,48,0,0,0,0,0,32);	//back right
		TNT1 A 0 A_SpawnItemEx("LegShield8Tics",-48,-48,0,0,0,0,0,32);	//back left
		TNT1 A 0 A_SpawnItemEx("TopShield8Tics",0,0,68,0,0,0,0,32);		//"head" shield
		TNT1 A 1;
		Stop;
	}
}


class LegShield8Tics : LegShield
{
	States
	{
	Spawn:
		TNT1 A 9;
		stop;
	}
}

class TopShield8Tics : LegShield8Tics
{
	Default
	{
		Radius 64;
		Height 30;
	}
}