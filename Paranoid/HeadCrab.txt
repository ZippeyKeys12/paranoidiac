//===========
// Head Crab
//===========

class HLHeadCrab : HLMonster
{
	int drowncounter;
	override bool Slam(Actor victim)
	{
		A_PlaySound("halflife/monsters/headcrab/meleehit",CHAN_BODY);
		return super.slam(victim);
	}
	Default
	{
		Damage 1;
		Health 10;
		Speed 1.6;
		Height 25;
		Radius 8;
		PainChance 200;
		PainChance "Crowbar",256;
		Scale .25;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/headcrab/sight";
		PainSound "halflife/monsters/headcrab/pain";
		DeathSound "halflife/monsters/headcrab/death";
		ActiveSound "halflife/monsters/headcrab/idle";
		maxdropoffheight 384;
		+DONTHARMCLASS;
		DamageFactor "Gonarch",0;
		Species "HLHeadCrab";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 3 A_Look;
		Loop;
	See:
		HLBW A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLBW A 1 A_Chase;
		HLBW AAAA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW B 1 A_Chase;
		HLBW BBBB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW C 1 A_Chase;
		HLBW CCCC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW D 1 A_Chase;
		HLBW DDDD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW E 1 A_Chase;
		HLBW EEEE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW F 1 A_Chase;
		HLBW FFFF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLBW G 1 A_Chase;
		HLBW GGGG 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		TNT1 A 0 A_JumpIf(waterlevel>=2,"Drown");
		HLBI A 4 A_FaceTarget;
		HLBA A 3 A_PlaySound("halflife/monsters/headcrab/jump");
		HLBA B 3;
		HLBA C 3 A_HeadCrabAttack(5,8.75);
		HLBA D 3;
		HLBA E 3 A_CheckFloor(1);
		Wait;
		WLBA F 30 A_Stop;
		WLBA G 30;
		Goto See;
	Pain:
		HLBP A 8 A_Pain;
		Goto See;
	Drown:
		HLBP A 12
		{
			if(drowncounter>=3)
				A_Die();
			else
			{
				A_Pain();
				drowncounter++;
				A_SetTics(random(3,5)*4);
			}
		}
		Goto See;
	Death:
		HLBD A 2 A_Scream;
		HLBD B 2 A_NoBlocking;
		HLBD CDEFGHIJ 3;
		HLBD K 2
		{
			if(waterlevel>=3)
				vel.z+=.75;
		}
		Wait;
	XDeath:
		TNT1 A 0
		{
			A_XenGibs(2);
			for(int i=0;i<4;i++)
				A_SpawnItemEx("XenGibsMini",random(5,-5),random(5,-5),random(10,40),random(1,7),random(1,7),random(5,13),random(0,359));
		}
		Stop;
	}
	void A_HeadCrabAttack(float min,float max)
	{
		A_SkullAttack();
		vel.z+=frandom(min,max);
	}
}

class HLBabyHeadCrab : HLHeadCrab
{
	bool gonarchbaby;
	Default
	{
		Height 8;
		Scale 1;
		-Solid
		RenderStyle "Translucent";
		Alpha .9;
		MaxTargetRange 128;
		maxdropoffheight 24;
		Species "HLBabyHeadCrab";
	}
	States
	{
	Spawn:
		Goto HLMonster::Spawn;
	See:
		HLW1 A 0 A_JumpIf(gonarchbaby,"See2");
		HLW1 A 1 A_Chase;
		HLW1 CEGI 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 K 1 A_Chase;
		HLW1 MOQS 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 U 1 A_Chase;
		HLW1 "WY[]" 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 B 1 A_Chase;
		HLW2 DF 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	See2:
		HLW1 A 1 A_Chase;
		HLW1 CE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 G 1 A_Chase;
		HLW1 IK 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 M 1 A_Chase;
		HLW1 OQ 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 S 1 A_Chase;
		HLW1 UW 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 Y 1 A_Chase;
		HLW1 "[]" 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW2 B 1 A_Chase;
		HLW2 DF 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		HLA1 A 1 A_FaceTarget;
		HLA1 CEGIKMOQS 1;
		HLA1 U 1 A_HeadCrabAttack(2.5,6.25);
		HLA1 "WY[]" 1;
		HLA1 \ 3 A_CheckFloor(1);
		Wait;
		HLA1 ] 60 A_stop;
		Goto See;
	Pain:
		HLP1 ACEGIKMO 1;
		Goto See;
	Death:
		HLD1 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[" 1;
		HLD1 \ 1;
		HLD1 ] -1;
		Stop;
	XDeath:
		TNT1 A 0
		{
			for(int i=0;i<4;i++)
				A_SpawnItemEx("XenGibsMini",random(2,-2),random(2,-2),random(0,20),random(1,4),random(1,4),random(2,6),random(0,359));
		}
		Stop;
	}
}