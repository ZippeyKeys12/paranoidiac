class HLGonarch : HLMonster
{
	Default
	{
		Health 5000;
		Mass 0x7FFFFFFF;
		+DONTHARMCLASS
		Speed 2.666667;
		Height 90;
		Radius 55;
		PainChance 10;
		PainChance "Crowbar",54;
		+BOSS
		+MISSILEEVENMORE
		//-SHOOTABLE
		//+VULNERABLE
		BloodColor "DE FE 72";
		SeeSound "gonarch/sight";
		PainSound "gonarch/pain";
		DeathSound "gonarch/death";
		ActiveSound "gonarch/idle";
		Obituary "%o was melted by the gonarch's acid.";
		HitObituary "%o suffered internal bleedings, courtesy of the gonarch.";
		MeleeRange 80;
		MaxTargetRange 1100;
		Scale .8;
		Species "HLGonarch";
	}
	States
	{
	Spawn:
		HLNI A 3 A_Look;
		Loop;
	See:
		HLNW A 1 A_Chase();
		HLNW AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW A 1 A_Chase();
		HLNW AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW B 1 A_Chase();
		HLNW BB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW B 1 A_Chase();
		HLNW BB 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW C 1 A_Chase();
		HLNW CC 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW C 1 A_Chase();
		HLNW CC 1 A_Chase(null,null,HLSMOOTHCHASE);
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		HLNW D 1 A_Chase();
		HLNW DD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW D 1 A_Chase();
		HLNW DD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW E 1 A_Chase();
		HLNW EE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW E 1 A_Chase();
		HLNW EE 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW F 1 A_Chase();
		HLNW FF 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLNW F 1 A_Chase();
		HLNW FF 1 A_Chase(null,null,HLSMOOTHCHASE);
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/step",6);
		TNT1 A 0 A_Jump(210,"See");
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/sight",5);
		Loop;
	Melee:
		TNT1 A 0 A_Jump(100,"AcidSimple");
		TNT1 A 0
		{
			if(random(0,255)<80)
				A_PlaySound("halflife/monsters/gonarch/sight",4);
			else
				A_PlaySound("halflife/monsters/gonarch/attack",5);
		}
		HLNM AB 5 A_FaceTarget();
		TNT1 A 0 A_PlaySound("halflife/monsters/zombie/attswing",6);
		HLNM C 5 A_FaceTarget();
		HLNM D 4 A_CustomMeleeAttack(random(1,8)*25,"zombie/hit");
		HLNM EF 5;
		Goto See;
	Missile:
		TNT1 A 0 A_Jump(145,"AcidSimple");
		Goto SpawnCrab;
	AcidSimple:
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/attack",5);
		HLNO AABBBB 4 A_FaceTarget;
		HLNO C 8
		{
			class<actor> acid="GonarchAcid",acidextra="GonarchAcidExtra";
			if(random(0,255)<80)
			{
				acid="GonarchAcid2";
				acidextra="GonarchAcid2Extra";
			}
			for(int i=0;i<15;i++)
				A_SpawnItemEx(acidextra,0,0,80,random(2,-2),random(2,-2),random(1,8));
			A_SpawnProjectile(acid,80);
		}
		HLNO D 8;
		Goto See;
	SpawnCrab:
		HLNN A 8 A_PlaySound("halflife/monsters/gonarch/attack",5);
		HLNN BC 8;
		TNT1 A 0 A_PlaySound("halflife/monsters/gonarch/spawnchild",4);
		HLNN DD 2 A_SpawnBabyCrab;
		HLNN D 1 A_SpawnBabyCrab;
		HLNN E 32;
		Goto See;
	Pain:
		HLNI A 4 A_Pain;
		Goto See;
	Death:
		HLND A 3 A_Scream;
		HLND B 3 A_NoBlocking;
		HLND CD 4;
		HLND E 4 A_PlaySound("halflife/monsters/gonarch/step",6);
		HLND F 4;
		HLND G 100;
		TNT1 A 10 A_XenGibsBig;
		Stop;
	}
	void A_SpawnBabyCrab()
	{
		bool num;
		actor baby;
		[num,baby]=A_SpawnItemEx("HLBabyHeadCrab",random(10,-10),random(10,-10),flags:SXF_NOCHECKPOSITION);
		let me=HLBabyHeadCrab(baby);
		me.Speed=10.0/3.0;
		me.gonarchbaby=1;
	}
}

class GonarchAcid : Actor
{
	Default
	{
		Projectile;
		Radius 12;
		Height 8;
		Speed 24;
		Scale .6;
		Damage 25;
		RenderStyle "Add";
		Alpha .9;
		//Translation "80:111=118:127";
		-NOGRAVITY
		Gravity .18;
		PoisonDamage 50;
		+FORCERADIUSDMG
		DamageType "Gonarch";
		DeathSound "halflife/monsters/bullsquid/spitehit";
	}
	States
	{
	Spawn:
		X022 A 0 NODELAY {vel.z+=3;}
		X022 ABCD 4 AcidExtra;
		Goto Spawn+1;
	Death:
		TNT1 A 0 AcidDeath;
		Stop;
	}
	void AcidExtra(class<actor> acid="GonarchAcidExtra")
	{
		for(int i=0;i<2;i++)
			A_SpawnItemEx(acid,random(5,-5),random(5,-5),random(5,-5));
	}
	void AcidDeath(int exp=128,class<actor> acid="GonarchAcidExtra",int cnt=10)
	{
		A_Explode(exp,exp,0);
		for(int i=0;i<cnt;i++)
			A_SpawnItemEx(acid,0,0,0,random(2,-2),random(2,-2),random(0,4));
	}
}

class GonarchAcidExtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Scale .3;
		RenderStyle "Add";
		Alpha .9;
		-NOGRAVITY
		Gravity .2;
		//Translation "80:111=118:127";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(100,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

class GonarchAcid2 : GonarchAcid
{
	Default
	{
		Speed 19;
		Scale .9;
		Damage 30;
		RenderStyle "Translucent";
		Alpha .8;
		Translation "80:111=152:159";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY {vel.z+=3;}
		X022 ABCD 4 AcidExtra("GonarchAcid2Extra");
		Goto Spawn+1;
	Death:
		TNT1 A 0
		{
			AcidDeath(256,"GonarchAcid2Extra",12);
			for(int i=0;i<8;i++)
				A_SpawnItemEx("GonarchAcid2MiniExplosive",0,0,5,random(-3,3),random(-3,3),random(2,7),random(0,359));
		}
		Stop;
	}
}

class GonarchAcid2Extra : GonarchAcidExtra
{
	Default
	{
		RenderStyle "Translucent";
		Alpha .8;
		Scale .3;
		Translation "80:111=152:159";
	}
}

class GonarchAcid2MiniExplosive : GonarchAcid2
{
	Default
	{
		Damage 6;
		Speed 10;
		Radius 8;
		Height 12;
		Scale .55;
		Gravity .3;
		PoisonDamage 15;
		-FORCERADIUSDMG
		DeathSound "";
	}
	States
	{
	Spawn:
		X022 ABCD 3;
		Loop;
	Death:
		TNT1 AAAAAAAA 0 A_SpawnItemEx("GonarchAcid2Mini",0,0,0,random(3,-3),random(3,-3),random(1,5),random(0,359));
		Stop;
	}
}

class GonarchAcid2Mini : GonarchAcid2Extra
{
	Default
	{
		DamageType "Gonarch";
		PoisonDamage 3;
		-NOCLIP
		Damage 1;
		Scale .18;
	}
}